# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Maintainer: Jelle van der Waa <jelle@archlinux.org>

pkgname=archlinux-repro
pkgver=20200921
pkgrel=1
pkgdesc='Tools to reproduce Arch Linux packages'
arch=('any')
license=('MIT')
makedepends=('git' 'asciidoc')
depends=('diffoscope' 'curl' 'gnupg' 'git' 'diffutils' 'zstd')
url="http://github.com/archlinux/archlinux-repro"
source=("https://github.com/archlinux/${pkgname}/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.gz"{,.sig})
validpgpkeys=('C100346676634E80C940FB9E9C02FF419FECBE16')
sha256sums=('816d0d00e9bb647101a44681cc05448fa26c106e1e72ce3e4313417e65ca60cb'
            'SKIP')

build(){
  cd "${pkgname}-${pkgver}"
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make PREFIX=/usr DESTDIR="$pkgdir" install
}
