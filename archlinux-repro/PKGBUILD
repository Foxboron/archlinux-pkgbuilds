# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Maintainer: Jelle van der Waa <jelle@archlinux.org>

pkgname=archlinux-repro
pkgver=20211205
pkgrel=1
pkgdesc='Tools to reproduce Arch Linux packages'
arch=('any')
license=('MIT')
makedepends=('git' 'asciidoc')
depends=('diffoscope' 'curl' 'gnupg' 'git' 'diffutils' 'zstd')
url="http://github.com/archlinux/archlinux-repro"
source=("https://github.com/archlinux/${pkgname}/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.gz"{,.sig})
validpgpkeys=('C100346676634E80C940FB9E9C02FF419FECBE16')
sha256sums=('3d6e40bcbf606fdf0e760321df0af8b6623eca969ead645640b5ae9e45975316'
            'SKIP')

build(){
  cd "${pkgname}-${pkgver}"
  make TAG="${pkgver}"
}

package() {
  cd "${pkgname}-${pkgver}"
  make PREFIX=/usr DESTDIR="$pkgdir" install
}
