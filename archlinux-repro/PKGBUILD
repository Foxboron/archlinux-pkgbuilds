# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Maintainer: Jelle van der Waa <jelle@archlinux.org>

pkgname=archlinux-repro
pkgver=20211221
pkgrel=1
pkgdesc='Tools to reproduce Arch Linux packages'
arch=('any')
license=('MIT')
makedepends=('git' 'asciidoc')
depends=('diffoscope' 'curl' 'gnupg' 'git' 'diffutils' 'zstd')
url="http://github.com/archlinux/archlinux-repro"
source=("https://github.com/archlinux/${pkgname}/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.gz"{,.sig})
validpgpkeys=('C100346676634E80C940FB9E9C02FF419FECBE16')
sha256sums=('c397cd128289ba6786ae82db51dbea97cd52fe118407b931ca1d9f9f68daf041'
            'SKIP')

build(){
  cd "${pkgname}-${pkgver}"
  make TAG="${pkgver}"
}

package() {
  cd "${pkgname}-${pkgver}"
  make PREFIX=/usr DESTDIR="$pkgdir" install
}
