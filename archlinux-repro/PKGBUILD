# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Maintainer: Jelle van der Waa <jelle@archlinux.org>

pkgname=archlinux-repro
pkgver=20200411.1
pkgrel=1
pkgdesc='Tools to reproduce Arch Linux packages'
arch=('any')
license=('MIT')
makedepends=('git' 'asciidoc')
depends=('diffoscope' 'curl' 'gnupg' 'git' 'diffutils')
url="http://github.com/archlinux/archlinux-repro"
source=("https://github.com/archlinux/${pkgname}/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.gz"{,.sig})
validpgpkeys=('C100346676634E80C940FB9E9C02FF419FECBE16')
sha256sums=('9795557ec5444476ac24bf7656951ab6bc9e1d03214a63b5532359114eba7d2e'
            'SKIP')

build(){
  cd "${pkgname}-${pkgver}"
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make PREFIX=/usr DESTDIR="$pkgdir" install
}
