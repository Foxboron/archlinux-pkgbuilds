# Maintainer: Sergej Pupykin <arch+pub@sergej.pp.ru>
# Maintainer: Christian Brauner <christianvanbrauner@gmail.com>
# Maintainer: Morten Linderud <foxboron@archlinux.org>

pkgname=lxcfs
pkgver=5.0.1
pkgrel=2
pkgdesc="FUSE filesystem for LXC"
arch=(x86_64)
url="https://linuxcontainers.org/"
license=('GPL')
depends=('lxc' 'libnih' 'fuse3' 'dbus-glib')
makedepends=('help2man' 'meson' 'python-jinja' 'systemd')
options=('emptydirs' 'debug')
source=("https://linuxcontainers.org/downloads/lxcfs/lxcfs-$pkgver.tar.gz"{,.asc}
        "lxcfs-5.0.1-fix-reinitialization-with-fuse3.patch::https://bugs.archlinux.org/task/75498?getfile=21663"
        "lxcfs.service")
validpgpkeys=('602F567663E593BCBD14F338C638974D64792D67')
sha256sums=('1951b03155000311c7a2df17760554f1bb081015cfd0996304ac243b8e93dc81'
            'SKIP'
            'c2d7b4cd2abeffffb309a6e8277eb201b7f1a663ec6caee939d352dc0f435e59'
            '3549e83ebbd6ec5934be0c3137b0dbb1101cec098bc49d135fea29f733b9e2a1')

prepare() {
    cd "$pkgname-$pkgver"
    patch -p1 <../lxcfs-5.0.1-fix-reinitialization-with-fuse3.patch
}

build() {
    cd "$pkgname-$pkgver"
    arch-meson ../build
    meson compile -C ../build
}

package() {
    cd "$pkgname-$pkgver"
    DESTDIR="$pkgdir" meson install -C ../build
    install -dm0755 "$pkgdir"/var/lib/lxcfs
    install -Dm0644 "$srcdir"/lxcfs.service "$pkgdir"/usr/lib/systemd/system/lxcfs.service
}
