# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: fenuks

pkgbase=python-language-server
pkgname=(python-language-server python2-language-server)
pkgver=0.19.0
pkgrel=2
pkgdesc="An implementation of the Language Server Protocol for Python"
arch=("any")
url="https://github.com/palantir/python-language-server"
license=("MIT")
makedepends=("python" "python-setuptools"
             "python2" "python2-setuptools")
checkdepends=("python-pytest" "python-pytest-runner" "python-pytest-cov"  "python-pyflakes"
              "python-mock" "python-jedi" "python-future" "yapf" "python-mccabe" "python-pycodestyle"
	      "autopep8" "python-rope" "python-pydocstyle"
              "python2-pytest" "python2-pytest-runner" "python2-pytest-cov" "python2-pyflakes"
	      "python2-autopep8" "python2-rope" "python2-mccabe" "python2-pydocstyle"
	      "python2-mock" "python2-jedi" "python2-future" "python2-futures" "python2-yapf" "python2-pycodestyle" "python2-autopep8")
source=("$pkgname-$pkgver.tar.gz::https://github.com/palantir/${pkgname}/archive/${pkgver}.tar.gz")
sha256sums=('5bd07b4c6224622517862dfe704d2015ccfa53fe091e13a40421687dc24a8acc')

prepare() {
  cp -r "$pkgname-$pkgver"{,-py2}
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    python setup.py build

    cd "${srcdir}/${pkgname}-${pkgver}-py2"
    python2 setup.py build
}

check() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    py.test

    cd "${srcdir}/${pkgname}-${pkgver}-py2"
    py.test2
}

package_python-language-server() {
    depends=("python" "python-json-rpc" "python-future" "python-jedi" "python-pluggy")
    optdepends=(
	"python-mccabe: for complexity checking"
        "python-rope: for completions and renaming"
	"python-pyflakes: for linter to detect various errors"
	"python-pycodestyle: for style checking"
	"autopep8: for code formatting"
	"yapf: for code formatting (preferred over autopep8)"
	"python-pydocstyle: for docstring style checking")
    cd "${pkgbase}-${pkgver}"
    python setup.py install --root="${pkgdir}/" --optimize=1 --skip-build
}

package_python2-language-server() {
    depends=("python2" "python2-json-rpc" "python2-future" "python2-jedi" "python2-pluggy")
    optdepends=(
	"python2-mccabe: for complexity checking"
        "python2-rope: for completions and renaming"
	"python2-pyflakes: for linter to detect various errors"
	"python2-pycodestyle: for style checking"
	"python2-autopep8: for code formatting"
	"python2-yapf: for code formatting (preferred over autopep8)"
	"python2-pydocstyle: for docstring style checking")
    cd "${pkgbase}-${pkgver}-py2"
    python2 setup.py install --root="${pkgdir}/" --optimize=1 --skip-build
    mv "$pkgdir/usr/bin/pyls" "$pkgdir/usr/bin/pyls2"
}
