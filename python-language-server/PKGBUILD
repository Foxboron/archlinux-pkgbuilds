# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Maintainer: Maxim Baz <$pkgname at maximbaz dot com>
# Contributor: fenuks

pkgname=python-language-server
pkgver=0.28.3
pkgrel=1
pkgdesc="An implementation of the Language Server Protocol for Python"
arch=("any")
url="https://github.com/palantir/python-language-server"
license=("MIT")
depends=("python" "python-jsonrpc-server" "python-future" "python-jedi" "python-pluggy")
optdepends=("python-mccabe: for complexity checking"
            "python-rope: for completions and renaming"
            "python-pyflakes: for linter to detect various errors"
            "python-pycodestyle: for style checking"
            "python-pylint: for code linting"
            "autopep8: for code formatting"
            "yapf: for code formatting (preferred over autopep8)"
            "python-pydocstyle: for docstring style checking")
makedepends=("python" "python-setuptools")
checkdepends=("python-pytest" "python-pytest-runner" "python-pytest-cov"  "python-pyflakes"
              "python-mock" "python-jedi" "python-future" "yapf" "python-mccabe" "python-pycodestyle"
	          "autopep8" "python-rope" "python-pydocstyle" "python-pylint" "python-jsonrpc-server")
source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        jedi15.patch)
sha256sums=('bf172c9c21fc5c2baa293155ddcb27712905f1ea1ebc0ba90985f7fd13039550'
            'eaf656ed8400e0ebd8a2cc52311be018029f9c46b5dc23c4bb1a0a5067da7356')

build() {
    cd "${pkgname}-${pkgver}"
    patch -Np1 -i "${srcdir}/jedi15.patch"
    python setup.py build
}

# check() {
#     cd "${pkgname}-${pkgver}"
#     py.test
# }

package() {
    cd "${pkgname}-${pkgver}"
    python setup.py install --root="${pkgdir}/" --optimize=1 --skip-build
    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=4 sw=4 et:
