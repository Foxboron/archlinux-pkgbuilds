# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Maintainer: Maxim Baz <$pkgname at maximbaz dot com>
# Contributor: fenuks

pkgname=python-language-server
pkgver=0.26.0
pkgrel=1
pkgdesc="An implementation of the Language Server Protocol for Python"
arch=("any")
url="https://github.com/palantir/python-language-server"
license=("MIT")
depends=("python" "python-jsonrpc-server" "python-future" "python-jedi" "python-pluggy")
optdepends=("python-mccabe: for complexity checking"
            "python-rope: for completions and renaming"
            "python-pyflakes: for linter to detect various errors"
            "python-pycodestyle: for style checking"
            "python-pylint: for code linting"
            "autopep8: for code formatting"
            "yapf: for code formatting (preferred over autopep8)"
            "python-pydocstyle: for docstring style checking")
makedepends=("python" "python-setuptools")
checkdepends=("python-pytest" "python-pytest-runner" "python-pytest-cov"  "python-pyflakes"
              "python-mock" "python-jedi" "python-future" "yapf" "python-mccabe" "python-pycodestyle"
	          "autopep8" "python-rope" "python-pydocstyle" "python-pylint" "python-jsonrpc-server")
source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        ".pylintrc") # https://github.com/palantir/python-language-server/issues/520
sha256sums=('2334f3c0276e14559c127fe89902e19856bc086487fa5f7398d1dc7974b73dba'
            '66f7975240cab2710d6419aa56a2af1371373e102e128cab3f4048c31a7dcf9f')

# https://github.com/palantir/python-language-server/issues/520
prepare() {
    cp .pylintrc "${pkgname}-${pkgver}"
}

build() {
    cd "${pkgname}-${pkgver}"
    python setup.py build
}

check() {
    cd "${pkgname}-${pkgver}"
    py.test
}

package() {
    cd "${pkgname}-${pkgver}"
    python setup.py install --root="${pkgdir}/" --optimize=1 --skip-build
    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=4 sw=4 et:
