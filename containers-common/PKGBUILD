# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=containers-common
pkgver=0.44.4
pkgrel=1
epoch=1
_image_pkgver=5.17.0
_podman_pkgver=3.4.4
_shortnames_pkgver=2022.02.08
_skopeo_pkgver=1.6.0
_storage_pkgver=1.37.0
pkgdesc="Configuration files and manpages for containers"
arch=(any)
url="https://github.com/containers"
license=(Apache)
makedepends=(go-md2man)
checkdepends=(ripgrep)
backup=(
  etc/containers/containers.conf
  etc/containers/mounts.conf
  etc/containers/policy.json
  etc/containers/registries.conf
  etc/containers/seccomp.json
  etc/containers/storage.conf
)
# configuration override directories need to exist
options=(emptydirs)
source=("common-$pkgver.tar.gz::https://github.com/containers/common/archive/v$pkgver.tar.gz"
        "image-$_image_pkgver.tar.gz::https://github.com/containers/image/archive/v$_image_pkgver.tar.gz"
        "podman-$_podman_pkgver.tar.gz::https://github.com/containers/podman/archive/v$_podman_pkgver.tar.gz"
        "skopeo-$_skopeo_pkgver.tar.gz::https://github.com/containers/skopeo/archive/v$_skopeo_pkgver.tar.gz"
        "storage-$_storage_pkgver.tar.gz::https://github.com/containers/storage/archive/v$_storage_pkgver.tar.gz"
        "shortnames-$_shortnames_pkgver.tar.gz::https://github.com/containers/shortnames/archive/refs/tags/v$_shortnames_pkgver.tar.gz"
        'mounts.conf'
)
sha512sums=('875a392c4b5e5e15ff058ccd4b5a9f5f5b0e460a59d9192f08a9092746963237b98eb3a2a3724b845297df4c61c046a106b626142d55a046fd9f11aed3a00aa5'
            '20dabc2a8931c97f94bf53c845e5fe4a6ddc13d255d266e4b6181f9ee6970fae50075ddfedeb0d023344310e68cea91b3d6cecc50c59757c0702e05b9442c294'
            'cfd295bf50ce86ea70741c3e663b409ed47b1e560c962bc579f319151a0fe2b24cdd3045667660083cce89449a9c5de1508c94a9a02375165a72ce7c16166666'
            '13a3660780ce43e31a9840811aaa7626f9d0de9999e52c469a2a58b4bf96bd5e4d67e0f0556ac2baef14c5a662afae1f2d407b260c8833d573326791a2193e48'
            '3a65a50946e1eed1c79ea77b65feb1c6c8cf58a0b7b81932acb8891b6daa25e007582eb7f70ef7e7f2a6d8262be5db7757ead2fe079e47c99114b47917d81084'
            'd0f72ad6f86cc1bcb0f02d9c29d3a982c541679098e417410c8f1a3df42550753e4f491efdec09dc02fe3ab4e3f5d8971c8ab9e964293e6b4e1f1261191b3501'
            '11fa515bbb0686d2b49c4fd2ab35348cb19f9c6780d6eb951a33b07ed7b7c72a676627f36e8c74e1a2d15e306d4537178f0e127fd3490f6131d078e56b46d5e1')
b2sums=('a6cb6881d8cade117a07309bffb83a6015a4105de90c5de0da7a56490132c795af8ad0feac1a53496fb43e6ca895bb0be30bb6188ad3d01a1028daf54a50f759'
        '13a1268f2b7229245b3ecebb6d4b3a410a551ae9dc926e66d224ce8ae537dc74aac14be831797de662dc244eb0a72e476e0ce0c7b79f15a7fe1684ebaf149c5a'
        '3de69c9bc3bd1334837d21cb2817a9e9757fbb561a0b047658c7401608ecf1fbe09d5cd0c65497a82150a67a3775c77705d0dbd314a54f7b3a1953733cfb2906'
        'd2898d8f80a04340e3174179e2397f7b5e66ed50ec8c11919adf1b0620c74a3a69f8be548c5c3bf081735e1a86dc5ae3390d058ef64fc4b73d8f01074ef4a210'
        'ebe5c8e562149a9d007ac87d7fc615d29ad7decbfd6b1b8d919b14684a6341575d8f65080a808ba52674664d187a18bb47e88f10e1a02a2975898862873dadf9'
        'da94f0fe514031d3f02cbc3524fb8ba2ffab1e53949bea009768663f487301c3029a026abff23771ebf73ee94868490cf1758ebee2646c95dc5406111fb12208'
        '2f4b0af3271103362a898e7fcc3ec05f06755902ad664ac3107bb8debb8b2ac0d50de311d5fc651279a817a56e3ff05864a7e77c0d8fc628ff7411bfb98c9b69')

prepare() {
  sed -r 's/(GOMD2MAN = ).*/\1 go-md2man/' -i storage-$_storage_pkgver/docs/Makefile
}

build() {
  (
    cd common-$pkgver/docs
    for _man_page in *.md; do
      go-md2man -in $_man_page -out ${_man_page//.md}
    done
  )
  (
    cd image-$_image_pkgver/docs
    mkdir -vp man5
    mv -v *.5.md man5/
    for _man_page in *.md; do
      go-md2man -in $_man_page -out ${_man_page//.md}.1
    done
    for _man_page in man5/*.md; do
      go-md2man -in $_man_page -out ${_man_page//.md}
    done
  )
  (
    cd podman-$_podman_pkgver
    go-md2man -in pkg/hooks/docs/oci-hooks.5.md -out oci-hooks.5
  )
  (
    cd storage-$_storage_pkgver
    make -C docs
  )
}

check() {
  local _podman_common_ver
  _podman_common_ver="$(rg "github.com/containers/common" podman-$_podman_pkgver/go.mod | cut -d ' ' -f2 | sed 's/v//')"
  _podman_image_ver="$(rg "github.com/containers/image" podman-$_podman_pkgver/go.mod | cut -d ' ' -f2 | sed 's/v//')"
  _podman_storage_ver="$(rg "github.com/containers/storage" podman-$_podman_pkgver/go.mod | cut -d ' ' -f2 | sed 's/v//')"

  printf "Common version in podman: %s\n" $_podman_common_ver
  printf "Image version in podman: %s\n" $_podman_image_ver
  printf "Storage version in podman: %s\n" $_podman_storage_ver

  # NOTE: we are comparing the major.minor version of containers/common required by podman with the one of containers/common that we are trying to build
  # https://github.com/containers/common/issues/923
  if (( $(vercmp "${_podman_common_ver%.*}" "${pkgver%.*}") != 0 )); then
    printf "The podman package in version %s requires containers/common in version %s but we are trying to build version %s\n" $_podman_pkgver ${_podman_common_ver%.*} ${pkgver%.*}
    exit 1
  fi
  if (( $(vercmp "${_podman_image_ver%.*}" "${_image_pkgver%.*}") != 0 )); then
    printf "The podman package in version %s requires containers/image in version %s but we are trying to use version %s\n" $_podman_pkgver ${_podman_image_ver%.*} ${_image_pkgver%.*}
    exit 1
  fi
  if (( $(vercmp "${_podman_storage_ver%.*}" "${_storage_pkgver%.*}") != 0 )); then
    printf "The podman package in version %s requires containers/storage in version %s but we are trying to use version %s\n" $_podman_pkgver ${_podman_storage_ver%.*} ${_storage_pkgver%.*}
    exit 1
  fi
}

package() {
  # directories
  install -vdm 755 "$pkgdir/etc/containers/oci/hooks.d/"
  install -vdm 755 "$pkgdir/etc/containers/registries.conf.d/"
  install -vdm 755 "$pkgdir/usr/share/containers/oci/hooks.d/"
  install -vdm 755 "$pkgdir/var/lib/containers/"

  # configs
  install -vDm 644 mounts.conf -t "$pkgdir/etc/containers/"

  (
    cd "common-$pkgver"
    # configs
    install -vDm 644 pkg/config/containers.conf -t "$pkgdir/etc/containers/"
    install -vDm 644 pkg/config/containers.conf -t "$pkgdir/usr/share/containers/"
    install -vDm 644 pkg/seccomp/seccomp.json -t "$pkgdir/etc/containers/"
    install -vDm 644 pkg/seccomp/seccomp.json -t "$pkgdir/usr/share/containers/"
    # man pages
    install -vDm 644 docs/*.5 -t "$pkgdir/usr/share/man/man5/"
  )
  (
    cd image-$_image_pkgver
    # configs
    install -vDm 644 registries.conf -t "$pkgdir/etc/containers/"
    # man pages
    install -vDm 644 docs/*.1 -t "$pkgdir/usr/share/man/man1/"
    install -vDm 644 docs/man5/*.5 -t "$pkgdir/usr/share/man/man5/"
  )
  (
    cd podman-$_podman_pkgver
    install -vDm 644 *.5 -t "$pkgdir/usr/share/man/man5/"
  )
  (
    cd shortnames-$_shortnames_pkgver
    install -vDm 644 shortnames.conf "$pkgdir/etc/containers/registries.conf.d/00-shortnames.conf"
  )
  (
    cd skopeo-$_skopeo_pkgver
    # configs
    install -vDm 644 default-policy.json "$pkgdir/etc/containers/policy.json"
    install -vDm 644 default.yaml -t "$pkgdir/etc/containers/registries.d/"
  )
  (
    cd storage-$_storage_pkgver
    # configs
    install -vDm 644 storage.conf -t "$pkgdir/etc/containers/"
    install -vDm 644 storage.conf -t "$pkgdir/usr/share/containers/"
    # man pages
    install -vDm 644 docs/*.1 -t "$pkgdir/usr/share/man/man1/"
    install -vDm 644 docs/*.5 -t "$pkgdir/usr/share/man/man5/"
  )
}
