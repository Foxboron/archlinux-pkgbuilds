# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=containers-common
pkgver=0.48.0
pkgrel=1
epoch=1
_image_pkgver=5.21.1
_podman_pkgver=4.1.0
_shortnames_pkgver=2022.02.08
_skopeo_pkgver=1.7.0
_storage_pkgver=1.40.2
pkgdesc="Configuration files and manpages for containers"
arch=(any)
url="https://github.com/containers"
license=(Apache)
depends=('container-network-stack>1')
makedepends=(go-md2man)
checkdepends=(ripgrep)
backup=(
  etc/containers/containers.conf
  etc/containers/mounts.conf
  etc/containers/policy.json
  etc/containers/registries.conf
  etc/containers/seccomp.json
  etc/containers/storage.conf
)
# configuration override directories need to exist
options=(emptydirs)
source=(
  common-$pkgver.tar.gz::https://github.com/containers/common/archive/v$pkgver.tar.gz
  image-$_image_pkgver.tar.gz::https://github.com/containers/image/archive/v$_image_pkgver.tar.gz
  podman-$_podman_pkgver.tar.gz::https://github.com/containers/podman/archive/v$_podman_pkgver.tar.gz
  skopeo-$_skopeo_pkgver.tar.gz::https://github.com/containers/skopeo/archive/v$_skopeo_pkgver.tar.gz
  storage-$_storage_pkgver.tar.gz::https://github.com/containers/storage/archive/v$_storage_pkgver.tar.gz
  shortnames-$_shortnames_pkgver.tar.gz::https://github.com/containers/shortnames/archive/refs/tags/v$_shortnames_pkgver.tar.gz
  mounts.conf
)
sha512sums=('82d823899a39e6d08de5e8880edc5af41165188d9a54346b0ddb0a27e3697af3a4b33e9905fa87ae9e545a0bb5f68fea9a88ae2268e252f0fe20852a41e80057'
            'd4d6d6dc9e1359dbf23165d0f636eeb89246efa89c2b38cb5760e3cec6439234fb708a72e8ec5d07fe8bf9a12ef81b9040c03d70cacff2764c926a3f77d133b0'
            '4d5f240423f21b1224ce44853a2265359dacee0d921f0ea53a7fca34f9076ebfaa49f4327a59ac51f5bf5b9dafb47dda3a9bb8e609237cc69f833b42b95bcd0a'
            '7d17f98e86677150fb2ddba9edae18d8b1ac8f06fba575268e6ba54c60f7b371b5e5a9eab1a4fe19da6c7e85eb79c1eef169ad2a39eff979130ee8d141bd8d6b'
            'f4ff23e26d9103debc1709cbbbc6e715d2b9378a3bd7c2e72393841b2eb499ffce525526aa53bd1f7985cda87962a98f1d0b4601610f7cee8c9c879957575ad2'
            'd0f72ad6f86cc1bcb0f02d9c29d3a982c541679098e417410c8f1a3df42550753e4f491efdec09dc02fe3ab4e3f5d8971c8ab9e964293e6b4e1f1261191b3501'
            '11fa515bbb0686d2b49c4fd2ab35348cb19f9c6780d6eb951a33b07ed7b7c72a676627f36e8c74e1a2d15e306d4537178f0e127fd3490f6131d078e56b46d5e1')
b2sums=('cfb3f20f1a3ee7559e580ae1b7b3cc771852e346ed73d42c5c0458db16f97457e99d5812af3e00dbd1d2a7a7f5e6ea52ee4165a5ad21dedf214258ef5142885c'
        'f5bb2350a3d32e444e06c163ec1afe7a3bb1cc8dd8d382ab5016b5583bc0a6dafe34225f39ad13e3b5f0ead37f090010424e87ebf6402dd4068d7124eac21bf8'
        '6d01a65b13494dc948c0667147eca6f250a81056f1bcf6eda6285566b8aa7aa526794ea96a571aecad642a8b1d3d1877cd31683ccf46662f8681a2afac083b5a'
        '990ad594df9483ad114debeb2882ed3976306d69983ae3d91d807e2654583e7a6599ddf50152df9a937c1c64c1e5a8abb6ed83433036e7efd4ef7e911f99c191'
        '980f714e86bb198687b351876e1bb55a6a115e5aa1c73b1c54f63f57ee75727a3b720322b62300c21de30982b5028c82f6ca94d9457e35f9e88d67de96a2f16f'
        'da94f0fe514031d3f02cbc3524fb8ba2ffab1e53949bea009768663f487301c3029a026abff23771ebf73ee94868490cf1758ebee2646c95dc5406111fb12208'
        '2f4b0af3271103362a898e7fcc3ec05f06755902ad664ac3107bb8debb8b2ac0d50de311d5fc651279a817a56e3ff05864a7e77c0d8fc628ff7411bfb98c9b69')

prepare() {
  sed -r 's/(GOMD2MAN = ).*/\1 go-md2man/' -i storage-$_storage_pkgver/docs/Makefile
}

build() {
  (
    cd common-$pkgver/docs
    for _man_page in *.md; do
      go-md2man -in $_man_page -out ${_man_page//.md}
    done
  )
  (
    cd image-$_image_pkgver/docs
    mkdir -vp man5
    mv -v *.5.md man5/
    for _man_page in *.md; do
      go-md2man -in $_man_page -out ${_man_page//.md}.1
    done
    for _man_page in man5/*.md; do
      go-md2man -in $_man_page -out ${_man_page//.md}
    done
  )
  (
    cd podman-$_podman_pkgver
    go-md2man -in pkg/hooks/docs/oci-hooks.5.md -out oci-hooks.5
  )
  (
    cd storage-$_storage_pkgver
    make -C docs
  )
}

check() {
  local _podman_common_ver
  _podman_common_ver="$(rg "github.com/containers/common" podman-$_podman_pkgver/go.mod | cut -d ' ' -f2 | sed 's/v//')"
  _podman_image_ver="$(rg "github.com/containers/image" podman-$_podman_pkgver/go.mod | cut -d ' ' -f2 | sed 's/v//')"
  _podman_storage_ver="$(rg "github.com/containers/storage" podman-$_podman_pkgver/go.mod | cut -d ' ' -f2 | sed 's/v//')"

  printf "Common version in podman: %s\n" $_podman_common_ver
  printf "Image version in podman: %s\n" $_podman_image_ver
  printf "Storage version in podman: %s\n" $_podman_storage_ver

  # NOTE: we are comparing the major.minor version of containers/common required by podman with the one of containers/common that we are trying to build
  # https://github.com/containers/common/issues/923
  if (( $(vercmp "${_podman_common_ver%.*}" "${pkgver%.*}") != 0 )); then
    printf "The podman package in version %s requires containers/common in version %s but we are trying to build version %s\n" $_podman_pkgver ${_podman_common_ver%.*} ${pkgver%.*}
    exit 1
  fi
  if (( $(vercmp "${_podman_image_ver%.*}" "${_image_pkgver%.*}") != 0 )); then
    printf "The podman package in version %s requires containers/image in version %s but we are trying to use version %s\n" $_podman_pkgver ${_podman_image_ver%.*} ${_image_pkgver%.*}
    exit 1
  fi
  if (( $(vercmp "${_podman_storage_ver%.*}" "${_storage_pkgver%.*}") != 0 )); then
    printf "The podman package in version %s requires containers/storage in version %s but we are trying to use version %s\n" $_podman_pkgver ${_podman_storage_ver%.*} ${_storage_pkgver%.*}
    exit 1
  fi
}

package() {
  # directories
  install -vdm 755 "$pkgdir/etc/containers/oci/hooks.d/"
  install -vdm 755 "$pkgdir/etc/containers/registries.conf.d/"
  install -vdm 755 "$pkgdir/usr/share/containers/oci/hooks.d/"
  install -vdm 755 "$pkgdir/var/lib/containers/"

  # configs
  install -vDm 644 mounts.conf -t "$pkgdir/etc/containers/"

  (
    cd "common-$pkgver"
    # configs
    install -vDm 644 pkg/config/containers.conf -t "$pkgdir/etc/containers/"
    install -vDm 644 pkg/config/containers.conf -t "$pkgdir/usr/share/containers/"
    install -vDm 644 pkg/seccomp/seccomp.json -t "$pkgdir/etc/containers/"
    install -vDm 644 pkg/seccomp/seccomp.json -t "$pkgdir/usr/share/containers/"
    # man pages
    install -vDm 644 docs/*.5 -t "$pkgdir/usr/share/man/man5/"
  )
  (
    cd image-$_image_pkgver
    # configs
    install -vDm 644 registries.conf -t "$pkgdir/etc/containers/"
    # man pages
    install -vDm 644 docs/*.1 -t "$pkgdir/usr/share/man/man1/"
    install -vDm 644 docs/man5/*.5 -t "$pkgdir/usr/share/man/man5/"
  )
  (
    cd podman-$_podman_pkgver
    install -vDm 644 *.5 -t "$pkgdir/usr/share/man/man5/"
  )
  (
    cd shortnames-$_shortnames_pkgver
    install -vDm 644 shortnames.conf "$pkgdir/etc/containers/registries.conf.d/00-shortnames.conf"
  )
  (
    cd skopeo-$_skopeo_pkgver
    # configs
    install -vDm 644 default-policy.json "$pkgdir/etc/containers/policy.json"
    install -vDm 644 default.yaml -t "$pkgdir/etc/containers/registries.d/"
  )
  (
    cd storage-$_storage_pkgver
    # configs
    install -vDm 644 storage.conf -t "$pkgdir/etc/containers/"
    install -vDm 644 storage.conf -t "$pkgdir/usr/share/containers/"
    # man pages
    install -vDm 644 docs/*.1 -t "$pkgdir/usr/share/man/man1/"
    install -vDm 644 docs/*.5 -t "$pkgdir/usr/share/man/man5/"
  )
}
