# Maintainer: Morten Linderud <foxboron@arhlinux.org>
# Contributor: SÃ©bastien "Seblu" Luttringer <seblu@arhlinux.org>

pkgname=go-md2man
pkgver=2.0.1
pkgrel=1
pkgdesc='A markdown to manpage generator'
arch=('x86_64')
url='https://github.com/cpuguy83/go-md2man'
license=('MIT')
makedepends=('go')
source=($pkgname-$pkgver.tar.gz::"https://github.com/cpuguy83/go-md2man/archive/v$pkgver.tar.gz")
sha256sums=('889309ecf43e40d3a76d666b0259dcd71b340ea0fad003784ff3fe0b6c21990e')

build() {
  cd "$pkgname-$pkgver"
  export GOFLAGS="-buildmode=pie -mod=vendor -trimpath"
  export CGO_LDFLAGS="$LDFLAGS"
  go build -o go-md2man .
  ./go-md2man -in=go-md2man.1.md -out=go-md2man.1
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm755 go-md2man "$pkgdir/usr/bin/go-md2man"
  install -Dm755 go-md2man.1 "$pkgdir/usr/share/man/man1/go-md2man.1"
  install -Dm755 LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
