# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>

pkgname=cni-plugins
pkgver=1.0.1
pkgrel=2
pkgdesc='Some standard networking plugins, maintained by the CNI team'
arch=(x86_64)
url="https://github.com/containernetworking/plugins"
license=(Apache)
depends=(glibc)
makedepends=(go)
provides=(container-network-stack)
# unable to build with LTO: https://github.com/containernetworking/plugins/issues/706
options=(!lto)
source=($pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz)
sha512sums=('01edfb3d3c9cf34da7c97a255c9396d49b2b73a11352526d4dd7dfaa0b63e93b09261aa5f68a36f3dcf3d31c0ffd48070717abcd8a65ddb563e3402350f20352')
b2sums=('b77a4612c5ec4dbd18a63dda33cc7d826a302c1f354c56253c768f36ea102fc5a53d15a85ac98d7249a1e03152f400c68c7479700f02476bd55876b5b00f7616')

build() {
  cd plugins-$pkgver
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  ./build_linux.sh
}

package() {
  cd plugins-$pkgver
  install -vDm755 bin/* -t "$pkgdir/usr/lib/cni/"
  # some applications would build their own versions of CNI plugins in
  # /opt/cni/bin if they are not found (as non-symlink files), so we have to
  # install them twice... ;_;
  install -vDm755 bin/* -t "$pkgdir/opt/cni/bin/"
  install -vdm755 "$pkgdir/etc/cni/net.d/"
  install -vDm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm644 plugins/ipam/dhcp/systemd/* -t "$pkgdir/usr/lib/systemd/system/"
}
