# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: David Runge <dvzrv@archlinux.org>

pkgname=cni-plugins
pkgver=1.0.0
pkgrel=1
pkgdesc='Some standard networking plugins, maintained by the CNI team'
arch=(x86_64)
url="https://github.com/containernetworking/plugins"
license=(Apache)
depends=(glibc)
makedepends=(go)
options=('emptydirs')
source=($pkgname-$pkgver.tar.gz::$url/archive/v${pkgver}.tar.gz)
sha512sums=('daf820bc0ce0278e0dd5782f29b5302c880ab6cbbe2dc0b043948ab2c8213e63374914fa85a74edcbc89d1632006a92bf7b1ba004defbd6a6df3dcecfdefdfb3')
b2sums=('fc7aa5de40610ebe9dc821f3043a35018d693f9cc9d0e46899f0feca7e3a2d5e6eb95a9638484074ba56b8b56e41c5b85c0b2b12b63910afd0f349ee8041b734')

build() {
  cd plugins-$pkgver
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  ./build_linux.sh
}

package() {
  cd plugins-$pkgver
  install -vDm755 bin/* -t "$pkgdir/usr/lib/cni/"
  install -vdm755 "${pkgdir}/etc/cni/net.d/"
  install -vDm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -dm755 "$pkgdir/opt/cni/bin"

  install -vDm644 plugins/ipam/dhcp/systemd/* -t "$pkgdir/usr/lib/systemd/system/"

  # Some CNI stuff would build their binaries into /usr/lib/cni with the
  # old approach. This is unfortunate so we'll just symlink each binary
  # instead.
  install -vDm755 bin/* -t "$pkgdir/opt/cni/bin/"
}
