# Maintainer: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: David Runge <dvzrv@archlinux.org>

pkgname=cni-plugins
pkgver=0.9.0
pkgrel=3
pkgdesc='Some standard networking plugins, maintained by the CNI team'
arch=(x86_64)
url="https://github.com/containernetworking/plugins"
license=(Apache)
depends=(glibc)
makedepends=(go)
options=('emptydirs')
source=($pkgname-$pkgver.tar.gz::$url/archive/v${pkgver}.tar.gz)
sha512sums=('8d545d17e6bf4180755708e47607c855b99f6ea4183a33930b7d05974d2151c90873f1e2064b806059a26caba6942502d9954fce697bf000995d539c2208811c')
b2sums=('82f723501901da40e4ea2f111bd1ed3023a1d11e1715902830f3cd0cf0d46d9f9c57389b3a763448fcd52c40aba97a731986ca1862e2ecd912e8a274abf626c9')

build() {
  cd plugins-$pkgver
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  ./build_linux.sh
}

package() {
  cd plugins-$pkgver
  install -vDm 755 bin/* -t "$pkgdir/usr/lib/cni/"
  install -vdm 755 "${pkgdir}/etc/cni/net.d/"
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -dm755 "$pkgdir/opt/cni"
  ln -s /usr/lib/cni "$pkgdir/opt/cni/bin"
}
