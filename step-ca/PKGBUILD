# Maintainer: Morten Linderud <foxboron@archlinux.org>

pkgname=step-ca
pkgver=0.15.8
pkgrel=1
pkgdesc="A private certificate authority (X.509 & SSH) & ACME server for secure automated certificate management, so you can use TLS everywhere & SSO for SSH."
url="https://github.com/smallstep/certificates"
arch=(x86_64)
license=(Apache)
depends=(glibc pcsclite)
makedepends=(go)
source=("$url/releases/download/v${pkgver}/step-certificates_${pkgver}.tar.gz")
sha512sums=('144b07cc013d4936c86055959f39dc160bd761762d171e7b6a3bdf1b80de78e7f7b0700e7bb31dbab513d55ad076a0c1568cfd6d3b9fc800807aebe03049c7a6')

prepare(){
  mkdir -p bin
}

build() {
  BUILD_DATE="$(date --date="@${SOURCE_DATE_EPOCH:-$(date +%s)}" '+%Y-%m-%d %H:%M UTC')"
  go build \
      -trimpath \
      -buildmode=pie \
      -mod=readonly \
      -modcacherw \
      -ldflags "-linkmode external -extldflags \"${LDFLAGS}\" -X \"main.Version=${pkgver}\" -X \"main.BuildTime=${BUILD_DATE}\"" \
      -o bin ./cmd/...
}

check() {
  go test -v ./...
}

package() {
  for bin in step-awskms-init  step-ca  step-cloudkms-init  step-yubikey-init; do
    install -Dm755 "bin/$bin" "$pkgdir/usr/bin/$bin"
  done
}
