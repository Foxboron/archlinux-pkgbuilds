# Maintainer: Morten Linderud <foxboron@archlinux.org>

pkgname=step-ca
pkgver=0.17.3
pkgrel=1
pkgdesc="A private certificate authority (X.509 & SSH) & ACME server for secure automated certificate management, so you can use TLS everywhere & SSO for SSH."
url="https://github.com/smallstep/certificates"
arch=(x86_64)
license=(Apache)
depends=(glibc pcsclite)
makedepends=(go)
source=("$url/releases/download/v${pkgver}/step-ca_${pkgver}.tar.gz")
sha512sums=('df17b7eb5b0c6e99cf924fe34b2c5d6b260c444ad5c9b02cc45a30650ea7de6eb8751de054cef6fdfb46eba8d44012aed4e49f94542fe25bde0aba359a426554')

prepare(){
  mkdir -p bin
}

build() {
  BUILD_DATE="$(date --date="@${SOURCE_DATE_EPOCH:-$(date +%s)}" '+%Y-%m-%d %H:%M UTC')"
  go build \
      -trimpath \
      -buildmode=pie \
      -mod=readonly \
      -modcacherw \
      -ldflags "-linkmode external -extldflags \"${LDFLAGS}\" -X \"main.Version=${pkgver}\" -X \"main.BuildTime=${BUILD_DATE}\"" \
      -o bin ./cmd/...
}

check() {
  go test -v ./...
}

package() {
  for bin in step-awskms-init  step-ca  step-cloudkms-init  step-yubikey-init; do
    install -Dm755 "bin/$bin" "$pkgdir/usr/bin/$bin"
  done
}
