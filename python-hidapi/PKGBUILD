# Maintainer: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: Andy Weidenbaum <archbaum@gmail.com>
# Contributor: Kevin Azzam <arch@kevin.azz.am>

pkgname=python-hidapi
_pipname=hidapi
pkgver=0.7.99.21
_pkgver=${pkgver%.*}.post${pkgver##*.}
pkgrel=4
arch=('x86_64')
pkgdesc="A Cython interface to the hidapi from signal11/hidapi"
url="https://github.com/trezor/cython-hidapi"
depends=('libusb' 'python')
makedepends=('cython' 'python-setuptools' 'udev')
license=('custom')
source=(https://pypi.org/packages/source/${_pipname:0:1}/$_pipname/$_pipname-$_pkgver.tar.gz)
sha512sums=('40efcbbb1d30c1413a5bc764e9bcef8b798f194456b35c83ea4f46c2eacf3eccfca5bb482602892ba8f24ed8085eb6e21e1fb94f7f12a7abb22bd2c571b21008')

build() {
  cd "$_pipname-$_pkgver"

  python setup.py build
}

package_python-hidapi() {
  cd $_pipname-$_pkgver

  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm 755 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
  install -Dm 755 LICENSE-bsd.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE-bsd.txt
  install -Dm 755 LICENSE-gpl3.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE-gpl3.txt
  install -Dm 755 LICENSE-orig.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE-orig.txt
}

