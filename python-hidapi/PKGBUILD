# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: Andy Weidenbaum <archbaum@gmail.com>
# Contributor: Kevin Azzam <arch@kevin.azz.am>

pkgname=python-hidapi
_pipname=hidapi
pkgver=0.9.0.2
_pkgver=${pkgver%.*}.post${pkgver##*.}
pkgrel=4
arch=('x86_64')
pkgdesc="A Cython interface to the hidapi from signal11/hidapi"
url="https://github.com/trezor/cython-hidapi"
depends=('python' 'hidapi')
makedepends=('cython' 'python-setuptools' 'udev')
license=('custom')
source=(https://pypi.org/packages/source/${_pipname:0:1}/$_pipname/$_pipname-$_pkgver.tar.gz)
sha512sums=('9d0264280f978ee62664c5f1fa2dcdf1d02cb7d7ef7b08b3a7ed9768a2b6820bd124f71061fa97fe0a7b61bdf289e764c1db1178a581d9a9c029895f2fc75a30')

build() {
  cd "$_pipname-$_pkgver"

  python setup.py build \
        --without-libusb --with-system-hidapi
}

package_python-hidapi() {
  cd $_pipname-$_pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build \
        --without-libusb --with-system-hidapi

  install -Dm 755 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
  install -Dm 755 LICENSE-bsd.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE-bsd.txt
  install -Dm 755 LICENSE-gpl3.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE-gpl3.txt
  install -Dm 755 LICENSE-orig.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE-orig.txt
}

