# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>

pkgname=bolt
pkgver=0.5
pkgrel=1
pkgdesc="Thunderbolt 3 device manager"
arch=('x86_64')
url="https://gitlab.freedesktop.org/bolt/bolt"
license=('LGPL')
depends=('polkit' 'systemd')
makedepends=('asciidoc' 'meson' 'python-setuptools')
checkdepends=('umockdev')
source=("https://gitlab.freedesktop.org/$pkgname/$pkgname/-/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('f0ed0a6f50fd3a741fbe2b51290daf1cd4c9d4859941df832c5c1f681a418631')

build() {
  cd $pkgname-$pkgver

  install -d ../build
  arch-meson ../build
  ninja -v -C ../build
}

check() {
  cd $pkgname-$pkgver

  ninja -C ../build test
}

package() {
  cd $pkgname-$pkgver

  DESTDIR="${pkgdir}" ninja -C ../build install
# Fixup mode to match polkit
  install -d -o root -g 102 -m 750 "${pkgdir}/usr/share/polkit-1/rules.d"
}
