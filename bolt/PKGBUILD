# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>

pkgname=bolt
pkgver=0.7
pkgrel=2
pkgdesc="Thunderbolt 3 device manager"
arch=('x86_64')
url="https://gitlab.freedesktop.org/bolt/bolt"
license=('LGPL')
depends=('polkit' 'systemd')
makedepends=('asciidoc' 'meson' 'python-setuptools')
checkdepends=('umockdev')
source=(https://gitlab.freedesktop.org/$pkgname/$pkgname/-/archive/$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('46edbd2265a565625d8d236cc33c2c75125925a44960993d254453ae3ff09262')

build() {
  cd $pkgname-$pkgver

  install -d ../build
  arch-meson ../build
  ninja -v -C ../build
}

check() {
  cd $pkgname-$pkgver

  ninja -C ../build test
}

package() {
  cd $pkgname-$pkgver

  DESTDIR="${pkgdir}" ninja -C ../build install
# Fixup mode to match polkit
  install -d -o root -g 102 -m 750 "${pkgdir}/usr/share/polkit-1/rules.d"
# D-Bus policy in /usr/share/dbus-1/system.d
  mv "${pkgdir}"/etc/dbus-1/system.d/ "${pkgdir}"/usr/share/dbus-1/ && rm -r "${pkgdir}"/etc
}
