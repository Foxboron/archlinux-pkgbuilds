# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>

pkgname=bolt
pkgver=0.9.1
pkgrel=1
pkgdesc="Thunderbolt 3 device manager"
arch=('x86_64')
url="https://gitlab.freedesktop.org/bolt/bolt"
license=('LGPL')
depends=('polkit' 'systemd')
makedepends=('asciidoc' 'meson' 'python-setuptools' 'patch')
checkdepends=('umockdev')
source=(https://gitlab.freedesktop.org/$pkgname/$pkgname/-/archive/$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('f938db68df79d0ada135a055fd01204f6f3436e17e962dfb8dc6699de77e1895')

build() {
  cd $pkgname-$pkgver

  arch-meson ../build
  ninja -v -C ../build
}

check() {
  cd $pkgname-$pkgver

  meson test -C ../build --print-errorlogs
}

package() {
  cd $pkgname-$pkgver

  DESTDIR="${pkgdir}" meson install -C ../build
  install -d -o root -g 102 -m 750 "$pkgdir/usr/share/polkit-1/rules.d"
}
