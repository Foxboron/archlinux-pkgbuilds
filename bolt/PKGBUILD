# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>

pkgname=bolt
pkgver=0.8
pkgrel=3
pkgdesc="Thunderbolt 3 device manager"
arch=('x86_64')
url="https://gitlab.freedesktop.org/bolt/bolt"
license=('LGPL')
depends=('polkit' 'systemd')
makedepends=('asciidoc' 'meson' 'python-setuptools' 'patch')
checkdepends=('umockdev')
source=(https://gitlab.freedesktop.org/$pkgname/$pkgname/-/archive/$pkgver/$pkgname-$pkgver.tar.gz
        probing-is-timing-out-fix.diff::https://gitlab.freedesktop.org/bolt/bolt/commit/ea3e3e30eb3ce76f6a0ae816a0f35809872f4edf.diff)
sha256sums=('18dee2a08860cff963d505e382aa5ea9f6a90c0d0279854063839e4a02426e96'
            '8f32244fa7c5c25c441c0d431996cb1cd2696253c66dbb2c98db51d2ade923ee')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i "${srcdir}"/probing-is-timing-out-fix.diff
}

build() {
  cd $pkgname-$pkgver

  arch-meson ../build
  ninja -v -C ../build
}

check() {
  cd $pkgname-$pkgver

  meson test -C ../build --print-errorlogs
}

package() {
  cd $pkgname-$pkgver

  DESTDIR="${pkgdir}" meson install -C ../build
  install -d -o root -g 102 -m 750 "$pkgdir/usr/share/polkit-1/rules.d"
}
