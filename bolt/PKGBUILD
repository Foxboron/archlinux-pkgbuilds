# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>

pkgname=bolt
pkgver=0.9.2
pkgrel=1
pkgdesc="Thunderbolt 3 device manager"
arch=('x86_64')
url="https://gitlab.freedesktop.org/bolt/bolt"
license=('LGPL')
depends=('polkit' 'systemd')
makedepends=('asciidoc' 'meson' 'python-setuptools' 'patch')
checkdepends=('umockdev')
source=(https://gitlab.freedesktop.org/$pkgname/$pkgname/-/archive/$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('9119ad9bbc6406b401b5cae99d2b583006ef6c0e61e48a9d120a7f832b202b4e')

build() {
  cd $pkgname-$pkgver

  arch-meson ../build
  ninja -v -C ../build
}

check() {
  cd $pkgname-$pkgver

  meson test -C ../build --print-errorlogs
}

package() {
  cd $pkgname-$pkgver

  DESTDIR="${pkgdir}" meson install -C ../build
  install -d -o root -g 102 -m 750 "$pkgdir/usr/share/polkit-1/rules.d"
}
