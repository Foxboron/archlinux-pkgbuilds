# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Evgeniy Alekseev <arcanis at archlinux dot org>
# Contributor: Julien Nicoulaud <julien.nicoulaud at gmail dot com>

pkgname=zsh-completions
pkgver=0.33.0
pkgrel=1
pkgdesc="Additional completion definitions for Zsh"
arch=(any)
url="https://github.com/zsh-users/zsh-completions"
license=('BSD')
depends=('zsh')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/zsh-users/${pkgname}/archive/refs/tags/${pkgver}.tar.gz")
sha512sums=('8eaa7d9d2a58e8e2daa6ba7d9a8498e607b271bfd0484184482ec610e7f647ca72ba945f6897b2d619918304a18d048c4b079dae0993dc660cba61d4bce1e0a3')
b2sums=('bcbbb2966a48f1b8c06e14f14432e147b4a4ca28c7d9769b4a108e3d4e005c37874c93c836c8636dabb860780077ccc5d9ebbd339f63c34fd16975ec5486a5c4')
changelog=ChangeLog

package() {
  cd "${pkgname}-${pkgver}"
  install -vDm 644 src/* -t "${pkgdir}/usr/share/zsh/site-functions"
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim:set ts=2 sw=2 et:
