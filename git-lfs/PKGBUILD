# Maintainer: Morten Linderud <foxboron@archlinux.org>

pkgname=git-lfs
pkgver=2.7.2
pkgrel=1
pkgdesc="Git extension for versioning large files"
arch=('x86_64')
url="https://git-lfs.github.com"
license=('MIT')
makedepends=('go-pie' 'ruby-ronn' 'go-tools')
depends=('git')
source=("git+https://github.com/git-lfs/git-lfs.git#tag=v${pkgver}?signed")
#validpgpkeys=('4DB92D1D8CEE7E54F06713452D0C9BC12F82B3A1') # brian m. carlson <sandals@crustytoothpaste.net>
validpgpkeys=('88ACE9B29196305BA9947552F1BA225C0223B187') # brian m. carlson <sandals@crustytoothpaste.net>
sha256sums=('SKIP')

build(){
  cd "$pkgname"
  go build \
    -gcflags "all=-trimpath=${PWD}" \
    -asmflags "all=-trimpath=${PWD}" \
    -ldflags "-extldflags ${LDFLAGS}" \
    .
  make man
}

check(){
  cd "$pkgname"
  make test
}

package() {
  cd "$pkgname"
  install -Dm755 $pkgname "$pkgdir"/usr/bin/$pkgname
  install -Dm644 LICENSE.md "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  install -Dm644 -t "$pkgdir"/usr/share/man/man1 man/*.1
  install -Dm644 -t "$pkgdir"/usr/share/man/man5 man/*.5
}
