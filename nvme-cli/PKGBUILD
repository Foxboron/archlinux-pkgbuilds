# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Devin Cofer <ranguvar[at]ranguvar[dot]io>
# Contributor: DriverX
# Contributor: Nicolas Qui√©not <niQo @ aur>
# Contributor: Martin Schrodt <martin@schrodt.org>

pkgname=nvme-cli
pkgver=1.13
pkgrel=2
pkgdesc="NVM-Express user space tooling for Linux"
arch=('i686' 'x86_64')
url="https://github.com/linux-nvme/nvme-cli"
license=('GPL')
makedepends=('libsystemd')
depends=('util-linux')
install=nvme-cli.install
# checkdepends=('python2-nose' 'python-nose')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linux-nvme/${pkgname}/archive/v${pkgver}.tar.gz"
		"fix-69374.patch::https://github.com/linux-nvme/nvme-cli/commit/a67280f377b23870b5f82ee8482f3fc6f4c594ac.patch")
sha256sums=('03f4875f3e68d326b0280e16df2f027ca6b086cfa148c34e8569df61319b4b85'
            'd90e490525362abc280cc2ad4786ef70b9294ec2c408d4e748bb0c439ce55017')

prepare() {
    cd "${pkgname}-${pkgver}"
    patch -Np1 < "$srcdir/fix-69374.patch"
}

build() {
	cd "${pkgname}-${pkgver}"
	make PREFIX=/usr
}

# Tests require a working NVME disk.
# check() {
# 	cd "${pkgname}-${pkgver}"
# 	make test
# }

package() {
	cd "${pkgname}-${pkgver}"
	make DESTDIR="$pkgdir" UDEVDIR=/usr/lib/udev PREFIX=/usr SBINDIR=/usr/bin install-spec

	# This is done because we don't want the hostnqn and hostid files tracked.
	# they are supposed to be unique
	rm $pkgdir/etc/nvme/host{nqn,id}
}
