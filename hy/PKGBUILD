# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: JP Cimalando <jp-dev@inbox.ru>

pkgname=hy
pkgver=1.0a3
pkgrel=1
pkgdesc="A dialect of Lisp that's embedded in Python"
arch=('any')
url="http://hylang.org/"
license=('MIT')
depends=('python-rply' 'python-astor' 'python-funcparserlib' 'python-colorama')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-pytest-runner')
source=("$pkgname-$pkgver.tar.gz::https://github.com/hylang/hy/archive/$pkgver.tar.gz"
	"fix-linetable.patch::https://github.com/hylang/hy/commit/b04d3ddbf98e6c7b39979ce15f678bfc64f98e56.patch")
sha256sums=('cfc1d554c7859016794e33adf8093129de1c7d8c999a633e2ea7e654d487c555'
            '7642e8306fdb8d8a24cb42923e46732766c8adcd30d6903627b6efaee7d88dfe')

prepare() {
    cd "$pkgname-$pkgver"

    # usually generated from git metadata and aded to PyPI sdist
    # PyPI does not include testsuite files.
    echo "__version__ = '$pkgver'" > hy/version.py
    patch -Np1 < "$srcdir/fix-linetable.patch"
}

build() {
    cd "$pkgname-$pkgver"
    python setup.py build
}

check(){
    cd "$pkgname-$pkgver"

    python setup.py develop --user
    PATH="$HOME/.local/bin:$PATH"
    pytest

    # Hy does magic to the bytecode, but we need this gone from check.
    # this should be generated as part of the build step
    for file in hy/contrib/__pycache__/__init__.cpython-*.pyc \
	hy/contrib/__pycache__/loop.cpython-*.pyc \
	hy/contrib/__pycache__/sequences.cpython-*.pyc \
	hy/contrib/__pycache__/walk.cpython-*.pyc \
	hy/core/__pycache__/__init__.cpython-*.pyc \
	hy/core/__pycache__/language.cpython-*.pyc \
	hy/core/__pycache__/macros.cpython-*.pyc \
	hy/core/__pycache__/shadow.cpython-*.pyc \
	hy/extra/__pycache__/__init__.cpython-*.pyc \
	hy/extra/__pycache__/anaphoric.cpython-*.pyc \
	hy/extra/__pycache__/reserved.cpython-*.pyc
    do
        rm "$file" || true
    done

}

package() {
    cd "$pkgname-$pkgver"
    python setup.py install --root="$pkgdir/" --optimize=1 --skip-build
    install -D -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
    rm -rf "$pkgdir/usr/get_version"
}
