# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: JP Cimalando <jp-dev@inbox.ru>

pkgname=hy
pkgver=0.17.0
pkgrel=2
pkgdesc="A dialect of Lisp that's embedded in Python"
arch=('any')
url="http://hylang.org/"
license=('MIT')
depends=('python-rply' 'python-astor' 'python-clint' 'python-funcparserlib')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-pytest-runner')
source=("$pkgname-$pkgver.tar.gz::https://github.com/hylang/hy/archive/$pkgver.tar.gz"
        python-3.8.patch)
md5sums=('fc719573093f14caba2fade84a8d11e9'
         '5f8c4d26d959168f88faaccc8b4dfb6c')

prepare() {
    cd "$pkgname-$pkgver"

    patch -Np1 -i ../python-3.8.patch

    # usually generated from git metadata and aded to PyPI sdist
    # PyPI does not include testsuite files.
    echo "__version__ = '$pkgver'" > hy/version.py
}

build() {
    cd "$pkgname-$pkgver"
    python setup.py build
}

check(){
    cd "$pkgname-$pkgver"
    python setup.py develop --user
    PATH="$HOME/.local/bin:$PATH"
    python setup.py pytest #--addopts "-k 'not test_bin'"
}

package() {
    cd "$pkgname-$pkgver"
    python setup.py install --root="$pkgdir/" --optimize=1 --skip-build
    install -D -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
    rm -rf "$pkgdir/usr/get_version"
}
