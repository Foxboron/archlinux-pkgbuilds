# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Maikel Wever <maikelwever@gmail.com>

pkgbase=python-pipenv
pkgname=(python-pipenv python2-pipenv)
pkgver=2018.7.1
pkgrel=2
pkgdesc="Sacred Marriage of Pipfile, Pip, & Virtualenv."
url="https://docs.pipenv.org"
arch=('any')
license=('MIT')
makedepends=('python-setuptools'
             'python2-setuptools')
#checkdepends=('python-pytest' 'python-pytest-runner'
#	      'python-pip' 'python-certifi' 'python-virtualenv'
#	      'python-virtualenv-clone' 'python-strict-rfc3339'
#	      'python-pytz' 'python-flaky' 'python-mock'
#	      'python2-pytest' 'python2-pytest-runner')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/pypa/pipenv/archive/v${pkgver}.tar.gz")
sha256sums=('e7428c4a3ac93b8ad0a7819e043191865fc75b93a4a26a7288bf2a12883bca7a')

prepare() {
  cp -a pipenv-$pkgver{,-py2}
}

build(){
  cd "$srcdir/pipenv-$pkgver"
  python setup.py build

  cd "$srcdir/pipenv-$pkgver-py2"
  python2 setup.py build
}

#check(){
#  cd "$srcdir/pipenv-$pkgver"
#  python setup.py pytest
#
#  cd "$srcdir/pipenv-$pkgver-py2"
#  python2 setup.py pytest
#}

package_python2-pipenv() {
  depends=('python2' 'python2-pip' 'python2-pew' 'python2-virtualenv'
  	   'python2-virtualenv-clone' 'python2-ordereddict'
	   'python2-setuptools' 'python2-certifi' 'python2-pyopenssl'
	   'python2-cryptography' 'python2-idna')
  cd "pipenv-$pkgver-py2"
  python2 setup.py install --root="$pkgdir/" --optimize=1 --skip-build
  install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  mv "${pkgdir}/usr/bin/pipenv" "${pkgdir}/usr/bin/pipenv2"
  mv "${pkgdir}/usr/bin/pewtwo" "${pkgdir}/usr/bin/pewtwo2"
  mv "${pkgdir}/usr/bin/pipenv-resolver" "${pkgdir}/usr/bin/pipenv-resolver2"

  SHELL=bash python2 -m pipenv --completion | sed 's/pipenv/pipenv2/g' | install -Dm644 /dev/stdin "${pkgdir}/usr/share/bash-completion/completions/pipenv2"
  SHELL=zsh  python2 -m pipenv --completion | sed 's/pipenv/pipenv2/g' | install -Dm644 /dev/stdin "${pkgdir}/usr/share/zsh/site-functions/_pipenv2"
  SHELL=fish python2 -m pipenv --completion | sed 's/pipenv/pipenv2/g' | install -Dm644 /dev/stdin "${pkgdir}/usr/share/fish/vendor_completions.d/pipenv2.fish"
}

package_python-pipenv() {
  depends=('python' 'python-pip' 'python-pew' 'python-virtualenv'
  	   'python-setuptools' 'python-virtualenv-clone' 'python-certifi')
  cd "pipenv-$pkgver"
  python setup.py install --root="$pkgdir/" --optimize=1 --skip-build
  install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  SHELL=bash python -m pipenv --completion | install -Dm644 /dev/stdin "${pkgdir}/usr/share/bash-completion/completions/pipenv"
  SHELL=zsh  python -m pipenv --completion | install -Dm644 /dev/stdin "${pkgdir}/usr/share/zsh/site-functions/_pipenv"
  SHELL=fish python -m pipenv --completion | install -Dm644 /dev/stdin "${pkgdir}/usr/share/fish/vendor_completions.d/pipenv.fish"
}
