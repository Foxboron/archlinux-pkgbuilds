# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Maintainer: Andrew Crerar <crerar@archlinux.org>
# Contributor: Maikel Wever <maikelwever@gmail.com>

pkgname=python-pipenv
pkgver=2022.4.21
pkgrel=2
pkgdesc="Sacred Marriage of Pipfile, Pip, & Virtualenv."
url="https://pipenv.pypa.io"
arch=('any')
license=('MIT')
depends=('python' 'python-pip' 'python-certifi'
         'python-virtualenv-clone' 'python-virtualenv')
makedepends=('python-setuptools')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/pypa/pipenv/archive/v${pkgver}.tar.gz"
        "requirements_downgrade.patch")
sha512sums=('b79901c65d13a346c6666ff15284453069c58306228ae93c0257b473c2ef8efa2c8611071f1535d15253ce694929412546aba7988829b843b835e7862822a426'
            'a36117df9f9731e0c9393ac02434608191cd760e3e2c8e4d28fef6260c8db69edd7e08cffb321c8792eaa45b8b49cd68475593d11c9d90850cb9940d4472ee6b')

prepare() {
  cd "pipenv-${pkgver}"
  patch -Np1 -i ../requirements_downgrade.patch
}

build() {
  cd "pipenv-${pkgver}"
  python setup.py build
}

package() {
  cd "pipenv-${pkgver}"
  python setup.py install --root="$pkgdir/" --optimize=1 --skip-build
  install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  pushd pipenv
  find . -name \*LICENSE\* -exec install -Dm 644 \{\} "${pkgdir}/usr/share/licenses/${pkgname}/"\{\} \;
  popd

  PIPENV_SHELL=bash python -m pipenv --completion | install -Dm644 /dev/stdin "${pkgdir}/usr/share/bash-completion/completions/pipenv"
  PIPENV_SHELL=zsh  python -m pipenv --completion | install -Dm644 /dev/stdin "${pkgdir}/usr/share/zsh/site-functions/_pipenv"
  PIPENV_SHELL=fish python -m pipenv --completion | install -Dm644 /dev/stdin "${pkgdir}/usr/share/fish/vendor_completions.d/pipenv.fish"
}
