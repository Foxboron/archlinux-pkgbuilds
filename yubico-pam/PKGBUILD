# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=yubico-pam
_shortname=pam_yubico
pkgver=2.26
pkgrel=2
pkgdesc='Yubico YubiKey PAM module'
arch=('x86_64')
url='https://github.com/Yubico/yubico-pam'
license=('BSD')
depends=('pam' 'yubico-c-client' 'yubikey-personalization' 'json-c')
checkdepends=('perl-net-ldap-server')
provides=("${_shortname}")
conflicts=("${_shortname}")
validpgpkeys=('0A3B0262BCA1705307D5FF06BCA00FD4B2168C0A') # Klas Lindfors <klas@yubico.com>
source=("https://developers.yubico.com/${pkgname}/Releases/${_shortname}-${pkgver}.tar.gz"{,.sig}
        '0001-fix_compiler_warning_in_pam_test_c.patch'
        '0002-fix_implicit_declaration_warning_of_mkostemp.patch'
        '0003-tests-add-a-http-1.1-header-to-the-ykval-mock.patch')
sha256sums=('2de96495963fefd72b98243952ca5d5ec513e702c596e54bc667ef6b5e252966'
            'SKIP'
            '151f57fcb7df2661a16f2ef840f805067a84b746b8683177e1a52581e7ffc19b'
            'eacbe45d4580e4987ebb4d8e3f13e5df26be974d43731d811c858234ba4363d0'
            '8773e6d5ca76cb46b6f1e7d37123f8116f6062b5ba3e537a140f20afcb205c31')

prepare() {
	cd "${_shortname}-${pkgver}"

	patch -Np1 < ../0001-fix_compiler_warning_in_pam_test_c.patch
	patch -Np1 < ../0002-fix_implicit_declaration_warning_of_mkostemp.patch
	patch -Np1 < ../0003-tests-add-a-http-1.1-header-to-the-ykval-mock.patch
}

build() {
	cd "${_shortname}-${pkgver}"

	./configure \
		--prefix=/usr
	make
}

check() {
	cd "${_shortname}-${pkgver}"

	make check
}

package() {
	cd "${_shortname}-${pkgver}"

	install -D -m0644 COPYING "${pkgdir}/usr/share/licenses/yubico-pam/COPYING"
	install -D -m0644 README "${pkgdir}/usr/share/doc/yubico-pam/README"
	make DESTDIR="${pkgdir}/" install
}

