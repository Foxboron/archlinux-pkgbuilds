# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=qprint
pkgver=1.1
pkgrel=2
pkgdesc="Encoder and decoder for quoted-printable (RFC1521) encoding"
arch=(x86_64)
url="https://www.fourmilab.ch/webtools/qprint/"
license=('custom:Public Domain')
depends=(glibc)
source=("${url}${pkgname}-${pkgver}.tar.gz")
sha256sums=('ffa9ca1d51c871fb3b56a4bf0165418348cf080f01ff7e59cd04511b9665019c')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr #--mandir=/usr/share/man
  make
}

package() {
  cd ${pkgname}-${pkgver}
  # Dirs are not installed by make install
  install -d "${pkgdir}"/usr/{bin,share/man/man1}
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
