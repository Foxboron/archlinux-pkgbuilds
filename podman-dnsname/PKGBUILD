# Maintainer: Morten Linderud <foxboron@archlinux.org>

pkgname=podman-dnsname
_pkgname=dnsname
pkgver=1.1.1
pkgrel=1
pkgdesc="name resolution for containers"
arch=("x86_64")
url="https://github.com/containers/dnsname"
license=("APACHE")
makedepends=("git" "go")
_commit=c654c95366ac5f309ca3e5727c9b858864247328
source=("git+${url}.git#commit=${_commit}")
validpgpkeys=("B7DBDCA456F7335E91F1C25CD3624C551D0515C4")
sha256sums=('SKIP')


build() {
  cd "${_pkgname}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOFLAGS="-buildmode=pie -ldflags=-linkmode=external -trimpath -mod=readonly -modcacherw"
  make
}

# check() {
#   cd "${_pkgname}"
#   go test ./...
# }

package() {
  cd "${_pkgname}"
  make PREFIX="/usr" LIBEXECDIR="/usr/lib/cni" DESTDIR="${pkgdir}" install
  install -vDm 755 "$pkgdir/usr/lib/cni/dnsname" -t "$pkgdir/opt/cni/bin/"
}
