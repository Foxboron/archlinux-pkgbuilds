# Maintainer: Christian Hesse <mail@eworm.de>
# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Karol Babioch <karol@babioch.de>

pkgname='yubikey-manager'
epoch=1
pkgver=4.0.9
pkgrel=3
pkgdesc='Python library and command line tool for configuring a YubiKey'
arch=('any')
url='https://developers.yubico.com/yubikey-manager/'
license=('BSD')
depends=('python' 'python-click' 'python-cryptography' 'python-pyopenssl' 'python-pyscard'
         'python-fido2' 'ccid' 'yubikey-personalization' 'python-keyring')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
# https://developers.yubico.com/Software_Projects/Software_Signing.html
validpgpkeys=('8D0B4EBA9345254BCEC0E843514F078FF4AB24C3'  # Dag Heyman <dag@yubico.com>
              '57A9DEED4C6D962A923BB691816F3ED99921835E'  # Emil Lundberg (Software Developer) <emil@yubico.com>
              '20EE325B86A81BCBD3E56798F04367096FBA95E8') #  Dain Nilsson <dain@yubico.com>
#source=("https://developers.yubico.com/$pkgname/Releases/yubikey-manager-$pkgver.tar.gz"{,.sig})
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/Yubico/yubikey-manager/releases/download/${pkgver}/yubikey-manager-$pkgver.tar.gz"
	"${pkgname}-${pkgver}.tar.gz.sig::https://github.com/Yubico/yubikey-manager/releases/download/${pkgver}/yubikey-manager-$pkgver.tar.gz.sig")
sha256sums=('384e3a2a66df4ce417aede734eb8c5d0c1b7981290e9bc940c85cc000ef833b4'
            'SKIP')

build() {
	#cd "${pkgname/-/_}-${pkgver}"
	cd "${pkgname}-${pkgver}"
	python setup.py build
}

# check dependency python-makefun is missing...
#check(){
#	#cd "${pkgname/-/_}-${pkgver}"
#	cd "${pkgname}-${pkgver}"
#	python -m unittest discover -vs .
#}

package() {
	#cd "${pkgname/-/_}-${pkgver}"
	cd "${pkgname}-${pkgver}"
	python setup.py install --root="${pkgdir}/" --optimize=1 --skip-build

	install -D -m0644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"

	install -d "${pkgdir}"/usr/share/{bash-completion/completions,zsh/site-functions}
 	PYTHONPATH="$PYTHONPATH:${pkgdir}/usr/" _YKMAN_COMPLETE=bash_source "${pkgdir}"/usr/bin/ykman > "${pkgdir}"/usr/share/bash-completion/completions/ykman || true
 	PYTHONPATH="$PYTHONPATH:${pkgdir}/usr/" _YKMAN_COMPLETE=zsh_source "${pkgdir}"/usr/bin/ykman > "${pkgdir}"/usr/share/zsh/site-functions/_ykman || true
}
