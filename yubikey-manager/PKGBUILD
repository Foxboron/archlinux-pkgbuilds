# Maintainer: Christian Hesse <mail@eworm.de>
# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Karol Babioch <karol@babioch.de>

pkgname='yubikey-manager'
pkgver=3.1.1
pkgrel=2
pkgdesc='Python library and command line tool for configuring a YubiKey'
arch=('any')
url='https://developers.yubico.com/yubikey-manager/'
license=('BSD')
depends=('python' 'python-click' 'python-cryptography' 'python-pyopenssl' 'python-pyscard'
         'python-pyusb' 'python-six' 'python-fido2' 'ccid' 'yubikey-personalization')
makedepends=('python-setuptools')
checkdepends=('python-mock')
validpgpkeys=('8D0B4EBA9345254BCEC0E843514F078FF4AB24C3'  # Dag Heyman <dag@yubico.com>
              '57A9DEED4C6D962A923BB691816F3ED99921835E') # Emil Lundberg (Software Developer) <emil@yubico.com>
source=("https://developers.yubico.com/$pkgname/Releases/yubikey-manager-$pkgver.tar.gz"{,.sig})
sha256sums=('68ef41ac3cd2e891019e755a492427ecdd63d8816525d05f2f32c37b8c440cfa'
            'SKIP')

build() {
	cd "${pkgname}-${pkgver}"

	python setup.py build
}

check(){
	cd "${pkgname}-${pkgver}"

	python setup.py test
}

package() {
	cd "${pkgname}-${pkgver}"

	python setup.py install --root="${pkgdir}/" --optimize=1 --skip-build

	install -D -m0644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"

	install -d "${pkgdir}"/usr/share/{bash-completion/completions,zsh/site-functions}
	PYTHONPATH="$PYTHONPATH:${pkgdir}/usr/" _YKMAN_COMPLETE=source "${pkgdir}"/usr/bin/ykman > "${pkgdir}"/usr/share/bash-completion/completions/ykman || true
	PYTHONPATH="$PYTHONPATH:${pkgdir}/usr/" _YKMAN_COMPLETE=source_zsh "${pkgdir}"/usr/bin/ykman > "${pkgdir}"/usr/share/zsh/site-functions/_ykman || true
}
