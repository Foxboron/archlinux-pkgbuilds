# Maintainer: Andrew Crerar <crerar@archlinux.org>
# Contributor: Kevin Azzam <aur@azz.am>
# Contributor: David Runge <dave@sleepmap.de>

pkgname=python-dotenv
pkgver=0.20.0
pkgrel=1
pkgdesc="Get and set values in your .env file in local and production servers"
arch=('any')
url="https://github.com/theskumar/python-dotenv/"
license=('BSD')
depends=('python-click')
makedepends=('python-setuptools')
optdepends=('ipython: ipython support')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/theskumar/python-dotenv/archive/v${pkgver}.tar.gz")
sha512sums=('3b1b5d3454ad23fdb2c5b5efad96be4bf5cac76431bc06dc3e03d9aa5a9f85c53ac0cfebbfef076f593a59a8c7073b6883668168f2b927630f32b047f31e0a55')

build() {
    cd "${pkgname}-${pkgver}"
    python setup.py build
}

package() {
    cd "${pkgname}-${pkgver}"
    python setup.py install --root="$pkgdir/" --optimize=1 --skip-build
    install -Dm0644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
