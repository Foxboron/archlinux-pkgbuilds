# Maintainer: David Runge <dvzrv@archlinux.org>

_name=QCustomPlot
pkgbase=qcustomplot
pkgname=('qcustomplot' 'qcustomplot-doc')
pkgver=2.1.0
pkgrel=1
pkgdesc="Qt C++ widget for plotting and data visualization"
arch=('x86_64')
url="https://www.qcustomplot.com/"
license=('GPL3')
makedepends=('doxygen' 'graphviz' 'qt5-tools')
source=(
  "${pkgname}-${pkgver}.tar.bz2::https://gitlab.com/DerManu/${_name}/-/archive/v${pkgver}/${_name}-v${pkgver}.tar.bz2"
  "${pkgname}-${pkgver}-source.tar.gz::https://www.qcustomplot.com/release/${pkgver}fixed/${_name}-source.tar.gz"
)
sha512sums=('90dabc3fd6473be59f06dd8ca46d213d12945e1c79c55333e44f3e97ae39b4c359e192d6b52e3d81e90d667702ea6a1dcf49326e5b134d9623a91aa6356b780a'
            '8b209d386393b616cc99e47f99f5a5bcd2c4923e827d948461bd6cfd15e110f2dfa5981abfed35d65a9cf5aa9e09c8820d038c8c10bcfc6585af61a4ba558c2d')
b2sums=('1af3a4833a82a01ff2552aaa0cd2f27c52144f375267747d9621219fc80fb2d638000bdf5797d3e561da670c29eccb9aa039dd605ff42f1447d801b2b1408acc'
        'cdb6b64e53c26b9ea156329c46fb215048f0076784ab301e39a7e42c2191d459339cbf3ea5e24c517aed9a47d5fda68b401221aaeec072bbf97fd2bf72a0861f')

prepare() {
  mv -v "${_name}-v${pkgver}" "${pkgbase}-${pkgver}"
  cd "${pkgbase}-${pkgver}"
  # move required amalgameted sources into place
  mv -v "../${pkgname}-source/"*.{cpp,h} .
}

build() {
  cd "${pkgbase}-${pkgver}"
  qmake-qt5 sharedlib/sharedlib-compilation/sharedlib-compilation.pro
  make
  # create documentation
  doxygen
  qhelpgenerator documentation/html/index.qhp \
    -o documentation/qthelp/qcustomplot.qch
  find documentation/html -type f \
    -iname "*.qhp" -or -iname "*.map" -or -iname "*.md5" -delete
}

package_qcustomplot() {
  depends=('qt5-base')
  provides=('libqcustomplot.so')
  replaces=('qcustomplot-qt5')

  cd "${pkgbase}-${pkgver}"
  # there is no install target
  install -vDm 644 "${pkgname}.h" -t "${pkgdir}/usr/include/"
  install -vdm 755 "${pkgdir}/usr/lib/"
  cp -av "lib${pkgname}.so"* "${pkgdir}/usr/lib/"
  install -vDm 644 changelog.txt -t "${pkgdir}/usr/share/doc/${pkgname}/"
}

package_qcustomplot-doc() {
  pkgdesc+=" (docs)"

  cd "${pkgbase}-${pkgver}"
  install -vDm 644 documentation/qthelp/*.qch -t "${pkgdir}/usr/share/doc/qt/"
  find examples -type f -exec install -vDm 644 {} "${pkgdir}/usr/share/doc/${pkgbase}/"{} \;
  (
    cd documentation
    find html -type f -exec install -vDm 644 {} "${pkgdir}/usr/share/doc/${pkgbase}/"{} \;
  )
}
