# Maintainer: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Maintainer: Morten Linderud <foxboron@archlinux.org>

pkgname=libslirp
pkgver=4.6.0
pkgrel=1
pkgdesc='General purpose TCP-IP emulator'
arch=(x86_64)
url='https://gitlab.freedesktop.org/slirp/libslirp'
license=(BSD MIT)
depends=(glib2)
makedepends=(git meson)
_commit=6ef66db79938d763212320c1b64bc562c23d25a8	#refs/tags/v4.6.0^{}
source=("git+$url#commit=$_commit")
md5sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

build() {
  arch-meson $pkgname build
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
  install -Dm644 $pkgname/COPYRIGHT "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
