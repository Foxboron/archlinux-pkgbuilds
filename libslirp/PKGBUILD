# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: David Runge <dvzrv@archlinux.org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>

pkgname=libslirp
pkgver=4.6.1
pkgrel=1
pkgdesc='General purpose TCP-IP emulator'
arch=(x86_64)
url='https://gitlab.freedesktop.org/slirp/libslirp'
license=(BSD MIT)
depends=(glib2)
makedepends=(meson)
provides=('libslirp.so')
source=("${url}/-/archive/v${pkgver}/libslirp-v${pkgver}.tar.gz")
sha512sums=('04a9dd88cd58c849a24b9cff405d951952760d99ea2bef0b070463dff088d79f44557a13c9427ba0043f58d4b9e06b68ff64a4f23a7b0d66df594e32e1521cae')
b2sums=('7a890912f2a8ce9d6820870e2d29b9c7a2253c403c2bcb127a38e02b2b81d1e262f34571c8a4c05fbf46874b9b2967cb5597cabe169ad54f12702fdd8b90bb3c')

build() {
  arch-meson "$pkgname-v${pkgver}" build
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
  install -vDm 644 "$pkgname-v${pkgver}"/COPYRIGHT "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
