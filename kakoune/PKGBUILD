# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Frank Lenormand <lenormf at gmail dot com>
pkgname=kakoune
pkgver=2018.09.04
pkgrel=2
pkgdesc="Vim-inspired, selection-oriented code editor"
arch=("x86_64")
url="http://kakoune.org/"
license=("custom:unlicense")
depends=("ncurses")
optdepends=(
    "aspell: spell checking support"
    "clang: C/C++ completion and diagnostics support"
    "ranger: filesystem explorer"
    "tmux: splitting and creating windows"
#    "universal-ctags-git: provides \`readtags\` used to jump to a symbol's definition"
    "xdotool: X11 utility to focus arbitrary kakoune clients"
    "xorg-xmessage: display debug messages in a new window"
)
makedepends=("asciidoc")
source=("$pkgname-$pkgver.tar.gz::https://github.com/mawww/kakoune/archive/v${pkgver}.tar.gz")
sha256sums=('6c143cae27cbd692627aeb81c1de8344e8891e47fafb0f67a02cb8ba9ad8de41')

build() {
    cd "${pkgname}-${pkgver}/src"
    make debug=no
}

check() {
    cd "${pkgname}-${pkgver}/src"
    make debug=no test
}

package() {
    cd "${pkgname}-${pkgver}/src"
    make debug=no install-strip DESTDIR="${pkgdir}" PREFIX=/usr
    install -D ../UNLICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
