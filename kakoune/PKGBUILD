# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Frank Lenormand <lenormf at gmail dot com>
pkgname=kakoune
pkgver=2018.10.27
pkgrel=3
pkgdesc="Vim-inspired, selection-oriented code editor"
arch=("x86_64")
url="http://kakoune.org/"
license=("custom:unlicense")
depends=("ncurses")
optdepends=(
    "aspell: spell checking support"
    "clang: C/C++ completion and diagnostics support"
    "ranger: filesystem explorer"
    "tmux: splitting and creating windows"
#    "universal-ctags-git: provides \`readtags\` used to jump to a symbol's definition"
    "xdotool: X11 utility to focus arbitrary kakoune clients"
    "xorg-xmessage: display debug messages in a new window"
)
makedepends=("asciidoc")
source=("$pkgname-$pkgver.tar.bz2::https://github.com/mawww/kakoune/releases/download/v${pkgver}/kakoune-${pkgver}.tar.bz2")
sha256sums=('687a173c8f94fb66aad899e7a3095fe8f08e1fdcab955dbc6785335427cc8a1d')

build() {
    cd "${pkgname}-${pkgver}/src"
    make debug=no
}

check() {
    cd "${pkgname}-${pkgver}/src"
    make debug=no test
}

package() {
    cd "${pkgname}-${pkgver}/src"
    make debug=no install-strip DESTDIR="${pkgdir}" PREFIX=/usr
    install -D ../UNLICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
