# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Nicolas Leclercq <nicolas.private@gmail.com>
# Contributor: Adam S Levy <adam@aslevy.com>
# Contributor: Charles B. Johnson <mail@cbjohnson.info>
# Contributor: Daichi Shinozaki <dsdseg@gmail.com>
# Contributor: Ben Alex <ben.alex@acegi.com.au>

pkgname=influxdb
pkgver=2.0.7
_commit_sha=2a45f0c
pkgrel=1
pkgdesc='Scalable datastore for metrics, events, and real-time analytics'
arch=('x86_64')
url='https://github.com/InfluxData/influxdb'
license=('MIT')
makedepends=('go' 'git' 'bzr' 'protobuf' 'yarn' 'cargo' 'clang' 'llvm')
backup=('etc/influxdb/influxdb.conf')
source=("git+https://github.com/influxdata/influxdb.git#tag=v${pkgver}"
        "influxdb.service")
sha256sums=('SKIP'
            '51444fae4a286d467ddd1cdfec4c54692ad9201f64c5c2af58304c1a7fe504ad')

build(){
  cd "$pkgname"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
  export LDFLAGS="-linkmode external"
  make -j1 VERSION=$pkgver COMMIT=$_commit_sha INFLUXDB_SHA=$_commit_sha
}

check(){
  cd "$pkgname"
  make test
}

package(){
  cd "$pkgname"
  install -Dm644 ../influxdb.service "$pkgdir/usr/lib/systemd/system/influxdb.service"

  install -d "$pkgdir/usr/bin/"
  install -Dm755 bin/linux/influx         "$pkgdir/usr/bin/"
  install -Dm755 bin/linux/influxd        "$pkgdir/usr/bin/"

  install -Dm644 LICENSE                  "$pkgdir/usr/share/licenses/influxdb/LICENSE"
}
