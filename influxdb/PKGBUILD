# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Nicolas Leclercq <nicolas.private@gmail.com>
# Contributor: Adam S Levy <adam@aslevy.com>
# Contributor: Charles B. Johnson <mail@cbjohnson.info>
# Contributor: Daichi Shinozaki <dsdseg@gmail.com>
# Contributor: Ben Alex <ben.alex@acegi.com.au>

pkgname=influxdb
pkgver=2.0.3
_commit_sha=fe04d34
_testdata_ver=2020.07.20.1
pkgrel=1
pkgdesc='Scalable datastore for metrics, events, and real-time analytics'
arch=('x86_64')
url='https://github.com/InfluxData/influxdb'
license=('MIT')
makedepends=('go' 'git' 'bzr' 'protobuf' 'yarn' 'cargo' 'clang' 'llvm')
backup=('etc/influxdb/influxdb.conf')
source=("git+https://github.com/influxdata/influxdb.git#tag=v${pkgver}"
        "${pkgname}-testdata-${_testdata_ver}.tar.gz::https://github.com/influxdata/testdata/archive/${_testdata_ver}.tar.gz"
        "influxdb.sysusers"
        "influxdb.tmpfiles")
sha256sums=('SKIP'
            '3b3e0ba5b86717439d65c5c25a0b753e48ef9afad87bce27f085b4f7934d533c'
            '809cca823ae3027984bc7ace70db75753d7d0d4f6658a36db9d94a33054ca5e9'
            'e2aa59413a6204737383b86cedefd866d7073f56ace5b89aad38c530cc86e60c')

prepare(){
  mv "testdata-${_testdata_ver}" influxdb/testdata
}

build(){
  cd "$pkgname"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
  export LDFLAGS="-linkmode external"
  make -j1 VERSION=$pkgver COMMIT=$_commit_sha INFLUXDB_SHA=$_commit_sha
}

check(){
  cd "$pkgname"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
  export LDFLAGS="-linkmode external"
  make test
}

package(){
  cd "$pkgname"

  install -Dm644 ../influxdb.sysusers "$pkgdir/usr/lib/sysusers.d/influxdb.conf"
  install -Dm644 ../influxdb.tmpfiles "$pkgdir/usr/lib/tmpfiles.d/influxdb.conf"


  install -Dm644 ../influxdb.service "$pkgdir/usr/lib/systemd/system/influxdb.service"

  install -d "$pkgdir/usr/bin/"
  install -Dm755 bin/linux/influx         "$pkgdir/usr/bin/"
  install -Dm755 bin/linux/influxd        "$pkgdir/usr/bin/"

  install -Dm644 LICENSE                  "$pkgdir/usr/share/licenses/influxdb/LICENSE"
}
