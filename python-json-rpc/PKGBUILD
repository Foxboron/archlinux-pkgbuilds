# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Andy Weidenbaum <archbaum@gmail.com>

pkgbase=python-json-rpc
pkgname=(python-json-rpc python2-json-rpc)
pkgver=1.11.1
pkgrel=1
pkgdesc="JSON-RPC transport realisation"
arch=('any')
makedepends=('python' 'python-setuptools'
             'python2' 'python2-setuptools')
checkdepends=('python-nose' 'python-mock' 'python-flask'
              'python2-nose' 'python2-mock' 'python2-flask')
url="https://github.com/pavlov99/json-rpc"
license=('MIT')
source=("$pkgname-$pkgver.tar.gz::https://github.com/pavlov99/json-rpc/archive/$pkgver.tar.gz")
sha256sums=('23a975c2737595e94c86d0cc6c9506086a44a2dacc1d506059dbda7fe6ce2e95')

prepare(){
  cp -r json-rpc-$pkgver{,-py2}
}

build() {
  cd  "$srcdir/json-rpc-$pkgver"
  python setup.py build

  cd  "$srcdir/json-rpc-$pkgver-py2"
  python2 setup.py build
}

check() {
  cd  "$srcdir/json-rpc-$pkgver"
  python setup.py nosetests

  cd  "$srcdir/json-rpc-$pkgver-py2"
  python2 setup.py nosetests
}

package_python-json-rpc() {
  depends=('python')
  cd "json-rpc-$pkgver"

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-json-rpc() {
  depends=('python2')
  cd "json-rpc-$pkgver-py2"

  python2 setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
