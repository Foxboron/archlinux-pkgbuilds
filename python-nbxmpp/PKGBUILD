# Maintainer:
# Contributor: Eric BÃ©langer <eric@archlinux.org>

pkgname=python-nbxmpp
pkgdesc="A Python library to use Jabber/XMPP networks in a non-blocking way"
pkgver=1.0.1
pkgrel=1
arch=(any)
url="https://dev.gajim.org/gajim/python-nbxmpp/"
license=(GPL3)
depends=(libsoup python-gobject python-idna python-precis_i18n)
makedepends=(python-setuptools)
source=($pkgname-$pkgver.tar.gz::https://dev.gajim.org/gajim/python-nbxmpp/repository/nbxmpp-$pkgver/archive.tar.gz)
sha256sums=('f15de5c908e5e6c62582614023beb3c218d55cb3170c3dd24470b49351065795')

prepare() {
  mv $pkgname-nbxmpp-* $pkgname-$pkgver
}

build() {
  cd $pkgname-$pkgver
  python setup.py build
}

package() {
  cd $pkgname-$pkgver
  python setup.py install --root="$pkgdir"
}
