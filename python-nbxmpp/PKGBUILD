# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Eric BÃ©langer <eric@archlinux.org>

pkgname=python-nbxmpp
pkgdesc="A Python library to use Jabber/XMPP networks in a non-blocking way"
pkgver=2.0.2
pkgrel=1
arch=(any)
url="https://dev.gajim.org/gajim/python-nbxmpp/"
license=(GPL3)
depends=(libsoup python-gobject python-idna python-precis_i18n)
makedepends=(python-setuptools)
source=($pkgname-$pkgver.tar.gz::https://dev.gajim.org/gajim/python-nbxmpp/repository/nbxmpp-$pkgver/archive.tar.gz)
sha256sums=('40de72ea33812c95a1c759c8e4a417283ea008d098a2390433545b2b47c167cc')

prepare() {
  mv $pkgname-nbxmpp-* $pkgname-$pkgver
}

build() {
  cd $pkgname-$pkgver
  python setup.py build
}

package() {
  cd $pkgname-$pkgver
  python setup.py install --root="$pkgdir"
}
