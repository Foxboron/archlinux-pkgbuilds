# Maintainer: Morten Linderud <foxboron@archlinux.org> 
# Contributor: Alad Wenter <https://wiki.archlinux.org/index.php/Special:EmailUser/Alad>

pkgbase=font-awesome
pkgname=(ttf-font-awesome otf-font-awesome)
pkgver=6.2.0
pkgrel=1
pkgdesc="Iconic font designed for Bootstrap"
url="https://fontawesome.com/"
license=('custom:OFL')
arch=('any')
source=("$pkgname-$pkgver.tar.gz::https://github.com/FortAwesome/Font-Awesome/archive/$pkgver.tar.gz")
sha256sums=('bebe89d335d55c021c5d3c11212f69849b540950a0eb79e6e3bdcf7e041b98e2')

package_ttf-font-awesome() {
  cd "Font-Awesome-${pkgver}"
  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt"
  install -d "$pkgdir/usr/share/fonts/TTF"
  install -m644 ./webfonts/*.ttf "$pkgdir/usr/share/fonts/TTF"
}

package_otf-font-awesome() {
  cd "Font-Awesome-${pkgver}"
  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt"
  install -d "$pkgdir/usr/share/fonts/OTF"
  install -m644 ./otfs/*.otf "$pkgdir/usr/share/fonts/OTF"
}
