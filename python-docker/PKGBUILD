# Maintainer: Andrew Crerar <crerar@archlinux.org>
# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Hideaki Takahashi <mymelo+aur@gmail.com>
# Contributor: Vincent Demeester <vincent@sbr.io>
# Contributor: Josh VanderLinden <arch@cloudlery.com>

pkgname=python-docker
pkgver=4.0.2
pkgrel=1
pkgdesc="A Python library for the Docker Engine API"
arch=('any')
url="https://github.com/docker/docker-py"
license=("Apache")
depends=('python-requests' 'python-six' 'python-websocket-client' 'python-docker-pycreds' 'python-paramiko')
makedepends=('python-pip' 'python-requests' 'python-six' 'python-websocket-client'
             'python-docker-pycreds')
checkdepends=('python-pytest' 'python-pytest-cov' 'python-pytest-timeout'
              'python-mock' 'python-coverage' 'flake8')
provides=('python-docker-py')
conflicts=('python-docker-py')
replaces=('python-docker-py')
source=("$pkgname-$pkgver.tar.gz::https://github.com/docker/docker-py/archive/$pkgver.tar.gz")
sha512sums=('d58ed82a4b614a1915a814833d2a2a376a439baa58be91b8d1a0402398acb0c45f497bf77f529ecdfbec64705454f0f2be3a807dd912aa975855d1beb8eba1f5')

prepare() {
  sed -i 's/==.*$//' docker-py-"$pkgver"/{,test-}requirements.txt
}

build() {
  cd "$srcdir"/docker-py-"$pkgver"
  python setup.py build
}

check() {
  cd "$srcdir"/docker-py-"$pkgver"
  py.test --cov=docker tests/unit
}

package() {
  cd docker-py-"$pkgver"
  python setup.py install -O1 --root="$pkgdir"
}
