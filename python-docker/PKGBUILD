# Maintainer: Andrew Crerar <crerar@archlinux.org>
# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Hideaki Takahashi <mymelo+aur@gmail.com>
# Contributor: Vincent Demeester <vincent@sbr.io>
# Contributor: Josh VanderLinden <arch@cloudlery.com>

pkgname=python-docker
pkgver=4.2.0
pkgrel=1
pkgdesc="A Python library for the Docker Engine API"
arch=('any')
url="https://github.com/docker/docker-py"
license=("Apache")
depends=('python-requests' 'python-six' 'python-websocket-client' 'python-docker-pycreds' 'python-paramiko')
makedepends=('python-pip' 'python-requests' 'python-six' 'python-websocket-client'
             'python-docker-pycreds')
checkdepends=('python-pytest' 'python-pytest-cov' 'python-pytest-timeout'
              'python-mock' 'python-coverage' 'flake8')
provides=('python-docker-py')
conflicts=('python-docker-py')
replaces=('python-docker-py')
source=("$pkgname-$pkgver.tar.gz::https://github.com/docker/docker-py/archive/$pkgver.tar.gz")
sha512sums=('df5a450355f11d580568ebb0e80abc967df35f2ae10ce83c4ece85e4cf3e97d9cd15e5f14a2d9a43650a844d9402a8d97370f6e5f615c186b12968379e52fa38')

prepare() {
  sed -i 's/==.*$//' docker-py-"$pkgver"/{,test-}requirements.txt
}

build() {
  cd "$srcdir"/docker-py-"$pkgver"
  python setup.py build
}

check() {
  cd "$srcdir"/docker-py-"$pkgver"
  py.test --cov=docker tests/unit
}

package() {
  cd docker-py-"$pkgver"
  python setup.py install -O1 --root="$pkgdir"
}
