# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Morten Linderud <foxboron@archlinux.org>

pkgname=rclone
pkgver=1.55.1
pkgrel=1
pkgdesc="Sync files to and from Google Drive, S3, Swift, Cloudfiles, Dropbox and Google Cloud Storage"
arch=('x86_64')
url="https://rclone.org/"
license=('MIT')
depends=('glibc')
optdepends=('fuse2: for rclone mount')
makedepends=('python' 'pandoc' 'go' 'git' 'fuse2')
source=("$pkgname-$pkgver.tar.gz::https://github.com/rclone/rclone/archive/v$pkgver.tar.gz")
sha512sums=('be8847d062f22ffbd57ae42bf2c36f32a29f5fc652051c7090232530a52e38edad39cc29679a522fcb55df783c304b6dcd59b5b45a528686a11bca67676128eb')

prepare() {
  cd "rclone-$pkgver"
  sed -i "1s/python$/&2/" bin/make_manual.py bin/make_backend_docs.py
}

build() {
  cd "rclone-$pkgver"
  export GOFLAGS="-buildmode=pie -trimpath"
  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  PATH="/build/go/bin:$PATH" make TAG=v$pkgver rclone rclone.1 || true
  PATH="/build/go/bin:$PATH" rclone genautocomplete bash rclone.bash_completion
  PATH="/build/go/bin:$PATH" rclone genautocomplete zsh rclone.zsh_completion
}

check() {
  cd "rclone-$pkgver"
  PATH="/build/go/bin:$PATH" make TAG=v$pkgver test
}

package() {
  cd "rclone-$pkgver"

  install -Dm755 rclone "$pkgdir"/usr/bin/rclone

  install -Dm644 rclone.bash_completion "$pkgdir"/usr/share/bash-completion/completions/rclone
  install -Dm644 rclone.zsh_completion "$pkgdir"/usr/share/zsh/site-functions/_rclone

  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING

  install -Dm644 rclone.1 "$pkgdir"/usr/share/man/man1/rclone.1
  install -d "$pkgdir"/usr/share/doc/$pkgname
  install -t "$pkgdir"/usr/share/doc/$pkgname -m644 MANUAL.html MANUAL.txt
}
