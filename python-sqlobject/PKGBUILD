# Maintainer: Morten Linderud <foxboron@archlinux.org>

pkgbase=python-sqlobject
pkgname=(python-sqlobject python2-sqlobject)
pkgver=3.8.0
pkgrel=1
pkgdesc="SQLObject, an object-relational mapper for Python"
url="http://sqlobject.org/"
arch=('any')
license=('LGPL')
makedepends=('python-setuptools' 'python2-setuptools')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/sqlobject/sqlobject/archive/${pkgver}.tar.gz")
sha256sums=('e148ef33b36f30de8b82fe50b724c83e91906e0f8deefbf524a37c875ab5bcf4')

build(){
  cd "$srcdir/sqlobject-$pkgver"
  python setup.py build
  python2 setup.py build
}

package_python2-sqlobject() {
  depends=('python2' 'python2-formencode' 'python2-pydispatcher')
  cd "sqlobject-$pkgver"
  python2 setup.py install --root="$pkgdir/" --optimize=1 --skip-build
  mv "${pkgdir}/usr/bin/sqlobject-admin" "${pkgdir}/usr/bin/sqlobject-admin2"
  mv "${pkgdir}/usr/bin/sqlobject-convertOldURI" "${pkgdir}/usr/bin/sqlobject-convertOldURI2"
}

package_python-sqlobject() {
  depends=('python' 'python-pydispatcher')
  cd "sqlobject-$pkgver"
  python setup.py install --root="$pkgdir/" --optimize=1 --skip-build
}

