# Maintainer: Morten Linderud <foxboron@archlinux.org>

pkgbase=python-sqlobject
pkgname=(python-sqlobject python2-sqlobject)
pkgver=3.7.0
pkgrel=2
pkgdesc="SQLObject, an object-relational mapper for Python"
url="http://sqlobject.org/"
arch=('any')
license=('LGPL-2.1')
makedepends=('python' 'python-setuptools'
             'python2' 'python2-setuptools')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/sqlobject/sqlobject/archive/${pkgver}.tar.gz")
sha256sums=('7bf45d866d668db5931af245e8012e6e8472b56874c0d096abd603ddb45023d7')

prepare() {
  cp -a sqlobject-$pkgver{,-py2}
}

build(){
  cd "$srcdir/sqlobject-$pkgver"
  python setup.py build

  cd "$srcdir/sqlobject-$pkgver-py2"
  python2 setup.py build
}

package_python2-sqlobject() {
  depends=('python2' 'python2-formencode' 'python2-pydispatcher')
  cd "sqlobject-$pkgver-py2"
  python2 setup.py install --root="$pkgdir/" --optimize=1 --skip-build
  mv "${pkgdir}/usr/bin/sqlobject-admin" "${pkgdir}/usr/bin/sqlobject-admin2"
  mv "${pkgdir}/usr/bin/sqlobject-convertOldURI" "${pkgdir}/usr/bin/sqlobject-convertOldURI2"
}

package_python-sqlobject() {
  depends=('python' 'python-pydispatcher')
  cd "sqlobject-$pkgver"
  python setup.py install --root="$pkgdir/" --optimize=1 --skip-build
}

