# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Fabius
# Contributor: Adria Arrufat <swiftscythe @t gmail d@t com>
# Contributor: Gordin <9ordin @t gmail d@t com>

pkgname=screenkey
pkgver=1.1
pkgrel=1
pkgdesc="A screencast tool to display your keys inspired by Screenflick"
arch=('any')
url="https://www.thregr.org/~wavexx/software/screenkey/"
license=('GPL3')
depends=('python-gobject' 'gtk3' 'python-cairo' 'libx11')
makedepends=('python-setuptools' 'python-distutils-extra')
optdepends=('slop' 'ttf-font-awesome' 'libappindicator-gtk3')
source=("https://gitlab.com/$pkgname/$pkgname/-/archive/v$pkgver/$pkgname-v$pkgver.tar.gz")
sha256sums=('305db12333f45f318152bb808bd032a6becf5233fa1a0ee019c9ae1ed4ca6be0')

build() {
  cd "$srcdir/$pkgname-v$pkgver"
  python setup.py build
}

package() {
  cd "$srcdir/$pkgname-v$pkgver"
  python setup.py install --skip-build --optimize=1 --root="$pkgdir/"
}
