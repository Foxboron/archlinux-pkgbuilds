# Maintainer: Morten Linderud <foxboron@archlinux.org>

pkgname=staticcheck
pkgver=2022.1.3
pkgrel=1
pkgdesc="The advanced Go linter"
arch=('x86_64' 'aarch64')
url="https://staticcheck.io"
license=("MIT")
depends=('glibc')
makedepends=('go' 'git')
source=("$pkgname-$pkgver.tar.gz::https://github.com/dominikh/go-tools/archive/$pkgver.tar.gz")
sha256sums=('b95691c8e7cfff90120249dbd827ee021f23031a498e201713d4dec23deb5974')

prepare(){
	cd "go-tools-$pkgver"
	mkdir -p bin
}

build(){
	cd "go-tools-$pkgver"
	export CGO_LDFLAGS="${LDFLAGS}"
	export CGO_CFLAGS="${CFLAGS}"
	export CGO_CPPFLAGS="${CPPFLAGS}"
	export CGO_CXXFLAGS="${CXXFLAGS}"
	export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw -ldflags=-linkmode=external"
	go build -o bin/staticcheck ./cmd/staticcheck
}

check(){
	cd "go-tools-$pkgver"
	GOROOT="/usr/lib/go" go test -v ./...
}

package(){
	cd "go-tools-$pkgver"
	install -p -Dm755 bin/staticcheck "${pkgdir}/usr/bin/staticcheck"
	install -Dm644 "LICENSE"  "${pkgdir}/usr/share/licenses/${pkgname}/LICENCE"
	install -Dm644 "LICENSE-THIRD-PARTY"  "${pkgdir}/usr/share/licenses/${pkgname}/LICENCE-THIRD-PARTY"
}
