# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor:	Batuhan Baserdem <lastname dot firstname at gmail>

_name="hjson-py"
pkgname="python-${_name}"
pkgver=3.6.5
pkgrel=1
pkgdesc="JSON for Humans, allows comments and is less error prone."
arch=(any)
url="https://github.com/laktak/hjson-py"
license=('MIT')
makedepends=('python-setuptools')
depends=('python')
source=("$pkgname-$pkgver.tar.gz::https://github.com/hjson/hjson-py/archive/v${pkgver}.tar.gz")
md5sums=('2ae51dd7a2d6aab3b4a0489c52a62fbd')

build() {
  cd "${_name}-${pkgver}"
  python setup.py build
}

check(){
  cd "${_name}-${pkgver}"
  PYTHONPATH="$PYTHONPATH:${PWD}" python simplejson/tests/__init__.py
}

package() {
  cd "${_name}-${pkgver}"
  python setup.py install --root="${pkgdir}/" --optimize=1 --skip-build
}
