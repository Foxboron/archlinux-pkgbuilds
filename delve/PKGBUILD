# Maintainer: Morten Linderud <foxboron@archlinux.org>
pkgname=delve
pkgver=1.3.1
pkgrel=1
pkgdesc="A debugger for the Go programming language."
arch=('x86_64')
url="https://github.com/derekparker/delve"
license=('MIT')
makedepends=('go-pie' 'git')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/derekparker/delve/archive/v${pkgver}.tar.gz")
sha256sums=('5308527636459805ac1ee29c9014c62bfc75465892cea26eb50cca1f9922bef6')

build(){
  cd "${pkgname}-${pkgver}"
  go build -gcflags "all=-trimpath=${GOPATH}" -asmflags "all=-trimpath=${GOPATH}" -ldflags "-extldflags $LDFLAGS" ./cmd/dlv
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm755 dlv "${pkgdir}/usr/bin/dlv"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
