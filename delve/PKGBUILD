# Maintainer: Morten Linderud <foxboron@archlinux.org>
pkgname=delve
pkgver=1.4.0
pkgrel=3
pkgdesc="A debugger for the Go programming language."
arch=('x86_64')
url="https://github.com/derekparker/delve"
license=('MIT')
makedepends=('go' 'git')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/derekparker/delve/archive/v${pkgver}.tar.gz")
sha256sums=('56f5e208a6d72e3fb00ba65fbd4255c41904c37169892478011ed24df034667c')

build(){
  cd "${pkgname}-${pkgver}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
  go build ./cmd/dlv
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm755 dlv "${pkgdir}/usr/bin/dlv"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
