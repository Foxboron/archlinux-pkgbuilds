# Maintainer: Morten Linderud <foxboron@archlinux.org>
pkgname=delve
pkgver=1.0.0
pkgrel=2
pkgdesc="A debugger for the Go programming language."
arch=('x86_64')
url="https://github.com/derekparker/delve"
license=('MIT')
makedepends=('go-pie')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/derekparker/delve/archive/v${pkgver}.tar.gz")
sha256sums=('38117c9db41db23a27a1c2e99be17d7fb73d1653de0751ee1262b460a2b26dc4')

prepare(){
  export GOPATH="${srcdir}"
  mkdir -p src
  mv "${pkgname}-${pkgver}"/vendor/* src/
  mkdir -p src/github.com/derekparker
  ln -rTsf "${pkgname}-${pkgver}" src/github.com/derekparker/delve
}

build(){
  cd "${pkgname}-${pkgver}"
  export GOPATH="${srcdir}"
  make build
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm755 dlv "${pkgdir}/usr/bin/dlv"
}
