# Maintainer: Morten Linderud <foxboron@archlinux.org>
pkgname=delve
pkgver=1.4.0
pkgrel=2
pkgdesc="A debugger for the Go programming language."
arch=('x86_64')
url="https://github.com/derekparker/delve"
license=('MIT')
makedepends=('go-pie' 'git')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/derekparker/delve/archive/v${pkgver}.tar.gz")
sha256sums=('56f5e208a6d72e3fb00ba65fbd4255c41904c37169892478011ed24df034667c')

build(){
  cd "${pkgname}-${pkgver}"
  go build -trimpath -ldflags "-extldflags $LDFLAGS" ./cmd/dlv
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm755 dlv "${pkgdir}/usr/bin/dlv"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
