# Maintainer: Morten Linderud <foxboron@archlinux.org>

pkgname=delve
pkgver=1.9.1
pkgrel=2
pkgdesc="A debugger for the Go programming language."
arch=('x86_64')
url="https://github.com/go-delve/delve"
license=('MIT')
makedepends=('go' 'git')
optdepends=('debuginfod: debuginfod support')
options=(!lto strip debug)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/go-delve/delve/archive/v${pkgver}.tar.gz")
sha256sums=('d8d119e74ae47799baa60c08faf2c2872fefce9264b36475ddac8e3a7efc6727')

build(){
  cd "${pkgname}-${pkgver}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -mod=readonly -modcacherw"
  go build -ldflags='-compressdwarf=false -linkmode=external' ./cmd/dlv
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm755 dlv "${pkgdir}/usr/bin/dlv"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
