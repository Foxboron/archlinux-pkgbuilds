# Maintainer: Morten Linderud <foxboron@archlinux.org>

pkgname=delve
pkgver=1.5.1
pkgrel=1
pkgdesc="A debugger for the Go programming language."
arch=('x86_64')
url="https://github.com/go-delve/delve"
license=('MIT')
makedepends=('go' 'git')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/go-delve/delve/archive/v${pkgver}.tar.gz")
sha256sums=('de0e2277d3415ac267064fcb417bfd7f08985575e84dc60ba1f620e122b5507c')

build(){
  cd "${pkgname}-${pkgver}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
  go build ./cmd/dlv
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm755 dlv "${pkgdir}/usr/bin/dlv"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
