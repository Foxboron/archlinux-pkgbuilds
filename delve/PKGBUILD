# Maintainer: Morten Linderud <foxboron@archlinux.org>
pkgname=delve
pkgver=1.2.0
pkgrel=1
pkgdesc="A debugger for the Go programming language."
arch=('x86_64')
url="https://github.com/derekparker/delve"
license=('MIT')
makedepends=('go-pie' 'git')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/derekparker/delve/archive/v${pkgver}.tar.gz")
sha256sums=('eebf6cd9647a2c3987fd38e8dd6e0e968ff41300cab20dc178a789a77b92627d')

build(){
  cd "${pkgname}-${pkgver}"
  go build -gcflags "all=-trimpath=${GOPATH}" -asmflags "all=-trimpath=${GOPATH}" -ldflags "-extldflags $LDFLAGS" ./cmd/dlv
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm755 dlv "${pkgdir}/usr/bin/dlv"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
