# Maintainer: Morten Linderud <foxboron@archlinux.org>

pkgbase=python-pydocstyle
pkgname=(python-pydocstyle python2-pydocstyle)
pkgver=2.1.1
pkgrel=3
pkgdesc="An implementation of the Language Server Protocol for Python"
arch=("any")
url="https://github.com/palantir/python-language-server"
license=("MIT")
makedepends=("python" "python-setuptools"
             "python2" "python2-setuptools")
checkdepends=("python-pytest" "python-pytest-runner" "python-mock" "python-pip"
              "python2-pytest" "python2-pytest-runner" "python2-mock" "python2-pip" "python2-pathlib")
source=("$pkgname-$pkgver.tar.gz::https://github.com/PyCQA/pydocstyle/archive/${pkgver}.tar.gz")
sha256sums=('e83f5644897a623aafb0c3fd9ed11fcdc70c72efff4c3ac4738b12bd8665d12f')

prepare() {
  cp -r "pydocstyle-$pkgver"{,-py2}
}

build() {
    cd "${srcdir}/pydocstyle-${pkgver}"
    python setup.py build

    cd "${srcdir}/pydocstyle-${pkgver}-py2"
    python2 setup.py build
}

check() {
    cd "${srcdir}/pydocstyle-${pkgver}"
    python setup.py pytest --addopts "-k 'not test_integration'"


    cd "${srcdir}/pydocstyle-${pkgver}-py2"
    python2 setup.py pytest --addopts "-k 'not test_integration'"
}

package_python-pydocstyle() {
    depends=("python" "python-snowballstemmer")
    cd "pydocstyle-${pkgver}"
    python setup.py install --root="${pkgdir}/" --optimize=1 --skip-build
    install -Dm644 LICENSE-MIT "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-pydocstyle() {
    depends=("python2" "python2-snowballstemmer" "python2-configparser")
    cd "pydocstyle-${pkgver}-py2"
    python2 setup.py install --root="${pkgdir}/" --optimize=1 --skip-build
    install -Dm644 LICENSE-MIT "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

    mv "$pkgdir/usr/bin/pydocstyle" "$pkgdir/usr/bin/pydocstyle2"
}
