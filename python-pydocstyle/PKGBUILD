# Maintainer: Morten Linderud <foxboron@archlinux.org>

pkgbase=python-pydocstyle
pkgname=(python-pydocstyle python2-pydocstyle)
pkgver=3.0.0
pkgrel=1
pkgdesc="Docstring style checker"
arch=("any")
url="http://www.pydocstyle.org/"
license=("MIT")
makedepends=("python" "python-setuptools"
             "python2" "python2-setuptools")
checkdepends=("python-pytest" "python-pytest-runner" "python-mock" "python-pip" "python-snowballstemmer"
              "python2-pytest" "python2-pytest-runner" "python2-mock" "python2-pip" "python2-pathlib"
              "python2-snowballstemmer")
source=("$pkgname-$pkgver.tar.gz::https://github.com/PyCQA/pydocstyle/archive/${pkgver}.tar.gz")
sha256sums=('99d4c0b584eca25a179766a1e4c7e69284bc94eb9abf6d8e010d69b5a22596ec')

build() {
    cd "${srcdir}/pydocstyle-${pkgver}"
    python setup.py build
    python2 setup.py build
}

check() {
    cd "${srcdir}/pydocstyle-${pkgver}"
    python setup.py pytest --addopts "-k 'not test_integration'"
    python2 setup.py pytest --addopts "-k 'not test_integration'"
}

package_python-pydocstyle() {
    depends=("python" "python-snowballstemmer")
    cd "pydocstyle-${pkgver}"
    python setup.py install --root="${pkgdir}/" --optimize=1 --skip-build
    install -Dm644 LICENSE-MIT "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-pydocstyle() {
    depends=("python2" "python2-snowballstemmer" "python2-configparser")
    cd "pydocstyle-${pkgver}"
    python2 setup.py install --root="${pkgdir}/" --optimize=1 --skip-build
    install -Dm644 LICENSE-MIT "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

    mv "$pkgdir/usr/bin/pydocstyle" "$pkgdir/usr/bin/pydocstyle2"
}
