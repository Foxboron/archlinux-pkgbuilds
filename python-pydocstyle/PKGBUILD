# Maintainer: Morten Linderud <foxboron@archlinux.org>

pkgname=python-pydocstyle
pkgver=6.0.0
pkgrel=1
pkgdesc="Docstring style checker"
arch=("any")
url="http://www.pydocstyle.org/"
license=("MIT")
depends=("python-snowballstemmer")
makedepends=("python-setuptools")
checkdepends=("python-pytest" "python-pytest-runner" "python-mock" "python-pip" "python-snowballstemmer")
source=("$pkgname-$pkgver.tar.gz::https://github.com/PyCQA/pydocstyle/archive/${pkgver}.tar.gz")
sha256sums=('3bc0973500c117ba4266c81e0371d011554f6cc581f9bdbe8eec1b204946bf8a')

build() {
    cd "pydocstyle-${pkgver}"
    python setup.py build
}

check() {
    cd "pydocstyle-${pkgver}"
    python setup.py pytest --addopts "-k 'not test_integration'"
}

package() {
    cd "pydocstyle-${pkgver}"
    export PYTHONHASHSEED=0
    python setup.py install --root="${pkgdir}/" --optimize=1 --skip-build
    install -Dm644 LICENSE-MIT "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
