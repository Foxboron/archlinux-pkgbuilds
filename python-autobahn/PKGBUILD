# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>
# Maintainer: Anatol Pomozov

pkgname=python-autobahn
pkgver=19.11.1
pkgrel=2
pkgdesc='Real-time framework for Web, Mobile & Internet of Things'
arch=(x86_64)
url='https://crossbar.io/autobahn/'
license=(MIT)
depends=(python-cffi python-twisted python-six python-txaio python-wsaccel
         python-setuptools python-cryptography)
makedepends=(python-argon2_cffi python-cbor python-flatbuffers
             python-msgpack python-passlib python-pynacl python-pytrie
             python-ubjson)
checkdepends=(python-mock python-pytest python-pytest-asyncio)
optdepends=(
  'python-cbor: CBOR serializer support'
  'python-flatbuffers: FlatBuffers serializer support'
  'python-msgpack: MsgPack serializer support'
  'python-ubjson: UBJSON serializer support'
  'python-argon2_cffi: WAMP-SCRAM authentication support'
  'python-passlib: WAMP-SCRAM authentication support'
  'python-pynacl: WAMP-cryptosign and WAMP-cryptobox support'
  'python-pytrie: WAMP-cryptobox support'
)

source=(https://files.pythonhosted.org/packages/source/a/autobahn/autobahn-$pkgver.tar.gz)
sha256sums=('201b9879b49c6e259d4126dbafe9e3c73807de0c242d50065fbebc62c6ccb181')

prepare() {
  cd "$srcdir/autobahn-$pkgver"
  # test_depleting checks whether the entropy on the running machine can be depleted or not.
  # Unrelated to autobahn and makes the test suite flaky
  sed -i -r 's/^(\s*)(def test_depleting)/\1@unittest.skip("flaky")\n\1\2/' autobahn/test/test_rng.py
}
build() {
  cd "$srcdir/autobahn-$pkgver"
  AUTOBAHN_USE_NVX=1 python setup.py build
}

check() {
  cd "$srcdir/autobahn-$pkgver"
  pyver=$(python -c "import sys; print('{}.{}'.format(*sys.version_info[:2]))")
  USE_TWISTED=1 PYTHONPATH=.:build/lib.linux-$CARCH-$pyver trial3 autobahn
  USE_ASYNCIO=1 PYTHONPATH=.:build/lib.linux-$CARCH-$pyver pytest -v autobahn
}

package() {
  cd "$srcdir/autobahn-$pkgver"
  AUTOBAHN_USE_NVX=1 python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
