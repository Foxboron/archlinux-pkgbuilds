# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contibutor: Pierre Neidhardt <ambrevar@gmail.com>

pkgbase=python-keyutils
pkgname=(python-keyutils python2-keyutils)
pkgver=0.6
pkgrel=1
pkgdesc="A set of python bindings for keyutils"
arch=("x86_64")
url="https://github.com/sassoftware/python-keyutils"
license=("Apache")
# python-pytest-runner in makedepends due to be set in setup_requires (which is wrong upstream)
makedepends=("python" "python-setuptools" "python-pytest-runner"
	     "python2" "python2-setuptools" "python2-pytest-runner")
checkdepends=("python-pytest"
	      "python2-pytest")
source=("$pkgname-$pkgver.tar.gz::https://github.com/sassoftware/python-keyutils/archive/$pkgver.tar.gz")
sha256sums=('f69e6cadc50525dcb117714e440ee6579b0e5b7f12910b2bb2e910b236a2b18b')

prepare(){
	cp -r "$pkgname-$pkgver"{,-py2}
}

build(){
	cd "$srcdir/$pkgname-$pkgver"
	python setup.py build

	cd "$srcdir/$pkgname-$pkgver-py2"
	python2 setup.py build
}

package_python-keyutils() {
	depends=("python" "keyutils")
	cd "$pkgname-$pkgver"
	python setup.py install --root="$pkgdir" --prefix=/usr --optimize=1 --skip-build
}

package_python2-keyutils() {
	depends=("python2" "keyutils")
	cd "$pkgbase-$pkgver-py2"
	python2 setup.py install --root="$pkgdir" --prefix=/usr --optimize=1 --skip-build
}
