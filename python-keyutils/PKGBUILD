# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contibutor: Pierre Neidhardt <ambrevar@gmail.com>

pkgbase=python-keyutils
pkgname=(python-keyutils python2-keyutils)
pkgver=0.5
pkgrel=3
pkgdesc="A set of python bindings for keyutils"
arch=("x86_64")
url="https://github.com/sassoftware/python-keyutils"
license=("Apache")
makedepends=("python" "python-setuptools"
	     "python2" "python2-setuptools")
checkdepends=("python-pytest" "python-pytest-runner"
	      "python2-pytest" "python2-pytest-runner")
source=("$pkgname-$pkgver.tar.gz::https://github.com/sassoftware/python-keyutils/archive/v$pkgver.tar.gz")
sha256sums=('a1a12891e618f1568d9a873c0990e17ff39abf11ba81b65e5dae3cd7ced63441')

prepare(){
	cp -r "$pkgname-$pkgver"{,-py2}
}

build(){
	cd "$srcdir/$pkgname-$pkgver"
	python setup.py build

	cd "$srcdir/$pkgname-$pkgver-py2"
	python2 setup.py build
}

package_python-keyutils() {
	depends=("python" "keyutils")
	cd "$pkgname-$pkgver"
	python setup.py install --root="$pkgdir" --prefix=/usr --optimize=1 --skip-build
}

package_python2-keyutils() {
	depends=("python2" "keyutils")
	cd "$pkgbase-$pkgver-py2"
	python2 setup.py install --root="$pkgdir" --prefix=/usr --optimize=1 --skip-build
}
