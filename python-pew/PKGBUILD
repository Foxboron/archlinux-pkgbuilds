# $Id$
# Maintainer: Morten Linderud <foxboron@archlinux.org> 
# Contributor: Jos? Luis Lafuente <jl@lafuente.me>
# Contributor: Arthur Vuillard <arthur@hashbang.fr>

pkgbase='python-pew'
pkgname=('python-pew' 'python2-pew')
pkgver=1.1.5
pkgrel=2
pkgdesc="Python Env Wrapper, a set of tools to manage multiple virtual environments"
url="https://github.com/berdario/pew"
arch=('any')
license=('MIT')
makedepends=('python' 'python-setuptools'
             'python2' 'python2-setuptools')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/berdario/pew/archive/$pkgver.zip")
sha256sums=('24b0fe0305a293acf4cc95136e73a313b4a749c5aa5ae2a56b4e384224b2381e')

prepare() {
  cp -a pew-$pkgver{,-py2}
}

build(){
  cd "$srcdir/pew-$pkgver"
  python setup.py build

  cd "$srcdir/pew-$pkgver-py2"
  python2 setup.py build
}

package_python-pew() {
  depends=('python' 'python-virtualenv' 'python-virtualenv-clone')
  cd "${pkgname##*-}-${pkgver}"
  #python3 setup.py install --prefix=/usr --root="$pkgdir"
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build

  install -D -m644 "pew/shell_config/complete.fish" \
    "$pkgdir/usr/share/fish/completions/pew.fish"

  install -D -m644 "pew/shell_config/complete.bash" \
    "$pkgdir/usr/share/bash-completion/completions/pew"

  install -D -m644 "pew/shell_config/complete.zsh" \
    "$pkgdir/usr/share/zsh/site-functions/_pew"

  install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-pew() {
  depends=('python2' 'python2-virtualenv' 'python2-virtualenv-clone' 
           'python2-pathlib' 'python2-backports.shutil_get_terminal_size' 
	   'python2-shutilwhich')
  cd "${pkgname##*-}-${pkgver}-py2"
  python2 setup.py install --root="${pkgdir}" --optimize=1 --skip-build

  mv "${pkgdir}/usr/bin/pew" "${pkgdir}/usr/bin/pew2"

  install -D -m644 "pew/shell_config/complete.fish" \
    "$pkgdir/usr/share/fish/completions/pew2.fish"

  install -D -m644 "pew/shell_config/complete.bash" \
    "$pkgdir/usr/share/bash-completion/completions/pew2"

  install -D -m644 "pew/shell_config/complete.zsh" \
    "$pkgdir/usr/share/zsh/site-functions/_pew2"

  install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

