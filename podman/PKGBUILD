# Maintainer: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>

pkgbase=podman
pkgname=(podman podman-docker)
pkgver=1.6.0
pkgrel=1
pkgdesc='Tool and library for running OCI-based containers in pods'
arch=(x86_64)
url='https://github.com/containers/libpod'
license=(Apache)
depends=(cni-plugins conmon device-mapper iptables libseccomp ostree runc skopeo
         btrfs-progs slirp4netns libsystemd)
makedepends=(go-pie go-md2man git)
source=(libpod-$pkgver.tar.gz::$url/archive/v${pkgver}.tar.gz)
sha256sums=('bc58ae9018be53a477f1cc86596d919c137a4e6774834cc830010d3c27602033')

prepare(){
  cd libpod-$pkgver
  sed -i '/^LDFLAGS_PODMAN ?=/s/$(LDFLAGS)//' Makefile
}

build() {
  export GOPATH="$srcdir"
  export BUILDTAGS='seccomp ostree varlink containers_image_ostree_stub systemd'
  export CGO_LDFLAGS=$LDFLAGS
  export GOFLAGS="-trimpath"

  cd libpod-$pkgver
  make
}

package_podman() {
  optdepends=('podman-docker: for Docker-compatible CLI')

  cd libpod-$pkgver
  make install install.completions install.config DESTDIR="$pkgdir" PREFIX=/usr
}

package_podman-docker() {
  pkgdesc='Emulate Docker CLI using podman'
  depends=(podman)
  conflicts=(docker)

  cd libpod-$pkgver
  make install.docker DESTDIR="$pkgdir" PREFIX=/usr
}
