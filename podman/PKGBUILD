# Maintainer: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Maintainer: Morten Linderud <foxboron@archlinux.org>

pkgbase=podman
pkgname=(podman podman-docker)
pkgver=1.9.1
pkgrel=1
pkgdesc='Tool and library for running OCI-based containers in pods'
arch=(x86_64)
url='https://github.com/containers/libpod'
license=(Apache)
depends=(cni-plugins conmon device-mapper iptables libseccomp runc
         slirp4netns libsystemd fuse-overlayfs skopeo)
makedepends=(go go-md2man git btrfs-progs)
optdepends=('catatonit: --init flag support'
            'crun: support for unified cgroupsv2'
            'btrfs-progs: support btrfs backend devices')
source=("git+$url#tag=v$pkgver?signed")
validpgpkeys=('B7DBDCA456F7335E91F1C25CD3624C551D0515C4')
sha256sums=('SKIP')

pkgver() {
  cd libpod
  git describe --tags | sed 's/^v//;s/-/+/g'
}

build() {
  export BUILDTAGS='seccomp varlink systemd'
  export CGO_CFLAGS="$CFLAGS"
  export CGO_LDFLAGS="$LDFLAGS"
  export GOFLAGS="-buildmode=pie -trimpath"

  cd libpod
  make
}

package_podman() {
  optdepends=('podman-docker: for Docker-compatible CLI')

  cd libpod
  make install install.completions install.config DESTDIR="$pkgdir" PREFIX=/usr
}

package_podman-docker() {
  pkgdesc='Emulate Docker CLI using podman'
  depends=(podman)
  conflicts=(docker)

  cd libpod
  make install.docker DESTDIR="$pkgdir" PREFIX=/usr
}
