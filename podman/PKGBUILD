# Maintainer: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>

pkgbase=podman
pkgname=(podman podman-docker)
pkgver=1.6.3
pkgrel=1
pkgdesc='Tool and library for running OCI-based containers in pods'
arch=(x86_64)
url='https://github.com/containers/libpod'
license=(Apache)
depends=(cni-plugins conmon device-mapper iptables libseccomp ostree runc skopeo
         btrfs-progs slirp4netns libsystemd)
makedepends=(go-pie go-md2man git)
_commit=9d087f6a766259ba53b224944f1b7b778035c370
source=("git+$url#commit=$_commit"
        "fix-makefile.patch::https://github.com/containers/libpod/commit/ca4c24cce1345ab8757cd9a590a0f099207150fa.patch")
sha256sums=('SKIP'
            'b94b7b07320e95e068972d697ae36a027bc48d173e1f52b96f7817e63e77f92f')

pkgver() {
  cd libpod
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd libpod
  sed -i '/^LDFLAGS_PODMAN ?=/s/$(LDFLAGS)//' Makefile
  patch -Np1 < "../fix-makefile.patch"
}

build() {
  export GOPATH="$srcdir"
  export BUILDTAGS='seccomp ostree varlink containers_image_ostree_stub systemd'
  export CGO_LDFLAGS=$LDFLAGS
  export GOFLAGS="-trimpath"

  cd libpod
  make
}

package_podman() {
  optdepends=('podman-docker: for Docker-compatible CLI')

  cd libpod
  make install install.completions install.config DESTDIR="$pkgdir" PREFIX=/usr
}

package_podman-docker() {
  pkgdesc='Emulate Docker CLI using podman'
  depends=(podman)
  conflicts=(docker)

  cd libpod
  make install.docker DESTDIR="$pkgdir" PREFIX=/usr
}
