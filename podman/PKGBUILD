# Maintainer: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>

pkgbase=podman
pkgname=(podman podman-docker)
pkgver=1.7.0
pkgrel=1
pkgdesc='Tool and library for running OCI-based containers in pods'
arch=(x86_64)
url='https://github.com/containers/libpod'
license=(Apache)
depends=(cni-plugins conmon device-mapper iptables libseccomp runc skopeo
         btrfs-progs slirp4netns libsystemd)
makedepends=(go-pie go-md2man git)
optdepends=('catatonit: --init flag support')
source=("git+$url#tag=v$pkgver?signed"
        "fix-makefile.patch")
validpgpkeys=('B7DBDCA456F7335E91F1C25CD3624C551D0515C4')
sha256sums=('SKIP'
            '7dc9b1bb6b134196ca5125ba40c1f6d82c260a42bc929d9fb6142a5ac61a1c6a')

prepare() {
  cd libpod
  patch -Np1 < "../fix-makefile.patch"
}

pkgver() {
  cd libpod
  git describe --tags | sed 's/^v//;s/-/+/g'
}

build() {
  export BUILDTAGS='seccomp varlink  systemd'
  export CGO_LDFLAGS=$LDFLAGS
  export GOFLAGS="-trimpath"

  cd libpod
  make
}

package_podman() {
  optdepends=('podman-docker: for Docker-compatible CLI')

  cd libpod
  make install install.completions install.config DESTDIR="$pkgdir" PREFIX=/usr
}

package_podman-docker() {
  pkgdesc='Emulate Docker CLI using podman'
  depends=(podman)
  conflicts=(docker)

  cd libpod
  make install.docker DESTDIR="$pkgdir" PREFIX=/usr
}
