# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: David Runge <dvzrv@archlinux.org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>

pkgbase=podman
pkgname=(podman podman-docker)
pkgver=3.1.0
pkgrel=1
pkgdesc='Tool and library for running OCI-based containers in pods'
arch=(x86_64)
url='https://github.com/containers/libpod'
license=(Apache)
depends=(cni-plugins conmon containers-common device-mapper iptables libseccomp
runc slirp4netns libsystemd fuse-overlayfs)
makedepends=(btrfs-progs go go-md2man git gpgme systemd)
source=("git+$url#tag=v$pkgver?signed")
validpgpkeys=('B7DBDCA456F7335E91F1C25CD3624C551D0515C4')
sha256sums=('SKIP')

pkgver() {
  cd libpod
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd libpod

  # From
  # Makefile: Fix make install.docker regression
  # https://github.com/containers/podman/pull/9898

  # Makefile: ensure install.docker creates BINDIR
  git cherry-pick -n ab3b6df130f5ce21b126a666f5df3bd85aaf4c46

  # Makefile: introduce install.docker-full
  git cherry-pick -n 7801072851315cd1409d5d82a5e649659aa06b79
}

build() {
  export BUILDTAGS='seccomp systemd'
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath"

  cd libpod
  make EXTRA_LDFLAGS='-s -w -linkmode=external'
}

package_podman() {
  depends+=('libgpgme.so')
  optdepends+=('podman-docker: for Docker-compatible CLI'
              'btrfs-progs: support btrfs backend devices'
              'catatonit: --init flag support'
              'crun: support for unified cgroupsv2')

  cd libpod
  make install install.completions DESTDIR="$pkgdir" PREFIX=/usr
  # remove man pages provided by containers-common
  rm -rvf "${pkgdir}/usr/share/man/man5"
}

package_podman-docker() {
  pkgdesc='Emulate Docker CLI using podman'
  depends=(podman)
  conflicts=(docker)
  provides=(docker)

  cd libpod
  make -j1 install.docker-full DESTDIR="$pkgdir" PREFIX=/usr
}
