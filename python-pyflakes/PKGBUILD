# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Karol 'Kenji Takahashi' Wo≈∫niak <kenji.sx>
# Contributor: Tianjiao Yin <ytj000+AUR@gmail.com>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: TDY <tdy@gmx.com>
# Contributor: Tiago Pierezan Camargo <tcamargo@gmail.com>

pkgname=python-pyflakes
pkgver=2.4.0
pkgrel=3
pkgdesc='A lint-like tool for Python to identify common errors quickly without executing code'
arch=('any')
url='https://pypi.python.org/pypi/pyflakes'
license=('MIT')
depends=('python-setuptools')
makedepends=('python-setuptools')
source=("https://pypi.io/packages/source/p/pyflakes/pyflakes-$pkgver.tar.gz")
sha512sums=('f4c6512eb811511c897623f52c4f88e50275a3292582d7dd34462e90e39fecce939818cb92e750eebdd66eab25b91c23540104fc4530c42621d7cfeb1d33c577')

build() {
  cd "$srcdir"/pyflakes-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/pyflakes-$pkgver
  python setup.py test
}

package() {
  cd pyflakes-$pkgver
  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1

  # We have python 3 as default python, and want to keep compatibility with the old pyflakes3k naming
  ln -s pyflakes "$pkgdir/usr/bin/pyflakes3k"

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
