# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: pandada8 <pandada8@gmail.com>

pkgname=v2ray
pkgver=4.27.5
pkgrel=1
pkgdesc="A platform for building proxies to bypass network restrictions"
arch=('x86_64')
url="https://github.com/v2fly/v2ray-core"
license=('MIT')
depends=('glibc' 'v2ray-domain-list-community' 'v2ray-geoip')
makedepends=('go' 'git')
backup=(etc/v2ray/config.json)
source=("$pkgname-$pkgver.tar.gz::https://github.com/v2fly/v2ray-core/archive/v$pkgver.tar.gz")
sha512sums=('f0c93458d091980d8daef95a3809c3fa05fcc23e713f1190a90d5708852d3c0998a6c20f4aa6fe56a0bdf6fcde61a595869679e6803be030d8a750a46d7508f2')

build() {
  cd "v2ray-core-$pkgver"
  export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external"
  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  go build -o v2ray ./main
  go build -o v2ctl ./infra/control/main
}

check() {
  cd "v2ray-core-$pkgver"
  go test -p 1 -tags json -v -timeout 30m v2ray.com/core/...
}

package() {
  cd v2ray-core-$pkgver
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/v2ray/LICENSE
  install -Dm644 release/config/systemd/system/v2ray.service "$pkgdir"/usr/lib/systemd/system/v2ray.service
  sed -i -e 's|/usr/bin/v2ray/v2ray|/usr/lib/v2ray/v2ray|' \
         "$pkgdir"/usr/lib/systemd/system/v2ray.service
  install -Dm644 release/config/systemd/system/v2ray@.service "$pkgdir"/usr/lib/systemd/system/v2ray@.service
  sed -i -e 's|/usr/bin/v2ray/v2ray|/usr/lib/v2ray/v2ray|' \
         "$pkgdir"/usr/lib/systemd/system/v2ray@.service
  install -Dm644 release/config/*.json -t "$pkgdir"/etc/v2ray/
  install -Dm755 v2ray -t "$pkgdir"/usr/lib/v2ray/
  install -Dm755 v2ctl -t "$pkgdir"/usr/lib/v2ray/
  install -dm755 "$pkgdir"/usr/bin
  ln -s ../lib/v2ray/v2ray "$pkgdir"/usr/bin/v2ray
  ln -s ../lib/v2ray/v2ctl "$pkgdir"/usr/bin/v2ctl
}
