# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: pandada8 <pandada8@gmail.com>

pkgname=v2ray
pkgver=4.23.4
pkgrel=1
pkgdesc="A platform for building proxies to bypass network restrictions"
arch=('x86_64')
url="https://github.com/v2ray/v2ray-core"
license=('MIT')
depends=('glibc' 'v2ray-domain-list-community' 'v2ray-geoip')
makedepends=('go' 'git')
backup=(etc/v2ray/config.json)
source=("$pkgname-$pkgver.tar.gz::https://github.com/v2ray/v2ray-core/archive/v$pkgver.tar.gz"
        'v2ray.service')
sha512sums=('db143c4eca37c0a5e84e2cad6f3d734cc5065c87bf7e25dcde0d139408e9879239bb6ebfdc9524374b0e6cb01f5dd7b81c64ad0a71c75288ec4eeceb6bcbba7d'
            '507cb9033ced1ae7886ea186461edb906e2ec7125aa9465c02f7c49e631604b718952d0ddfc1f90636c87a3ab6accd080f65f54c8e969e889eb4f79c23cdfcd9')

build() {
  cd "v2ray-core-$pkgver"
  export GOFLAGS="-buildmode=pie -trimpath"
  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  go build -o v2ray ./main
  go build -o v2ctl ./infra/control/main
}

check() {
  cd "v2ray-core-$pkgver"
  go test -p 1 -tags json -v -timeout 30m v2ray.com/core/...
}

package() {
  cd v2ray-core-$pkgver
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/v2ray/LICENSE
  install -Dm644 release/config/systemd/v2ray.service "$pkgdir"/usr/lib/systemd/system/v2ray.service
  sed -i -e 's|/usr/bin/v2ray/v2ray|/usr/lib/v2ray/v2ray|' \
         "$pkgdir"/usr/lib/systemd/system/v2ray.service
  install -Dm644 release/config/*.json -t "$pkgdir"/etc/v2ray/
  install -Dm755 v2ray -t "$pkgdir"/usr/lib/v2ray/
  install -Dm755 v2ctl -t "$pkgdir"/usr/lib/v2ray/
  install -dm755 "$pkgdir"/usr/bin
  ln -s ../lib/v2ray/v2ray "$pkgdir"/usr/bin/v2ray
  ln -s ../lib/v2ray/v2ctl "$pkgdir"/usr/bin/v2ctl

  install -Dm644 "$srcdir"/v2ray.service "$pkgdir"/usr/lib/systemd/system/v2ray@.service
}
