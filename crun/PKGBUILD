# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: hexchian <i at hexchain dot org>

pkgname=crun
pkgver=1.4.1
pkgrel=1
pkgdesc="A fast and lightweight fully featured OCI runtime and C library for running containers"
url="https://github.com/containers/crun"
license=('LGPL')
arch=('x86_64')
depends=('yajl' 'systemd-libs' 'libcap' 'libseccomp' 'criu')
makedepends=('libtool' 'python' 'go-md2man' 'systemd' 'git')
source=("https://github.com/containers/crun/releases/download/$pkgver/$pkgname-$pkgver.tar.xz"{,.asc}
        "patch-python-build.patch::https://github.com/containers/crun/commit/2f5be742a283b890925ae7070fe8bfa0e4d00f5d.patch")
validpgpkeys=('AC404C1C0BF735C63FF4D562263D6DF2E163E1EA')
sha256sums=('8e1035c209a22a8784acd6b7b880f9c988ad0b9358f3d05e745412966e2ce546'
            'SKIP'
            'ba4acfd84ed926b0a21e8cf40a74cc020a4bdbd4cdc569cc3be242db22a1e7ca')

prepare() {
    cd "${pkgname}-${pkgver}"
    patch -Np1 < "$srcdir/patch-python-build.patch"
}

build() {
    cd "$pkgname-$pkgver"
    ./autogen.sh
    ./configure \
        --prefix=/usr \
        --enable-shared \
        --enable-dynamic \
        --with-python-bindings
    make
}

package() {
    cd "$pkgname-$pkgver"
    make DESTDIR="$pkgdir" install
}
