# Maintainer: Morten Linderud <foxboron@archlinux.org>

pkgname=python-adblock
pkgver=0.4.0
pkgrel=1
pkgdesc="Brave's adblock library in Python"
arch=("x86_64")
url="https://github.com/ArniDagur/python-adblock"
license=("MIT")
depends=()
makedepends=("python-setuptools" "python-dephell" "rust")
checkdepends=("python-pytest")
source=("$pkgname-$pkgver.tar.gz::https://github.com/ArniDagur/${pkgname}/archive/${pkgver}.tar.gz")
sha256sums=('49cf5bef7a637b9c08ddf6d2f68d11b522f341d2667ea9eb9ef584a59d130a92')

export PYTHONHASHSEED=0

prepare() {
    cd "$pkgname-$pkgver"
    dephell deps convert --from pyproject.toml --to setup.py
    sed -i 's/package_dir.*//' ./setup.py
}

build() {
    cd $pkgname-$pkgver
    python setup.py build
    cargo build --release --locked --all-features --target-dir=target
}

# check() {
#     cd $pkgname-$pkgver
#     PYTHONPATH="$PWD" pytest ./tests
# }

package() {
    cd $pkgname-$pkgver
    install -Dm755 target/release/libadblock.so -t "$pkgdir/usr/lib"
    python setup.py install --root="$pkgdir" --optimize=1 --skip-build
    install -Dm0644 LICENSE-MIT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE-MIT"
}

