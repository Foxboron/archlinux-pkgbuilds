# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Maintainer: Morten Linderud <foxboron@archlinux.org>

pkgname=k9s
pkgver=0.18.1
pkgrel=1
pkgdesc='TUI for managing Kubernetes clusters and pods'
arch=(x86_64)
url='https://github.com/derailed/k9s'
license=(APACHE)
makedepends=(git go-pie)
source=("git+$url#commit=c6a8477e5b85e43d08cb58790b7d5ded56c766ce") # 0.18.1
sha256sums=('SKIP')

build() {
  cd $pkgname
  export GOFLAGS='-buildmode=pie -trimpath'
  export CGO_LDFLAGS="$LDFLAGS"
  make VERSION=v$pkgver build
}

check() {
  make -C $pkgname test
}

package() {
  cd $pkgname
  install -Dm755 "execs/$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# getver: github.com/derailed/k9s/releases
# vim: ts=2 sw=2 et:
