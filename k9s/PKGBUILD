# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Maintainer: Morten Linderud <foxboron@archlinux.org>

pkgname=k9s
pkgver=0.19.2
pkgrel=1
pkgdesc='TUI for managing Kubernetes clusters and pods'
arch=(x86_64)
url='https://github.com/derailed/k9s'
license=(APACHE)
makedepends=(git go)
_commit=e231f18a79c6fb40a173135704564af3cf23caa0 # 0.19.2
source=("git+$url#commit=$_commit")
sha256sums=('SKIP')

build() {
  cd $pkgname
  export GOFLAGS='-buildmode=pie -trimpath'
  export CGO_LDFLAGS="$LDFLAGS"
  export CGO_CFLAGS="$CFLAGS"
  make VERSION=v$pkgver build
}

check() {
  make -C $pkgname test
}

package() {
  cd $pkgname
  install -Dm755 "execs/$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# getver: github.com/derailed/k9s/releases
# vim: ts=2 sw=2 et:
