# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>

pkgname=k9s
pkgver=0.17.4
pkgrel=1
pkgdesc='Kubernetes TUI for managing clusters and pods'
arch=(x86_64)
url='https://github.com/derailed/k9s'
license=(APACHE)
makedepends=(git go-pie)
source=("git+$url#commit=413605a3e7eb0f9a38029b657d3aa0b6315bba35") # 0.17.4
sha256sums=('SKIP')

build() {
  cd $pkgname
  go build \
    -gcflags "all=-trimpath=${PWD}" \
    -asmflags "all=-trimpath=${PWD}" \
    -ldflags "-extldflags ${LDFLAGS}"
}

package() {
  cd $pkgname
  install -Dm755 "$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# getver: github.com/derailed/k9s/releases
# vim: ts=2 sw=2 et:
