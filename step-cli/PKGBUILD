# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Maintainer: Morten Linderud <foxboron@archlinux.org>

pkgname=step-cli
pkgver=0.17.2
pkgrel=1
pkgdesc="A zero trust swiss army knife for working with X509, OAuth, JWT, OATH OTP, etc."
url="https://github.com/smallstep/cli"
arch=(x86_64)
license=(Apache)
depends=(glibc)
makedepends=(go)
optdepends=("step-ca: setup CA/SSO/ACME server")
source=(https://github.com/smallstep/cli/releases/download/v${pkgver}/step_${pkgver}.tar.gz
        update_tests.patch
        update_identity_test.patch)
sha512sums=('30c8a0c1cbc28ff1d8e9ceecd89ecc45ea67e78540502e831590668be15baef8fffd09f2acaf719627a36f22de9361d91a1c98bcaa7e57a50aca7e6339a1ba66'
            'b3226046764984c67a7e9b489d043e360560633b637778a9eed359ce26c3d23087b4ba5ab18d7467348e5086a68e0a588f50a5f99078dc1da59ad01ab0e8226a'
            '2c5084bad7105cfc2a8bc2cbebfaaefbb163e0acfafe758995993d75d859b8610cdf5faca0b97bac47c04a8399617bf42d3f0a8deef4e7025c7cf48e84b48d26')

prepare() {
  sed -i "s/step/${pkgname}/g" "autocomplete/zsh_autocomplete"
  patch -Np1 -i update_tests.patch
  patch -Np1 -i update_identity_test.patch
}

build() {
  BUILD_DATE="$(date --utc --date="@${SOURCE_DATE_EPOCH:-$(date +%s)}" +%Y-%m-%d)"

  go build \
      -trimpath \
      -buildmode=pie \
      -mod=readonly \
      -modcacherw \
      -ldflags "-linkmode external -extldflags \"${LDFLAGS}\" -X \"main.Version=${pkgver}\" -X \"main.BuildTime=${BUILD_DATE}\"" \
      -o step-cli cmd/step/main.go
}

check() {
  go test ./...
}

package() {
  install -Dm755 $pkgname "$pkgdir"/usr/bin/$pkgname
  install -Dm644 "autocomplete/bash_autocomplete" "$pkgdir/usr/share/bash-completion/completions/$pkgname"
  install -Dm644 "autocomplete/zsh_autocomplete" "$pkgdir/usr/share/zsh/site-functions/_${pkgname}"
}
