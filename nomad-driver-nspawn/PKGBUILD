# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=nomad-driver-nspawn
pkgver=0.9.0
pkgrel=1
pkgdesc="A nomad taskdriver for systemd-nspawn"
arch=('x86_64')
url="https://github.com/JanMa/nomad-driver-nspawn"
license=('MIT')
depends=('glibc' 'nomad' 'systemd')
makedepends=('go' 'git')
options=('!lto')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('a4153eb8857be09ead18a1ca23ad60a8d5412bc7164a8ccdb180994c265c5f0db8acba43ae22384386004f771a4a87f6b68e7b89a169e9908db59310fbfd1b9c')
b2sums=('187d07eec0c2be2ffc66863a64a51248d01cb5c3907513988845085e503dbde47610984c51180f2b9570f29d1f16d973ddc55b4a5318fb805145bea5ca04ec08')

prepare() {
	cd "$pkgname-$pkgver"

  # create directory for build output
  mkdir build

  # verify modules
  go mod verify
}

build() {
	cd "$pkgname-$pkgver"

  go build -v \
    -trimpath \
    -buildmode=pie \
    -mod=vendor \
    -modcacherw \
    -ldflags "-extldflags $LDFLAGS -X "github.com/JanMa/nomad-driver-nspawn/nspawn.pluginVersion=${pkgver}"" \
    -o build \
    .
}

package() {
	cd "$pkgname-$pkgver"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md
  cp -r example "$pkgdir/usr/share/doc/$pkgname"

  # plugin
  install -vDm755 -t "$pkgdir/usr/lib/nomad/plugins" "build/$pkgname"

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
