# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=nomad-driver-nspawn
pkgver=0.8.0
pkgrel=1
pkgdesc="A nomad taskdriver for systemd-nspawn"
arch=('x86_64')
url="https://github.com/JanMa/nomad-driver-nspawn"
license=('MIT')
depends=('glibc' 'nomad' 'systemd')
makedepends=('go' 'git')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('ca6ca7439c7788b98aa4c01e947bad611f6f2e8386e14a509badf56db768dc5ffad4c960b74b9688f01a38ee636aba395f59aacb404a65f3057bc63b50163016')
b2sums=('2fa8afb69660169edc7fa2a03332386836aa117b659fdf1aea7d9f4881f915655df441418ec9dee08383ab0fbe9008ccd4e1070cbf1fab0fe39a3b4906f5379c')

prepare() {
	cd "$pkgname-$pkgver"

  # create directory for build output
  mkdir build

  # verify modules
  go mod verify
}

build() {
	cd "$pkgname-$pkgver"

  go build -v \
    -trimpath \
    -buildmode=pie \
    -mod=vendor \
    -modcacherw \
    -ldflags "-extldflags $LDFLAGS -X "github.com/JanMa/nomad-driver-nspawn/nspawn.pluginVersion=${pkgver}"" \
    -o build \
    .
}

package() {
	cd "$pkgname-$pkgver"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md
  cp -r example "$pkgdir/usr/share/doc/$pkgname"

  # plugin
  install -vDm755 -t "$pkgdir/usr/lib/nomad/plugins" "build/$pkgname"

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
