# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Axel Hinrichs <hinrichs@bitscape.de>

pkgname=catatonit
pkgver=0.1.7
pkgrel=1
pkgdesc="A container init that is so simple it's effectively brain-dead."
arch=('x86_64')
url="https://github.com/openSUSE/catatonit/"
license=('GPL3')
source=("$pkgname-$pkgver.tar.gz::https://github.com/openSUSE/catatonit/releases/download/v$pkgver/$pkgname.tar.xz"{,.asc}
        "patch-autoconf.patch::https://github.com/openSUSE/catatonit/commit/99bb9048f532257f3a2c3856cfa19fe957ab6cec.patch")
validpgpkeys=('5F36C6C61B5460124A75F5A69E18AA267DDB8DB4')
sha256sums=('6ea6cb8c7feeca2cf101e7f794dab6eeb192cde177ecc7714d2939655d3d8997'
            '6ea6cb8c7feeca2cf101e7f794dab6eeb192cde177ecc7714d2939655d3d8997'
            '93e0429aa58cecea6cf2a8727bcc53e6eca90da63305a24c4f826b5e31c90d1a')

prepare(){
  cd "$pkgname-$pkgver"
  patch -Np1 < "$srcdir/patch-autoconf.patch"
}

build() {
  cd "$pkgname-$pkgver"
  ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd "$pkgname-$pkgver"
  make PREFIX=/usr DESTDIR="$pkgdir" install
  install -d "${pkgdir}/usr/libexec/podman/"
  ln -s /usr/bin/$pkgname "${pkgdir}/usr/libexec/podman/"
}

# vim: ft=sh syn=sh
