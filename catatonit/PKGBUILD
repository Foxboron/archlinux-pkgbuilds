# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Axel Hinrichs <hinrichs@bitscape.de>

pkgname=catatonit
pkgver=0.1.6
pkgrel=1
pkgdesc="A container init that is so simple it's effectively brain-dead."
arch=('x86_64')
url="https://github.com/openSUSE/catatonit/"
license=('GPL3')
source=("$pkgname-$pkgver.tar.gz::https://github.com/openSUSE/catatonit/releases/download/v$pkgver/$pkgname.tar.xz"
        "$pkgname-$pkgver.tar.gz.asc::https://github.com/openSUSE/catatonit/releases/download/v$pkgver/$pkgname.tar.xz.asc")
validpgpkeys=('5F36C6C61B5460124A75F5A69E18AA267DDB8DB4')
sha256sums=('45cc16e339d2c34d86f62bebbe9b5071453d591f2d92aee33acd37f898b1754b'
            'SKIP')

prepare() {
  cd "$pkgname-$pkgver"
  autoreconf -fi
}

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

package() {
  cd "$pkgname-$pkgver"
  make PREFIX=/usr DESTDIR="$pkgdir" install
  install -d "${pkgdir}/usr/libexec/podman/"
  ln -s /usr/bin/$pkgname "${pkgdir}/usr/libexec/podman/"
}

# vim: ft=sh syn=sh
