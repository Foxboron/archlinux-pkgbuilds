# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: C. Dominik BÃ³di <dominik dot bodi at gmx dot de>

pkgname=libmd
pkgver=1.0.1
pkgrel=2
pkgdesc="Message Digest functions from BSD systems"
arch=('x86_64')
url="https://www.hadrons.org/software/libmd/"
license=('BSD')
depends=('glibc')
source=("https://archive.hadrons.org/software/$pkgname/$pkgname-$pkgver.tar.xz"{,.asc})
sha256sums=('e14eeb931cf85330f95ff822262d3033125488dfb2f867441e36e2d2c4a34c71' 'SKIP')
validpgpkeys=('4F3E74F436050C10F5696574B972BF3EA4AE57A3') # Guillem Jover

build() {
    cd ${pkgname}-${pkgver}
    ./configure --prefix=/usr
    make
}

check() {
    cd ${pkgname}-${pkgver}
    make -k check
}

package() {
    cd ${pkgname}-${pkgver}
    make DESTDIR="${pkgdir}" install
    install -Dm644 COPYING -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
