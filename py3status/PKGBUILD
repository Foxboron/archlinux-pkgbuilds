# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Alexis "Horgix" Chotard <alexis.horgix.chotard@gmail.com>
# Contributor: carstene1ns <arch carsten-teibes de> - http://git.io/ctPKG

pkgname=py3status
pkgver=3.12
pkgrel=3
pkgdesc="An extensible i3status replacement/wrapper written in python"
url="http://www.ultrabug.fr/tag/py3status/"
arch=('any')
license=('BSD')
depends=('python' 'python-setuptools' 'i3status')
optdepends=('i3status: for some of the functionality'
            'acpi: for some of the battery related modules'
            'pacman-contrib: for the arch_updates module')
source=($pkgname-$pkgver.tar.gz::"https://github.com/ultrabug/py3status/archive/$pkgver.tar.gz"
        "fix_to_ascii.patch")
sha256sums=('c85e10116be2c62ba0916c3cc2e2af75e7fb3c1c714f0690dbdadfcb3871c51f'
            'bf3d79664e0f6974b52a84c386663969b6394fc1c5f4680f6eaae24a8b459de9')

prepare() {
  cd $pkgname-$pkgver
  patch -p0 -i "$srcdir/fix_to_ascii.patch" setup.py
}

build() {
  cd $pkgname-$pkgver
  python setup.py build
}

package() {
  cd $pkgname-$pkgver
  python setup.py install --root="$pkgdir/" --optimize=1 --skip-build
  install -d "$pkgdir"/usr/share/doc/$pkgname
  install -m644 doc/* README.rst CHANGELOG "$pkgdir"/usr/share/doc/$pkgname
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
