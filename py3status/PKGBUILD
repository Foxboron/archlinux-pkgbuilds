# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Maintainer: Maxim Baz <$pkgname at maximbaz dot com>
# Contributor: Alexis "Horgix" Chotard <alexis.horgix.chotard@gmail.com>
# Contributor: carstene1ns <arch carsten-teibes de> - http://git.io/ctPKG

pkgname=py3status
pkgver=3.15
pkgrel=1
pkgdesc="An extensible i3status replacement/wrapper written in python"
url="http://www.ultrabug.fr/tag/py3status/"
arch=('any')
license=('BSD')
depends=('python' 'python-setuptools' 'i3status')
optdepends=('python-pyudev: for udev event monitoring'
            'python-pydbus: for modules that rely on DBUS'
            'python-tzlocal: for modules that need time zone info'
            'pacman-contrib: for the arch_updates module')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ultrabug/py3status/archive/$pkgver.tar.gz")
sha256sums=('79e487017e827e3428ae526f5c967f9e819b34a84bafee4fac2121d5de84ef1c')

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --root="$pkgdir/" --optimize=1 --skip-build
  install -d "$pkgdir/usr/share/doc/$pkgname"
  install -m644 doc/* README.rst CHANGELOG "$pkgdir/usr/share/doc/$pkgname"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
