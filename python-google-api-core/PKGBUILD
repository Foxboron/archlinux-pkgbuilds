# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Kaizhao Zhang <zhangkaizhao@gmail.com>

pkgname=python-google-api-core
pkgver=2.7.2
pkgrel=1
pkgdesc="Google API client core library"
arch=('any')
url="https://github.com/googleapis/googleapis"
license=('Apache')
depends=('python-pytz' 'python-requests' 'python-googleapis-common-protos' 'python-google-auth')
makedepends=('python-setuptools')
optdepends=('python-grpcio: for grpc support')
source=("https://files.pythonhosted.org/packages/source/g/google-api-core/google-api-core-${pkgver}.tar.gz")
sha256sums=('65480309a7437f739e4476da037af02a3ec8263f1d1f89f72bbdc8f54fe402d2')

build() {
  cd "google-api-core-${pkgver}"
  python setup.py build
}

package() {
  cd "google-api-core-${pkgver}"
  python setup.py install --root="$pkgdir" --optimize=1
  chmod -R a+r "$pkgdir"/usr/lib/python*/site-packages/google_api_core*.egg-info/
}
