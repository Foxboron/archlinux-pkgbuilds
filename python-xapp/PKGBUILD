# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgbase=python-xapp
pkgname=(python-xapp python2-xapp)
pkgver=1.6.0
pkgrel=1
pkgdesc="Python Xapp Library"
arch=('any')
url="https://github.com/linuxmint/${pkgbase}"
license=('LGPL')
makedepends=('python' 'python2')
source=("${pkgbase}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz")
sha512sums=('4418f3fc38ff8bf6993de1b7b9358bce16bef61832814147ae89f2bfc530244308bf270df98c10e451e97f017f766cb9106e143c608fbd8a7350ef5423cefc98')

build(){
  cd "${srcdir}"/${pkgbase}-${pkgver}

  python setup.py build
  python2 setup.py build
}

package_python-xapp() {
  depends=('python' 'python-psutil')

  cd "${srcdir}"/${pkgbase}-${pkgver}
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}

package_python2-xapp() {
  depends=('python2' 'python2-psutil')

  cd "${srcdir}"/${pkgbase}-${pkgver}
  python2 setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}
