# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgbase=python-xapp
pkgname=(python-xapp python2-xapp)
pkgver=1.4.0
pkgrel=1
pkgdesc="Python Xapp Library"
arch=('any')
url="https://github.com/linuxmint/${pkgbase}"
license=('LGPL')
depends=('python' 'python2')
source=("${pkgbase}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz")
sha512sums=('b364135536fde6175b7a5b5002ccb3103401760a78856b665715caec3c95e544238cfb3447600a1fd729a24917a37b2aba5a13fdcb4462510b5bf7090189309f')

build(){
  cd "${srcdir}"/${pkgbase}-${pkgver}

  python setup.py build
  python2 setup.py build
}

package_python-xapp() {
  depends=('python' 'python-psutil')

  cd "${srcdir}"/${pkgbase}-${pkgver}
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}

package_python2-xapp() {
  depends=('python2' 'python2-psutil')

  cd "${srcdir}"/${pkgbase}-${pkgver}
  python2 setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}
