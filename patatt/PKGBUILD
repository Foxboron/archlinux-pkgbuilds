# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Thomas Wei√üschuh <thomas t-8ch de>

pkgname=patatt
pkgver=0.5.0
pkgrel=1
pkgdesc="A simple library to add cryptographic attestation to patches sent via email"
arch=("any")
depends=('python-pynacl')
makedepends=('python-setuptools')
url="https://git.kernel.org/pub/scm/utils/patatt/patatt.git/about/"
license=("MIT")
source=("https://git.kernel.org/pub/scm/utils/$pkgname/$pkgname.git/snapshot/$pkgname-$pkgver.tar."{gz,asc})
validpgpkeys=('DE0E66E32F1FDD0902666B96E63EDCA9329DD07E')
sha256sums=('cb499b15ddee88de997293d23b403bdb2e6ce19a71ca49ffbcde190f04d48d90'
            'SKIP')

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --root="$pkgdir"
  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  install -d "$pkgdir/usr/share/man/man5"
  install -m644 man/*.5 "$pkgdir/usr/share/man/man5"
}

