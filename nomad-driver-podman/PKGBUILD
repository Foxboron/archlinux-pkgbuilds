# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=nomad-driver-podman
pkgver=0.3.0
pkgrel=2
pkgdesc='A nomad taskdriver for podman containers'
arch=('x86_64')
url='https://github.com/hashicorp/nomad-driver-podman'
license=('MPL2')
depends=('nomad' 'podman')
makedepends=('git' 'go')
options=('!lto')
_commit='ec01bb2b87216219da064359ef95dd3f7f226457'
source=("$pkgname::git+$url#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd "$pkgname"

  git describe --tags | sed 's/^v//'
}

prepare() {
	cd "$pkgname"

  mkdir build
}

build() {
	cd "$pkgname"

  go build -v \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-linkmode external -extldflags \"${LDFLAGS}\"" \
    -o build \
    .
}

package() {
	cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/lib/nomad/plugins" "build/$pkgname"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md
  cp -vr examples "$pkgdir/usr/share/doc/$pkgname"
  # note: examples/plugins is an empty folder
  rm -rf "$pkgdir/usr/share/doc/$pkgname/examples/plugins"
}
