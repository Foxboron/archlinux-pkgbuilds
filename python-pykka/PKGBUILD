# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: Antoine Pierlot-Garcin <antoine at bokbox dot com>

pkgname=python-pykka
pkgver=2.0.3
pkgrel=1
pkgdesc="Easy to use concurrency abstractions for Python using the actor model"
arch=('any')
url="http://pykka.readthedocs.org/"
license=('Apache')
depends=('python')
makedepends=('python-setuptools')
optdepends=('python-gevent: Use gevent based actors from pykka.gevent')
source=("https://pypi.python.org/packages/source/P/Pykka/Pykka-$pkgver.tar.gz")
sha512sums=('0f4d6d9e0c92a7e7cafb340f343afa1e78bcefdd0454341d3f457fa202acdd3de979881689cb3cd5f2cbd3a08f80177f6470db4fb895b20d9c1b4d0b9ef627ee')

build() {
  cd "Pykka-$pkgver"
  python setup.py build
}

package() {
  cd "Pykka-$pkgver"
  python setup.py install --prefix=/usr --root="$pkgdir"/ --optimize=1 --skip-build
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
