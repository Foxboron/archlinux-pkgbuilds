# Maintainer: Morten Linderud <foxboron@archlinux.org>

pkgname=dep
pkgver=0.4.1
pkgrel=3
pkgdesc="Go dependency management tool"
arch=('x86_64')
url='https://golang.github.io/dep/'
license=('BSD')
makedepends=('go-pie' 'git')
optdepends=('git: fetch git sources')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/golang/dep/archive/v${pkgver}.tar.gz")
sha256sums=('df9b050bf4dbb2a8cf04372097a68b04a6ae1986ed7c5086914ac86d74ea9d49')

prepare(){
  export GOPATH="${srcdir}"
  mkdir -p src
  mv "${pkgname}-${pkgver}"/vendor/* src/
  mkdir -p src/github.com/golang
  ln -rTsf "${pkgname}-${pkgver}" src/github.com/golang/dep
}

# TODO: Makefile next release

build(){
  cd "${pkgname}-${pkgver}"
  go build ./cmd/dep
}

check(){
  cd "${pkgname}-${pkgver}"
  go test
}

package(){
  cd "${pkgname}-${pkgver}"
  install -Dm755 dep "${pkgdir}/usr/bin/dep"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
