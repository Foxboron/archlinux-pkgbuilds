# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=baidupcs-go
pkgver=3.6.2
pkgrel=2
pkgdesc="Terminal utility for Baidu Network Disk"
arch=('x86_64')
url="https://github.com/iikira/BaiduPCS-Go"
license=('Apache')
depends=('glibc')
makedepends=('go' 'git')
conflicts=("baidupcs")
provides=("baidupcs")
source=("$pkgname-$pkgver.tar.gz::https://github.com/iikira/BaiduPCS-Go/archive/v$pkgver.tar.gz")
sha512sums=('e7248521cade634aa41224c88b8dd07cac3a84d0f262c369447c4c4872e52c9431dcee38b42cf6360c13a64cc1b6bce1cc4199de54602b4e8bac95b37a68688c')

build() {
  cd BaiduPCS-Go-$pkgver
  export GOFLAGS="-buildmode=pie -trimpath"
  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  go build -o baidupcs-go
}

package() {
  cd BaiduPCS-Go-$pkgver
  install -Dm755 baidupcs-go "$pkgdir"/usr/bin/baidupcs
}
