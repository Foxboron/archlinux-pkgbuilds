# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Ingo BÃ¼rk <admin at airblader dot de>

pkgname=i3-gaps
_pkgsourcename=i3
pkgver=4.18.2
pkgrel=2
pkgdesc='A fork of i3wm tiling window manager with more features, including gaps'
arch=('i686' 'x86_64')
url='https://github.com/Airblader/i3'
license=('BSD')
provides=('i3-wm')
conflicts=('i3-wm')
groups=('i3')
depends=('xcb-util-keysyms' 'xcb-util-wm' 'libev' 'yajl'
         'startup-notification' 'pango' 'perl' 'xcb-util-cursor' 'xcb-util-xrm'
         'libxkbcommon-x11')
makedepends=('bison' 'flex' 'asciidoc' 'xmlto')
optdepends=('rxvt-unicode: The terminal emulator used in the default config.'
            'dmenu: As menu.'
            'i3lock: For locking your screen.'
            'i3status: To display system information with a bar.'
            'perl-json-xs: For i3-save-tree'
            'perl-anyevent-i3: For i3-save-tree')
backup=(etc/i3/config)
options=('docs')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/Airblader/i3/archive/${pkgver}.tar.gz"
        "fix-tray.patch::https://github.com/i3/i3/commit/838b600fead202416013db5c1b57f7031f06bed6.diff")
sha256sums=('cc03cc008f51792b3c2fd5c8815ec213f8040385865fa558b0c78d5dc5f7150d'
            'bade69559a83187375dbe12a23079225ad443fb90d23383647cdc7998e617d0c')

prepare(){
  mkdir build
  cd "${_pkgsourcename}-${pkgver}"
  autoreconf -fvi
  patch -Np1 < "$srcdir/fix-tray.patch"
}

build() {
  cd build

  ../i3-$pkgver/configure \
    --disable-sanitizers \
    --prefix=/usr \
    --sysconfdir=/etc
  make
}

package() {
  cd build

  make DESTDIR="${pkgdir}/" install
  install -Dt "$pkgdir/usr/share/man/man1" -m644 man/*.1
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 ../i3-$pkgver/LICENSE
}

# vim:set ts=2 sw=2 et:
