# Maintainer: Andrew Crerar <crerar@archlinux.org>
# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Angel 'angvp' Velasquez <angvp[at]archlinux.com.ve>
# Contributor: Douglas Soares de Andrade <dsa@aur.archlinux.org>

pkgname=python-psycopg2
pkgver=2.9.1
pkgrel=1
pkgdesc="A PostgreSQL database adapter for the Python programming language."
arch=('x86_64')
url="http://initd.org/psycopg/"
license=('LGPL3')
depends=('python' 'postgresql-libs')
makedepends=('python-setuptools' 'postgresql-libs')
source=(http://initd.org/psycopg/tarballs/PSYCOPG-2-8/psycopg2-$pkgver.tar.gz{,.asc})
validpgpkeys=('8AD609956CF1899418E19A856013BD3AFCF957DE')
sha512sums=('478eaeaad98e4521424a0a48d5f0fdba934bb3b682ba111cc37b833cd3c953ad5fbe2a2071d3a9b95c6a447e95de88887ceda5143b63a08703c388f96dd9b4ad'
            'SKIP')
build(){
  cd "psycopg2-$pkgver"
  sed -i 's/,PSYCOPG_DEBUG$//' setup.cfg
  python setup.py build
}

package() {
  cd "psycopg2-$pkgver"
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}

