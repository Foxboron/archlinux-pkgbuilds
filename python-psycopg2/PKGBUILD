# Maintainer: Andrew Crerar <crerar@archlinux.org>
# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Angel 'angvp' Velasquez <angvp[at]archlinux.com.ve>
# Contributor: Douglas Soares de Andrade <dsa@aur.archlinux.org>

pkgname=python-psycopg2
pkgver=2.9.5
pkgrel=1
pkgdesc="A PostgreSQL database adapter for the Python programming language."
arch=('x86_64')
url="https://www.psycopg.org/"
license=('LGPL3')
depends=('python' 'postgresql-libs')
makedepends=('python-setuptools' 'postgresql-libs')
source=("https://github.com/psycopg/psycopg2/archive/${pkgver//\./_}.tar.gz")
b2sums=('4813a18fbf2dd36be5fbb893943efc861308368c56462e5755ea5a57c6ba7ce82af24acb83c6209f2eb3935c31d1faa9d6819f7e82ba870d12406a40c04e7da1')
build(){
  cd "psycopg2-${pkgver//\./_}"
  sed -i 's/,PSYCOPG_DEBUG$//' setup.cfg
  python setup.py build
}

package() {
  cd "psycopg2-${pkgver//\./_}"
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
