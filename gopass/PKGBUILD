# Maintainer: Morten Linderud <foxboron@archlinux.org>

pkgname=gopass
pkgver=1.10.1
pkgrel=2
pkgdesc="The slightly more awesome standard unix password manager for teams."
arch=('x86_64')
url="https://github.com/gopasspw/gopass"
license=('MIT')
makedepends=('go')
optdepends=('xdotool: for typing passwords selected by dmenu'
            'xsel: clipboard support'
            'xclip: clipboard support')
source=("https://github.com/gopasspw/gopass/releases/download/v${pkgver}/gopass-${pkgver}.tar.gz"
        "fix-broken.patch::https://github.com/gopasspw/gopass/commit/194199c6a8c7d32ce203f06910da5bc3a9cd7be7.patch")
sha512sums=('d175c1cfb996b56d3291e20c093093d4b7e35d91100d42059ad847dc5b61367861c81c3c92fcc689f9f59ea07998e607a9a3141fe57bbcb522ee2c9bba46218f'
            'd6bdb1bc0c2bbbf0f37cc66eedaa18ef96d45b5507c18ea323d95ecbe458c945b7cec60695e74314db353b1951e687e70dbd5aa4b7237274f82c5f2a50c1293f')

prepare(){
  sed -i 's|-gcflags="-trimpath=$(GOPATH)" -asmflags="-trimpath=$(GOPATH)"||' Makefile
  patch -Np1 < "$srcdir/fix-broken.patch"
}

build(){
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
  make 
}

package() {
  make DESTDIR="${pkgdir}" PREFIX="/usr" install
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
