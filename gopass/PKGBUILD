# Maintainer: Morten Linderud <foxboron@archlinux.org>

pkgname=gopass
pkgver=1.9.1
pkgrel=1
pkgdesc="The slightly more awesome standard unix password manager for teams."
arch=('x86_64')
url="https://github.com/gopasspw/gopass"
license=('MIT')
makedepends=('go')
optdepends=('xdotool: for typing passwords selected by dmenu'
            'xsel: clipboard support'
            'xclip: clipboard support')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/gopasspw/gopass/archive/v${pkgver}.tar.gz")
sha512sums=('a70affc278b5d46582a3362c28f1aff8be4481aae3d07f3b7e200117fc2ac79466c475d558fe5a642b0102a46fc95242334f406deca6ee4abd7d5e9f674451ab')

build(){
  cd "${pkgname}-${pkgver}"
  export GOPATH="${srcdir}"
  export GOFLAGS="-buildmode=pie -trimpath"
  export CGO_LDFLAGS="$LDFLAGS"
  export CGO_CFLAGS="$CFLAGS"
  make build
  make completion
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" PREFIX="/usr" install
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
