# Maintainer: Morten Linderud <foxboron@archlinux.org>

pkgname=gopass
pkgver=1.14.2
pkgrel=1
pkgdesc="The slightly more awesome standard unix password manager for teams."
arch=('x86_64')
url="https://github.com/gopasspw/gopass"
license=('MIT')
makedepends=('go')
optdepends=('xdotool: for typing passwords selected by dmenu'
            'xsel: clipboard support'
            'xclip: clipboard support')
options=("!lto")
source=("https://github.com/gopasspw/gopass/releases/download/v${pkgver}/gopass-${pkgver}.tar.gz")
sha512sums=('dffa3ae0b46932b46cd998052414fa8e0560eaf2e1a2ab2beee0ff9307f45d5ce1b408f828c4a969c95f760a6847e1bfd0be7a4273a039b431924f3c8aa3b911')

prepare(){
  sed -i 's|-gcflags="-trimpath=$(GOPATH)" -asmflags="-trimpath=$(GOPATH)"||' Makefile
}

build(){
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
  make 
}

package() {
  make DESTDIR="${pkgdir}" PREFIX="/usr" install
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
