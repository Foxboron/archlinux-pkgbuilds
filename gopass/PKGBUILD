# Maintainer: Morten Linderud <foxboron@archlinux.org>

pkgname=gopass
pkgver=1.9.0
pkgrel=2
pkgdesc="The slightly more awesome standard unix password manager for teams."
arch=('x86_64')
url="https://github.com/gopasspw/gopass"
license=('MIT')
makedepends=('go')
optdepends=('xdotool: for typing passwords selected by dmenu'
            'xsel: clipboard support'
            'xclip: clipboard support')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/gopasspw/gopass/archive/v${pkgver}.tar.gz"
        "clipboard.patch")
sha512sums=('5f0dbc42bcdbb5acdb36806b24cc607115eb403e3ca5268818590dccf23e62f5337e570b77c5fadd42e9b508fb419b51603a6cb6bb4abe6d9fa334c9710d61ca'
            '05442c85c4ffe4d579aee990cd75eee7fc93972d8adf82c423a2f2b79fab67ced132f64e941cb5f2681106879555f2f8b1db8a33181588283dd6fc741bd51e30')

prepare(){
  cd "${pkgname}-${pkgver}"
  patch -Np1 < "$srcdir/clipboard.patch"
}

build(){
  cd "${pkgname}-${pkgver}"
  export GOPATH="${srcdir}"
  export GOFLAGS="-buildmode=pie -trimpath"
  export CGO_LDFLAGS="$LDFLAGS"
  export CGO_CFLAGS="$CFLAGS"
  make build
  make completion
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" PREFIX="/usr" install
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
