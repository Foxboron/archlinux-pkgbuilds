# Maintainer: Andrew Crerar <crerar@archlinux.org>
# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Iwan Timmer <irtimmer@gmail.com>
# Contributor: Vincent Demeester <vincent@sbr.io>
# Contributor: Josh VanderLinden <arch@cloudlery.com>

pkgname=docker-compose
pkgver=1.23.1
pkgrel=1
pkgdesc="Fast, isolated development environments using Docker"
arch=('any')
url="https://www.docker.com/"
license=("Apache")
depends=('python-cached-property' 'python-docopt' 'python-yaml' 'python-requests'
         'python-texttable' 'python-websocket-client' 'python-docker' 'python-dockerpty'
         'python-six' 'python-jsonschema' 'python-setuptools' 'docker')
checkdepends=('python-pytest-runner' 'python-mock')
source=("$pkgname-$pkgver.tar.gz::https://github.com/docker/compose/archive/$pkgver.tar.gz")
sha512sums=('d294663e575657c361602693d66aca1ae3c0f5d4b25a74f4ab575be34054dc3168cb77d59fedf981f6bc98e253e87791187440f10c6957254c4f2e29ef7063d0')

prepare() {
  cd compose-$pkgver

  # Remove upper bound on requires
  sed -i 's/==/>=/g' requirements.txt
  sed -i "s/, < .*',$/',/" setup.py
}

build() {
  cd compose-$pkgver
  python setup.py build
}

check() {
  # Hack entry points by installing it
  # TODO: need a running docker daemon to test

  cd compose-$pkgver
  # python setup.py install --root="$PWD/tmp_install" --optimize=1
  # PATH="$PWD/tmp_install/usr/bin:$PATH" PYTHONPATH="$PWD/tmp_install/usr/lib/python3.7/site-packages:$PYTHONPATH" python setup.py pytest
}

package() {
  cd compose-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  install -Dm644 contrib/completion/bash/docker-compose      "$pkgdir"/usr/share/bash-completion/completions/docker-compose
  install -Dm644 contrib/completion/fish/docker-compose.fish "$pkgdir"/usr/share/fish/completions/docker-compose.fish
  install -Dm644 contrib/completion/zsh/_docker-compose      "$pkgdir"/usr/share/zsh/site-functions/_docker-compose
}
