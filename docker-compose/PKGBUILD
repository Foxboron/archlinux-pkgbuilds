# Maintainer: Andrew Crerar <crerar@archlinux.org>
# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Iwan Timmer <irtimmer@gmail.com>
# Contributor: Vincent Demeester <vincent@sbr.io>
# Contributor: Josh VanderLinden <arch@cloudlery.com>

pkgname=docker-compose
pkgver=1.27.3
pkgrel=1
pkgdesc="Fast, isolated development environments using Docker"
arch=('any')
url="https://www.docker.com/"
license=("Apache")
depends=('python-cached-property' 'python-docopt' 'python-yaml' 'python-requests'
         'python-texttable' 'python-websocket-client' 'python-distro' 'python-docker'
         'python-dockerpty' 'python-six' 'python-jsonschema' 'python-dotenv' 'docker')
makedepends=('python-setuptools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/docker/compose/archive/$pkgver.tar.gz")
sha512sums=('43e34760574d1ec009faa7a1fa09d621a2708f01f05819b46c67fe7ebf280807f97d9a5867cbfe75e9af6f097cd3f7c8c8fee66aa5392a4c62cdf44c7fb38075')

build() {
  cd "compose-$pkgver"
  python setup.py build
}

package() {
  cd "compose-$pkgver"
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  install -Dm644 contrib/completion/bash/docker-compose      "$pkgdir"/usr/share/bash-completion/completions/docker-compose
  install -Dm644 contrib/completion/fish/docker-compose.fish "$pkgdir"/usr/share/fish/vendor_completions.d/docker-compose.fish
  install -Dm644 contrib/completion/zsh/_docker-compose      "$pkgdir"/usr/share/zsh/site-functions/_docker-compose
}
