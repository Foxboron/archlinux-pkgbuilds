# Maintainer: Morten Linderd <morten@linderud.pw>

pkgname=nageru
pkgver=1.8.6
pkgrel=1
pkgdesc="live video mixer, based around the standard M/E workflow."
arch=(x86_64)
url="https://nageru.sesse.net/"
license=('GPL3')
depends=('libmicrohttpd' 'ffmpeg' 'libx264' 'protobuf' 
         'movit' 'bmusb' 'qcustomplot' 'pciutils' 'zita-resampler'
         'sqlite3' 'luajit' 'cmake')
makedepends=('meson' 'eigen')
source=("https://nageru.sesse.net/nageru-${pkgver}.tar.gz"
        "luajit-patch.patch")
sha256sums=('67dcb070a23589f624d7067c4b09fe11faa9b1c756b884c50473545fdf7817b3'
            '480c4de61188684fc8d1727a2e3ec615d33892234e8466db8011589e9277bdf7')

prepare() {
    cd "$pkgname-$pkgver"
    patch -Np1 -i "${srcdir}/luajit-patch.patch"
}

build() {
  cd "$pkgname-$pkgver"
  arch-meson ../build
  ninja -v -C ../build
}

check() {
  cd "$pkgname-$pkgver"
  ninja -C ../build test
}

package() {
  cd "$pkgname-$pkgver"
  DESTDIR="$pkgdir" ninja -C ../build install
}

# vim:set ts=2 sw=2 et:
