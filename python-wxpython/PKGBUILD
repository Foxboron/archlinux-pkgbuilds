# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=python-wxpython
_pkgname=wxPython
pkgver=4.0.5
pkgrel=1
pkgdesc="Phoenix wxWidgets GUI toolkit for Python"
arch=('x86_64')
license=('custom:wxWindows')
url="https://www.wxpython.org"
depends=('wxgtk3' 'python-six' 'python-sphinx')
optdepends=('python-pypubsub: Alternative to the deprecated wx.lib.pubsub API')
makedepends=('mesa' 'glu' 'webkit2gtk' 'python-pathlib2' 'python-requests' 'python-setuptools')
checkdepends=('xorg-server-xvfb' 'python-pytest' 'python-numpy')
source=("https://files.pythonhosted.org/packages/source/w/wxPython/wxPython-${pkgver}.tar.gz")
sha512sums=('9ae419b243da061b467428872d36d6e996c23844ffd040b525014dae0821b8507911854af89056680aaf7207755ded79ce51322a4582c14e11aff5fbdf2f8ed8')

prepare() {
  sed -i "s|WX_CONFIG = 'wx-config'|WX_CONFIG = 'wx-config-gtk3'|" $_pkgname-$pkgver/build.py
}

build() {
  cd $_pkgname-$pkgver

  python build.py build --use_syswx --release
}

check() {
  cd $_pkgname-$pkgver

  xvfb-run python build.py test
}

package() {
  cd $_pkgname-$pkgver

  python build.py install --destdir="$pkgdir"

  install -Dm 644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
  find "$pkgdir/usr/lib" -type f | xargs chmod 644
}

