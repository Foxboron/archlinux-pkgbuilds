# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Akaash Suresh <https://github.com/Curry>
# Contributor: Batuhan Baserdem <lastname dot firstname at gmail>

pkgname=qmk
pkgver=0.0.37
_qmk_pkgver=0.11.2
pkgrel=1
pkgdesc="CLI tool for customizing supported mechanical keyboards."
arch=('any')
url="https://github.com/qmk/qmk_cli"
license=('MIT')
depends=('python-appdirs' 'python-argcomplete' 'python-colorama'
         'python-milc' 'git' 'avr-gcc' 'arm-none-eabi-gcc' 'avr-libc'
         'dfu-programmer' 'dfu-util' 'avrdude' 'python-hjson' 'python-pygments')
makedepends=('python-setuptools')
optdepends=('arm-none-eabi-binutils'
            'arm-none-eabi-newlib'
            'avr-binutils'
            'gcc'
            'libffi'
            'libusb-compat'
            'clang: chibios-upgrader'
            'zip: some distribution script'
            'wget: some update scripts'
            'diffutils: some update scripts')
source=("https://files.pythonhosted.org/packages/source/q/qmk/qmk-${pkgver}.tar.gz"
        "https://raw.githubusercontent.com/qmk/qmk_firmware/${_qmk_pkgver}/util/udev/50-qmk.rules")
sha256sums=('c10a1b18a9a9ecfa1a188a4174ed97fcb9b46b5fb706af6acdf879cfdef1af24'
            '1347051edd5350f6aad8545f5f98907d9d75f0049803bbcc7ef1c4f83b7659ad')

build() {
  cd "${pkgname}-${pkgver}"
  python setup.py build
}

package() {
  cd "${pkgname}-${pkgver}"
  python setup.py install --root="${pkgdir}/" --optimize=1 --skip-build
  install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -d "${pkgdir}/usr/lib/udev/rules.d"
  install -Dm644 ../*.rules "${pkgdir}/usr/lib/udev/rules.d"
}
