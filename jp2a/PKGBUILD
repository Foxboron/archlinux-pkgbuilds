# Maintainer: Morten Linderud <foxboron@archlinux.org>

pkgname='jp2a'
pkgver=1.0.9
pkgrel=1
pkgdesc='A small utility for converting JPG images to ASCII'
arch=('i686' 'x86_64' 'armv7h')
url='https://github.com/cslarsen/jp2a'
license=('GPL')
depends=('curl' 'libjpeg')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/Talinx/jp2a/archive/v${pkgver}.tar.gz")
sha256sums=('180790d4dae5dc5ac4b028531dc3905a1b1aa39a6a83ce145420026223d37b88')

prepare(){
  cd "${pkgname}-${pkgver}"
  autoreconf -vi
}

build(){
  cd "${pkgname}-${pkgver}"
  ./configure --prefix='/usr'
  make
}

check() {
  cd "${pkgname}-${pkgver}"
  make check
}

package(){
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
