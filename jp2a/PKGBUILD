# Maintainer: Morten Linderud <foxboron@archlinux.org>
pkgname='jp2a'
pkgver=1.0.7
pkgrel=2
pkgdesc='A small utility for converting JPG images to ASCII'
arch=('i686' 'x86_64' 'armv7h')
url='https://github.com/cslarsen/jp2a'
license=('GPL')
depends=('curl' 'libjpeg')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/cslarsen/jp2a/archive/v${pkgver}.tar.gz")
sha256sums=('e509d8bbf9434afde5c342568b21d11831a61d9942ca8cb1633d4295b7bc5059')


prepare(){
  cd "${pkgname}-${pkgver}"
  autoreconf -vi
}

build(){
  cd "${pkgname}-${pkgver}"
  ./configure --prefix='/usr'
  make
}

check() {
  cd "${pkgname}-${pkgver}"
  make check
}

package(){
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
