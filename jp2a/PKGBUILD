# Maintainer: Morten Linderud <foxboron@archlinux.org>

pkgname='jp2a'
pkgver=1.1.0
pkgrel=1
pkgdesc='A small utility for converting JPG images to ASCII'
arch=('i686' 'x86_64' 'armv7h')
url='https://github.com/Talinx/jp2a/'
license=('GPL')
depends=('curl' 'libjpeg' 'libpng')
makedepends=('doxygen' 'autoconf-archive')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/Talinx/jp2a/archive/v${pkgver}.tar.gz")
sha256sums=('feedb8e49b34a24dd518b53d70c06ab2ce2fa6c2d3adb92caa9db61631dd9856')

prepare(){
  cd "${pkgname}-${pkgver}"
  autoreconf -vi
}

build(){
  cd "${pkgname}-${pkgver}"
  ./configure --prefix='/usr'
  make
}

check() {
  cd "${pkgname}-${pkgver}"
  make check
}

package(){
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
