# Maintainer: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>

pkgname=toolbox
pkgver=0.0.18
pkgrel=1
pkgdesc='Unprivileged development environment'
arch=(any)
url='https://github.com/containers/toolbox'
license=(APACHE)
depends=(podman bash)
makedepends=(shellcheck go-md2man bash-completion ninja git meson)
_commit=7b460e390d7e5f8a90038c9d0f03a56dc622bd1c  # tags/0.0.18^0
source=("git+$url#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd toolbox
  git describe --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  arch-meson toolbox build -D profile_dir=/etc/profile.d
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
}
