# Maintainer: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Maintainer: Morten Linderud <foxboron@archlinux.org>

pkgname=conmon
pkgver=2.1.5
pkgrel=1
epoch=1
pkgdesc='OCI container runtime monitor'
arch=(x86_64)
url='https://github.com/containers/conmon'
license=(APACHE)
depends=(glibc glib2 systemd)
makedepends=(git)
_commit=c9f7f19eb82d5b8151fc3ba7fbbccf03fdcd0325	#refs/tags/v2.1.5
source=("git+https://github.com/containers/conmon#commit=${_commit}")
md5sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

build() {
  cd conmon
  make PREFIX=/usr LIBEXECDIR=/usr/lib DESTDIR="$pkgdir"
}

package() {
  install -Dm755 conmon/bin/conmon "$pkgdir/usr/bin/conmon"
}
