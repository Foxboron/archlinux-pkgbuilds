# Maintainer: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Maintainer: Morten Linderud <foxboron@archlinux.org>

pkgname=conmon
pkgver=2.0.17
pkgrel=1
epoch=1
pkgdesc='OCI container runtime monitor'
arch=(x86_64)
url='https://github.com/containers/conmon'
license=(APACHE)
depends=(glibc glib2 systemd-libs)
makedepends=(git)
_commit=41877362fc4685d55e0473d2e4a1cbe5e1debee0 # tags/v2.0.17
source=("git+https://github.com/containers/conmon#commit=${_commit}")
md5sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

build() {
  cd conmon
  make PREFIX=/usr LIBEXECDIR=/usr/lib DESTDIR="$pkgdir"
}

package() {
  install -Dm755 conmon/bin/conmon "$pkgdir/usr/bin/conmon"
}
