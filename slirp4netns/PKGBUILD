# Maintainer: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Maintainer: Morten Linderud <foxboron@archlinux.org>

pkgname=slirp4netns
pkgdesc='User-mode networking for unprivileged network namespaces'
pkgver=0.4.4
pkgrel=1
arch=(x86_64)
url="https://github.com/rootless-containers/slirp4netns"
license=(GPL2)
depends=(glibc glib2 libcap libseccomp)
makedepends=(git)
_commit=d6d5ddec4d898c5592b273412c800bb384c9ac88 # tags/v0.4.4
source=("git+$url#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd $pkgname
  autoreconf -fi
}

build() {
  cd $pkgname
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
