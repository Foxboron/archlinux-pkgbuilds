# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Maikel Wever <maikelwever@gmail.com>

pkgname=python-ldap
pkgver=3.3.0
pkgrel=1
pkgdesc="LDAP client API for Python"
url="https://python-ldap.readthedocs.io/"
arch=('x86_64')
license=('MIT')
depends=('python-pyasn1' 'python-pyasn1-modules' 'libldap')
makedepends=('python-setuptools' 'python-coverage')
checkdepends=('openldap' 'python-pyasn1' 'python-pyasn1-modules')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/python-ldap/python-ldap/archive/${pkgname}-${pkgver}.tar.gz")
sha256sums=('6700e1312c03632d997cf535b994eccbfe06e76d3675f4c9ad2336110e0dd32c')

build(){
  cd "$pkgbase-$pkgbase-$pkgver"
  python setup.py build
}

check(){
  cd "$pkgbase-$pkgbase-$pkgver"
  python -m coverage run --parallel setup.py test
}

package() {
  cd "$pkgbase-$pkgbase-$pkgver"
  python setup.py install --root="$pkgdir/" --optimize=1 --skip-build
  install -Dm 644 LICENCE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

