# Maintainer: Giancarlo Razzolini <grazzolini@archlinux.org>
# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contibutor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor:  Pawel "kTT" Salata <rockplayer.pl@gmail.com>

pkgname=python-pycountry
pkgver=22.1.10
pkgrel=2
pkgdesc="ISO country, subdivision, language, currency and script definitions and their translations"
arch=('any')
url="https://pypi.org/project/pycountry/"
license=('LGPL2.1')
depends=(python python-lxml)
makedepends=('python' 'python-setuptools')
checkdepends=('python-pytest' 'python-pytest-runner')
source=("https://pypi.io/packages/source/p/pycountry/pycountry-${pkgver}.tar.gz")
sha512sums=('93e5737df29affe5bbf8a2aa2caef59727adcc198eab301a5ddbc4275e32955cec0de9ab2820470a8cf87b0c0a9d48bfddd18411d93a700212aba8372e5a52b4')

build(){
  cd "pycountry-${pkgver}"
  python setup.py build
}

check(){
  cd "pycountry-${pkgver}"
  python setup.py pytest
}

package() {
  cd "pycountry-${pkgver}"
  python setup.py install --root="$pkgdir/" --optimize=1 --skip-build
}
