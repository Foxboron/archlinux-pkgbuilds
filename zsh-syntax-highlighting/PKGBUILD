# Maintainer: Evgeniy Alekseev <arcanis at archlinux dot org>
# Contributor: Julien Nicoulaud <julien.nicoulaud at gmail dot com>

pkgname=zsh-syntax-highlighting
pkgver=0.7.1
pkgrel=1
pkgdesc="Fish shell like syntax highlighting for Zsh"
arch=('any')
url="https://github.com/zsh-users/zsh-syntax-highlighting"
license=('BSD')
depends=('zsh>=4.3.9')
checkdepends=('procps-ng')
install="${pkgname}.install"
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/zsh-users/${pkgname}/tarball/${pkgver}")
sha512sums=('b2594e265c6ff2b6efcdb92ff604ffb45b670c0f1a99c04fda3d7061154173e1bbaffe89f0665b9dd6e171027b15068e131d01baaf470fea624cd590e3986be4')
changelog=ChangeLog

build() {
  cd "zsh-users-zsh-syntax-highlighting-"*
  make
}

check() {
  cd "zsh-users-zsh-syntax-highlighting-"*
  make quiet-test
}

package() {
  cd "zsh-users-zsh-syntax-highlighting-"*
  make PREFIX="/usr" SHARE_DIR="${pkgdir}/usr/share/zsh/plugins/${pkgname}" DESTDIR="${pkgdir}" install
  # create symlink for using with oh-my-zsh
  ln -s "zsh-syntax-highlighting.zsh" \
       "${pkgdir}/usr/share/zsh/plugins/zsh-syntax-highlighting/zsh-syntax-highlighting.plugin.zsh"

  # licence
  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
  ln -s "/usr/share/doc/${pkgname}/COPYING.md" \
        "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
