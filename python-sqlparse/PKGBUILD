# $Id$
# Maintainer:
# Contributor: Jonathan Kotta <jpkotta AT gmail DOT com>

pkgname=('python-sqlparse' 'python2-sqlparse')
pkgver=0.2.4
pkgrel=2
pkgdesc='Non-validating SQL parser for Python'
arch=('any')
url=https://github.com/andialbrecht/sqlparse
license=('BSD')
makedepends=('python-setuptools' 'python2-setuptools')
#source=("https://pypi.python.org/packages/48/e4/ecd908ef40a9d0e78fc40f3abd2f329dd96b82402fee07f14dbf69948f1b/sqlparse-$pkgver.tar.gz")
source=("$pkgname-$pkgver.tar.gz::https://github.com/andialbrecht/sqlparse/archive/$pkgver.tar.gz")
sha256sums=('7087a2bd385c06ac1a5cf343e2e5ea7ce2bb6386849e59ef214e02af68f73fb4')

prepare() {
  cp -a sqlparse-$pkgver sqlparse2-$pkgver
}

package_python-sqlparse() {
  depends=('python')

  cd sqlparse-$pkgver/
  python setup.py install --prefix=/usr --root="$pkgdir"
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-sqlparse() {
  depends=('python2')

  cd sqlparse2-$pkgver/
  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  mv "$pkgdir"/usr/bin/sqlformat{,2}
}
