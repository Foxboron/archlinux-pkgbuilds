# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Jonathan Kotta <jpkotta AT gmail DOT com>

pkgname=python-sqlparse
pkgver=0.4.1
pkgrel=3
pkgdesc='Non-validating SQL parser for Python'
arch=('any')
url=https://github.com/andialbrecht/sqlparse
license=('BSD')
depends=('python')
makedepends=('python-setuptools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/andialbrecht/sqlparse/archive/$pkgver.tar.gz")
sha256sums=('f75cdec98a4cc8296890279d744e1ae8618bb14dbad77e3d0637f0d7bb5d6535')

build() {
  cd sqlparse-$pkgver
  python setup.py build
}

package() {
  cd sqlparse-$pkgver
  python setup.py install --prefix=/usr --root="$pkgdir" --skip-build
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
