# Maintainer: Maxim Baz <$pkgname at maximbaz dot com>
# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: fenuks

pkgname=python-jsonrpc-server
pkgver=0.4.0
pkgrel=5
pkgdesc="Python library implementing asynchronous JSON RPC server"
arch=("any")
url="https://github.com/palantir/python-jsonrpc-server"
license=("MIT")
depends=("python" "python-future" "python-ujson")
makedepends=("python" "python-setuptools")
checkdepends=("python-pytest" "python-pytest-runner" "python-pytest-cov"  "python-pyflakes" "python-mock")
source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        "fix-test.patch")
sha256sums=('62c543e541f101ec5b57dc654efc212d2c2e3ea47ff6f54b2e7dcb36ecf20595'
            '7f6dcef7052f666bafccaa2d43c037a511aaad8ee289fb65130eae809cf88072')

prepare(){
    cd "${pkgname}-${pkgver}"
    patch -p1 -i ../fix-test.patch
}

build() {
    cd "${pkgname}-${pkgver}"
    python setup.py build
}

check() {
    cd "${pkgname}-${pkgver}"
    py.test
}

package() {
    cd "${pkgname}-${pkgver}"
    python setup.py install --root="${pkgdir}/" --optimize=1 --skip-build
    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=4 sw=4 et:
