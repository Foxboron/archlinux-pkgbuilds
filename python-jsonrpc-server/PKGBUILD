# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: fenuks

pkgbase=python-jsonrpc-server
pkgname=(python-jsonrpc-server python2-jsonrpc-server)
pkgver=0.1.0
pkgrel=1
pkgdesc="An implementation of the Language Server Protocol for Python"
arch=("any")
url="https://github.com/palantir/python-jsonrpc-server"
license=("MIT")
makedepends=("python" "python-setuptools"
             "python2" "python2-setuptools")
checkdepends=("python-pytest" "python-pytest-runner" "python-pytest-cov"  "python-pyflakes" "python-mock"
              "python2-pytest" "python2-pytest-runner" "python2-pytest-cov" "python2-pyflakes" "python2-mock" "python2-futures")
source=("$pkgname-$pkgver.tar.gz::https://github.com/palantir/${pkgname}/archive/${pkgver}.tar.gz")
sha256sums=('4567f6af2b0691148308e50ab5ea6020eae231225fa72d7b34726900ab0dab83')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    python setup.py build
    python2 setup.py build
}

check() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    py.test
    py.test2
}

package_python-jsonrpc-server() {
    depends=("python" "python-future")
    cd "${pkgbase}-${pkgver}"
    python setup.py install --root="${pkgdir}/" --optimize=1 --skip-build
    install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-jsonrpc-server() {
    depends=("python2" "python2-future")
    cd "${pkgbase}-${pkgver}"
    python2 setup.py install --root="${pkgdir}/" --optimize=1 --skip-build
    install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
