# Maintainer: Maxim Baz <$pkgname at maximbaz dot com>
# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: fenuks

pkgname=python-jsonrpc-server
pkgver=0.3.2
pkgrel=1
pkgdesc="Python library implementing asynchronous JSON RPC server"
arch=("any")
url="https://github.com/palantir/python-jsonrpc-server"
license=("MIT")
depends=("python" "python-future" "python-ujson")
makedepends=("python" "python-setuptools")
checkdepends=("python-pytest" "python-pytest-runner" "python-pytest-cov"  "python-pyflakes" "python-mock")
source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        'python-3.8.patch')
sha256sums=('05bcf26eac4c98c96afec266acdf563d8f454e12612da9a3f9aabb66c46daf35'
            '718d791bd233eeb9c3876f5e18d32e28e75d919375c4c2dd511f8a4e1023b479')

prepare() {
    cd "${pkgname}-${pkgver}"
    patch -Np1 -i ../python-3.8.patch
}

build() {
    cd "${pkgname}-${pkgver}"
    python setup.py build
}

check() {
    cd "${pkgname}-${pkgver}"
    py.test
}

package() {
    cd "${pkgname}-${pkgver}"
    python setup.py install --root="${pkgdir}/" --optimize=1 --skip-build
    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=4 sw=4 et:
