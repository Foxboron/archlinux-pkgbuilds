# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Pierre Neidhardt <ambrevar@gmail.com>
# Contributor: Florian Bruhin (The Compiler) <archlinux.org@the-compiler.org>

pkgname=qutebrowser
pkgver=1.4.2
pkgrel=2
pkgdesc="A keyboard-driven, vim-like browser based on PyQt5"
arch=("any")
url="http://www.qutebrowser.org/"
license=("GPL")
depends=("python-attrs" "python-jinja" "python-pygments" "python-pypeg2"
	"python-pyqt5>=5.7" "python-yaml" "qt5-base>=5.7.1" "qt5-webengine")
makedepends=("asciidoc" "python-setuptools")
optdepends=("gst-libav: media playback"
	"gst-plugins-base: media playback"
	"gst-plugins-good: media playback"
	"gst-plugins-bad: media playback"
	"gst-plugins-ugly: media playback"
	"pdfjs: displaying PDF in-browser"
	"qt5-webkit: alternative backend")
options=(!emptydirs)
source=("https://github.com/qutebrowser/qutebrowser/releases/download/v$pkgver/qutebrowser-$pkgver.tar.gz"
	    "https://github.com/qutebrowser/qutebrowser/releases/download/v$pkgver/qutebrowser-$pkgver.tar.gz.asc"
        "initiator.patch")
validpgpkeys=("E04E560002401B8EF0E76F0A916EB0C8FD55A072")
sha256sums=('fd5d47b0e45e40b1348caf37e8ac304256d453d147f7a930193d3c4aeb21d2de'
            'SKIP'
            '44654dc6515245ae05597ad9b8a3917e9391210dfc4fd61210153502b49fd0a3')

prepare() {
    cd $pkgname-$pkgver
    patch -Np1 -i "${srcdir}/initiator.patch"
}

build() {
	cd "$pkgname-$pkgver"
	a2x -f manpage doc/qutebrowser.1.asciidoc
	python setup.py build
}

package() {
	cd "$pkgname-$pkgver"
	make -f misc/Makefile DESTDIR="$pkgdir" PREFIX=/usr install
}
