# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Jeff Mickey <jeff@archlinux.org>
# Contributor: Pappa <jcberg@gmail.com >

pkgname=python-xlib
pkgver=0.29
pkgrel=1
pkgdesc="A fully functional X client library for Python programs"
url="https://github.com/python-xlib/python-xlib"
arch=('any')
license=('LGPL')
depends=('python-six')
makedepends=('python-setuptools-scm')
checkdepends=('python-mock' 'xorg-xauth' 'xorg-server-xvfb' 'python-nose')
source=("$pkgname-$pkgver.tar.gz::https://github.com/python-xlib/python-xlib/archive/$pkgver.tar.gz")
sha256sums=('ae59dc56ead2f7280fc0f24310409e8250a79e930e06c703f8dd7b080cd4b4b6')

build(){
  cd "$pkgname-$pkgver"
  python setup.py build
}

check(){
  cd "$pkgname-$pkgver"
  python ./runtests.py
}

package_python-xlib() {
  cd "$pkgname-$pkgver"
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
