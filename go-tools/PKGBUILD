# Maintainer: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Maintainer: Morten Linderud <foxboron@archlinux.org>

pkgname=go-tools
epoch=2
pkgver=1.14+3923+c00d67ef2
pkgrel=1
pkgdesc='Developer tools for the Go programming language'
arch=(x86_64)
url='https://golang.org/'
license=(BSD)
depends=(glibc)
makedepends=(git go)
_commit=c00d67ef29d03e4dd86a6fb6a75dba1b71a5e294
_net_commit=ef20fe5d793301b553005db740f730d87993f778
source=(go-tools::git+https://go.googlesource.com/tools#commit=$_commit
        golang-net::git+https://github.com/golang/net#commit=$_net_commit)
md5sums=('SKIP'
         'SKIP')

_tools=(benchcmp callgraph compilebench cover digraph eg fiximports
        go-contrib-init godex godoc goimports gomvpkg gorename gotype 
        goyacc guru html2article present ssadump stringer gopls
        toolstash)

pkgver() {
  local _gover=1.14
  cd go-tools
  printf "%s+%s+%s" $_gover "$(git rev-list --count HEAD)" \
    "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p gopath/src/golang.org/x
  cp -rf go-tools gopath/src/golang.org/x/tools
  cp -rf golang-net gopath/src/golang.org/x/net
}

build() {
  export GOPATH="$srcdir/gopath"
  cd gopath/src/golang.org/x/tools
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
  go install -v -a ./cmd/...
}

check() {
  export GOPATH="$srcdir/gopath"
  cd gopath/src/golang.org/x/tools
  # Needs to be updated and godoc is failing the test suite
  # See https://github.com/golang/go/issues/35690
  go test ./cmd/... || true
}

package() {
  for tool in ${_tools[@]}; do
    install -Dm755 gopath/bin/$tool "$pkgdir/usr/bin/$tool"
  done

  install -Dm644 go-tools/LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
