# Maintainer: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Maintainer: Morten Linderud <foxboron@archlinux.org>

pkgname=go-tools
epoch=2
pkgver=1.12+2915+49d818b07
pkgrel=1
pkgdesc='Developer tools for the Go programming language'
arch=(x86_64)
url='http://golang.org/'
license=(BSD)
depends=(glibc)
makedepends=(git go-pie)
_commit=49d818b07734fb3230f2d00bc5d4453348348d24
_net_commit=16b79f2e4e95ea23b2bf9903c9809ff7b013ce85
source=(go-tools::git+https://go.googlesource.com/tools#commit=$_commit
        golang-net::git+https://github.com/golang/net#commit=$_net_commit)
md5sums=('SKIP'
         'SKIP')

_tools=(benchcmp callgraph compilebench cover digraph eg fiximports
        go-contrib-init godex godoc goimports gomvpkg gorename gotype 
        goyacc guru html2article present ssadump stringer 
        toolstash)

pkgver() {
  local _gover=1.12
  cd go-tools
  printf "%s+%s+%s" $_gover "$(git rev-list --count HEAD)" \
    "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p gopath/src/golang.org/x
  cp -rf go-tools gopath/src/golang.org/x/tools
  cp -rf golang-net gopath/src/golang.org/x/net
}

build() {
  export GOPATH="$srcdir/gopath"
  cd gopath/src/golang.org/x/tools
  go install -v -a -gcflags "all=-trimpath=${GOPATH}" -asmflags "all=-trimpath=${GOPATH}" ./cmd/...
}

check() {
  export GOPATH="$srcdir/gopath"
  cd gopath/src/golang.org/x/tools
  go test ./cmd/...
}

package() {
  for tool in ${_tools[@]}; do
    install -Dm755 gopath/bin/$tool "$pkgdir/usr/bin/$tool"
  done

  install -Dm644 go-tools/LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
