# Maintainer: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Maintainer: Morten Linderud <foxboron@archlinux.org>

pkgname=go-tools
epoch=2
pkgver=1.11+2495+9e9bf16a
pkgrel=1
pkgdesc='Developer tools for the Go programming language'
arch=(x86_64)
url='http://golang.org/'
license=(BSD)
depends=(glibc)
makedepends=(git go-pie)
_commit=9e9bf16a4efe175e2867e1661feefa6278e4e14e
_net_commit=c39426892332e1bb5ec0a434a079bf82f5d30c54
source=(git+https://go.googlesource.com/tools#commit=$_commit
        git+https://github.com/golang/net#commit=$_net_commit)
md5sums=('SKIP'
         'SKIP')

_tools=(benchcmp callgraph compilebench cover digraph eg fiximports
        go-contrib-init godex godoc goimports gomvpkg gorename gotype 
        goyacc guru heapview html2article present ssadump stringer 
        tip toolstash)

pkgver() {
  local _gover=1.11
  cd tools
  printf "%s+%s+%s" $_gover "$(git rev-list --count HEAD)" \
    "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p gopath/src/golang.org/x
  cp -rf tools net gopath/src/golang.org/x/
}

build() {
  export GOPATH="$srcdir/gopath"
  cd gopath/src/golang.org/x/tools
  go install -v -a -gcflags "all=-trimpath=${GOPATH}" -asmflags "all=-trimpath=${GOPATH}" ./cmd/...
}

check() {
  export GOPATH="$srcdir/gopath"
  cd gopath/src/golang.org/x/tools
  go test ./cmd/...
}

package() {
  for tool in ${_tools[@]}; do
    install -Dm755 gopath/bin/$tool "$pkgdir/usr/bin/$tool"
  done

  install -Dm644 tools/LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
