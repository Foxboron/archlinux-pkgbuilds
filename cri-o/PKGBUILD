# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Tony Lambiris <tony@libpcap.net>

pkgname=cri-o
pkgver=1.22.1
pkgrel=1
pkgdesc="Open Container Initiative-based implementation of Kubernetes Container Runtime Interface"
arch=('x86_64')
url="https://github.com/cri-o/cri-o"
license=('Apache')
depends=('cni-plugins' 'conmon' 'crun' 'conntrack-tools' 'containers-common'
'device-mapper' 'glibc' 'iproute2' 'iptables' 'runc')
makedepends=('apparmor' 'btrfs-progs' 'gpgme' 'go' 'go-md2man' 'libassuan'
'libseccomp' 'ostree')
optdepends=('apparmor: for apparmor integration'
            'btrfs-progs: for btrfs support')
provides=('container-runtime')
backup=('etc/crio/crio.conf'
        'etc/cni/net.d/100-crio-bridge.conf'
        'etc/cni/net.d/199-crio-loopback.conf'
)
# configuration override and hook directories should exist
options=('emptydirs')
install="${pkgname}.install"
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/${pkgname}/${pkgname}/archive/v${pkgver}.tar.gz"
        "modules-${pkgname}.conf"
        "sysctl-${pkgname}.conf"
        "${pkgname}-1.21.0-pinns_pie.patch")
sha512sums=('1cd1cad654cdc6575c2a1cbec1ebc096f90bf981fe4c5a25dd829a16e5405e762e70c7d7039ca0d3bb5121eb06a876e71256634025f656beef7d98b3d15b23ed'
            '85ee1310cb36c85c42b4068a9549a3ef72b856cd61b2c1036c3e871ef43a69ed80b43599ad94ce5b069ddd823e730596bb3d3875d4ba8cd77c4cc1985335ffff'
            'fbcc3c29d5d3dedf56ea96577dec50cb2656b4a22f5a82bf2d600ae20c6b113d35d5db116bac5520398d47a22d40835f56b86433c5713924060e1a7018d297ec'
            'dbe51c558a50212065de97794b87af3205b3624b43d073f70be245528a309d2f2bf5c6f6a15e4137d9f3e737a38166d08fed87ede27e33fad843b3b01194eb34')
b2sums=('2dae63870c77858bb3a01c55f00e97c0dae4e341301fdbfb69f320f3f3e5dbfc40fc2fff40b56b8e073063d919a149bdd21e70db62e9806c126e3c167ec08a59'
        '2771c64b45876d728fc139aa90754df8aadb07e14a7e1126a1488dfcbad7ebcbca923cb0230c4b87acdc6dad8243af2ad06ef4ce587d5c71fc200e40835d8b8d'
        'c8a2133df6575c54eb9bd0a89a0c6c764c0cd456bff7338c4a2e1ef4f70a5a778d5a05c335fcf2491ed55e15fc36c677853c0bc8ab66d673f1950fa2c7695f6e'
        '06558ff67ba4dd6386ada8d50883b7fbc6a74ee6110ad9dc1ee167c43ba48c2c4f15c6a3f7bcabdf6363c235fc482d6fad077821188d65489249ee5764fe7680')

prepare() {
  cd "${pkgname}-${pkgver}"
  # make sure that /run instead of /var/run is used
  sed -e 's|/var/run|/run|g' \
      -i crictl.yaml pkg/config/config{,_unix}.go \
      docs/*.md crio-umount.conf \
      vendor/github.com/containers/conmon/runner/config/config_unix.go
  # make sure that /usr/bin is used in systemd units
  sed -e 's|/usr/local|/usr|g' \
      -i contrib/systemd/*.service
  # set the correct default PATH for cni-plugins
  sed -e 's|/opt/cni/bin/|/usr/lib/cni/|g' \
      -i pkg/config/config_unix.go
  # do not statically link pinns and enable PIE
  patch -Np1 -i "../${pkgname}-1.21.0-pinns_pie.patch"
}

build() {
  cd "${pkgname}-${pkgver}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"

  make GO_MD2MAN=/usr/bin/go-md2man
}

package() {
  depends+=('libdevmapper.so' 'libgpgme.so' 'libseccomp.so')
  cd "${pkgname}-${pkgver}"
  # makefile is mixing DESTDIR and PREFIX
  make install install.systemd GO_MD2MAN=/usr/bin/go-md2man DESTDIR="${pkgdir}" PREFIX="${pkgdir}/usr"
  # install CNI configs
  install -vDm 644 contrib/cni/10-crio-bridge.conf "${pkgdir}/etc/cni/net.d/100-crio-bridge.conf"
  install -vDm 644 contrib/cni/99-loopback.conf "${pkgdir}/etc/cni/net.d/199-crio-loopback.conf"
  # modules
  install -vDm 644 "../modules-${pkgname}.conf" "${pkgdir}/usr/lib/modules-load.d/cri-o.conf"
  # sysctl
  install -vDm 644 "../sysctl-${pkgname}.conf" "${pkgdir}/usr/lib/sysctl.d/90-cri-o.conf"
  # docs
  install -vDm 644 {CONTRIBUTING,README,awesome,code-of-conduct,install,transfer,tutorial}.md \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
  install -vDm 644 tutorials/*.md \
    -t "${pkgdir}/usr/share/doc/${pkgname}/tutorials/"
  install -vDm 644 contrib/cni/*.{conf,md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/examples/cni/"
  install -vDm 644 contrib/metrics-exporter/{Containerfile,*.{yaml,gif,json,go}} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/examples/metrics-exporter/"
}
