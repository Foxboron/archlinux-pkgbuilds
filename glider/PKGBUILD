# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Morten Linderud <foxboron@archlinux.org>

pkgname=glider
pkgver=0.15.1
pkgrel=1
pkgdesc="A forward proxy with multiple protocols support"
arch=('x86_64')
url="https://github.com/nadoo/glider"
license=('GPL')
depends=('glibc')
makedepends=('go' 'git')
source=("$pkgname-$pkgver.tar.gz::https://github.com/nadoo/glider/archive/v$pkgver.tar.gz")
sha512sums=('7d54a52ffa6f0b7fa05d0188f475e67dd264cd545aca14252d134c2c1843310286a896b634534ae6534d37bbd07f74342390241a8e1cdaefaa086485bc453d03')

build() {
  cd glider-$pkgver
  export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external"
  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  go build -o glider .

}

check(){
  cd glider-$pkgver
  go test -v ./...
}

package() {
  cd glider-$pkgver
  install -Dm755 glider "$pkgdir"/usr/bin/glider
  install -Dm644 config/glider.conf.example "$pkgdir"/etc/glider/glider.conf.example
  install -Dm644 systemd/glider@.service "$pkgdir"/usr/lib/systemd/system/glider@.service
}
