# Maintainer: Thore Bödecker <foxxx0@archlinux.org>
# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Maintainer: Filipe Laíns (FFY00) <lains@archlinux.org>
# Contributor: Pierre Neidhardt <ambrevar@gmail.com>
# Contributor: Hermann Zahnweh (eigengrau)
# Contributor: Andy Weidenbaum <archbaum@gmail.com>
# Contributor: foalsrock <foalsrock at gmail dot-com>
# Contributor: jebaum <jebaum at ucla dot edu>

pkgname=fzf
pkgver=0.21.0.1
# Remove after next release
_pkgver="${pkgver%.*}"
_patch="${pkgver##*.}"
pkgrel=1
pkgdesc='Command-line fuzzy finder'
arch=('x86_64')
url='https://github.com/junegunn/fzf'
license=('MIT')
depends=('bash')
makedepends=('git' 'go-pie')
optdepends=('fish: fish keybindings'
            'tmux: fzf-tmux script for launching fzf in a tmux pane'
            'vim: plugin'
            'zsh: zsh keybindings')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$_pkgver-$_patch.tar.gz")
sha512sums=('4cdbeba23ae2434e090504321ba3e475a59a541c60d14132ce43c001deffa03e5ff47029dd7dea9651a0da0b8d2d155095a14d3e16d3125b23f4a8887c3ce828')

build() {
	cd $pkgname-$_pkgver-$_patch
	export CGO_LDFLAGS="$LDFLAGS"
	export GOFLAGS="-buildmode=pie -trimpath"
	go build -o fzf .
}

check() {
	cd $pkgname-$_pkgver-$_patch
	go test ./...
}

package() {
	cd $pkgname-$_pkgver-$_patch

	## Man page
	install -Dm644 man/man1/fzf.1 "$pkgdir"/usr/share/man/man1/fzf.1

	## License
	install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/fzf/LICENSE

	## Binaries
	install -dm755 "$pkgdir"/usr/bin
	install -m755 fzf bin/fzf-tmux "$pkgdir"/usr/bin/

	## Completion and keybindings
	install -dm755 "$pkgdir"/usr/share/fzf
	install -m644 shell/*.bash shell/*.zsh "$pkgdir"/usr/share/fzf

	## Fish keybindings
	install -Dm644 shell/key-bindings.fish "$pkgdir"/usr/share/fish/vendor_functions.d/fzf_key_bindings.fish

	## Vim plugin
	install -Dm644 plugin/fzf.vim "$pkgdir"/usr/share/vim/vimfiles/plugin/fzf.vim
}

