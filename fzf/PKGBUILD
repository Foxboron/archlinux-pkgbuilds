# Maintainer: Thore Bödecker <foxxx0@archlinux.org>
# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Maintainer: Filipe Laíns (FFY00) <lains@archlinux.org>
# Contributor: Pierre Neidhardt <ambrevar@gmail.com>
# Contributor: Hermann Zahnweh (eigengrau)
# Contributor: Andy Weidenbaum <archbaum@gmail.com>
# Contributor: foalsrock <foalsrock at gmail dot-com>
# Contributor: jebaum <jebaum at ucla dot edu>

pkgname=fzf
pkgver=0.30.0
pkgrel=3
pkgdesc='Command-line fuzzy finder'
arch=('x86_64')
url='https://github.com/junegunn/fzf'
license=('MIT')
depends=('bash')
makedepends=('auth-tarball-from-git' 'go')
optdepends=('fish: fish keybindings'
            'tmux: fzf-tmux script for launching fzf in a tmux pane'
            'vim: plugin'
            'zsh: zsh keybindings')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/junegunn/fzf/archive/refs/tags/${pkgver}.tar.gz
        junegunn.pgp)
sha512sums=('cebb685a7fa00bdff07751d0021edde82f0ff3bf705eff82f94f6bf57c5e008eb6894d023ed7b14378f1d92fa2394192c12b720c6f711b1a414438fe3f8b4ec3'
            '83a14444802e64a0de90599ca7d4fb1ea4575dbadf56eb042ce69e1b3f52e6821f9914d34d85346ee9e21fbf69e2fee5bc5c6ea783b52fdfb90d72b14b37f40b')

prepare() {
	cd "${pkgname}-${pkgver}"
	sed -i 's/-w /-w -linkmode external /' Makefile
	auth-tarball-from-git --keyring ../junegunn.pgp \
		--tag "${pkgver}" "${url}" "../${pkgname}-${pkgver}.tar.gz"
}

build() {
	cd "${pkgname}-${pkgver}"
	export CGO_LDFLAGS="${LDFLAGS}"
	export CGO_CFLAGS="${CFLAGS}"
	export CGO_CPPFLAGS="${CPPFLAGS}"
	export CGO_CXXFLAGS="${CXXFLAGS}"
	export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
	FZF_VERSION="${pkgver}" FZF_REVISION=tarball make
}

check() {
	cd "${pkgname}-${pkgver}"
	export CGO_LDFLAGS="${LDFLAGS}"
	export CGO_CFLAGS="${CFLAGS}"
	export CGO_CPPFLAGS="${CPPFLAGS}"
	export CGO_CXXFLAGS="${CXXFLAGS}"
	export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
	go test ./...
}

package() {
	cd "${pkgname}-${pkgver}"

	## Man page
	install -Dm644 man/man1/fzf.1 "$pkgdir"/usr/share/man/man1/fzf.1
	install -Dm644 man/man1/fzf-tmux.1 "$pkgdir"/usr/share/man/man1/fzf-tmux.1

	## License
	install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/fzf/LICENSE

	## Binaries
	install -dm755 "$pkgdir"/usr/bin
	install -m755 bin/fzf-tmux "$pkgdir"/usr/bin/
	install -Dm755 target/fzf-linux_amd64 "$pkgdir"/usr/bin/fzf

	## Completion and keybindings
	install -dm755 "$pkgdir"/usr/share/fzf
	install -m644 shell/*.bash shell/*.zsh "$pkgdir"/usr/share/fzf

	## Fish keybindings
	install -Dm644 shell/key-bindings.fish "$pkgdir"/usr/share/fish/vendor_functions.d/fzf_key_bindings.fish

	## Vim plugin
	install -Dm644 doc/fzf.txt "$pkgdir"/usr/share/vim/vimfiles/doc/fzf.txt
	install -Dm644 plugin/fzf.vim "$pkgdir"/usr/share/vim/vimfiles/plugin/fzf.vim
}

