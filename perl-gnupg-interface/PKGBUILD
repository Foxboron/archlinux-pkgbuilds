# Maintainer: Bruno Pagani <archange@archlinux.org>
# Maintainer: Morten Linderud <foxboron@archlinux.org>

_pkg=GnuPG-Interface
pkgname=perl-${_pkg,,}
pkgver=1.01
pkgrel=1
pkgdesc="Object-oriented methods for interacting with GnuPG"
arch=('any')
url="https://metacpan.org/release/${_pkg}"
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('gnupg' 'perl-math-bigint' 'perl-moo' 'perl-moox-handlesvia' 'perl-moox-late')
makedepends=('perl-module-install')
source=("https://cpan.metacpan.org/authors/id/B/BP/BPS/${_pkg}-${pkgver}.tar.gz")
sha512sums=('fccf0112cabed4e8263fe6152eaca5250821d120c29272432d1522ff0f22841d289bd5f2b86e587915a3fa7d255e9b70a58e95dd2c9aeb6e863adf991e464510')

build() {
    cd ${_pkg}-${pkgver}
    export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
    perl Makefile.PL
    make
}

check() {
    cd ${_pkg}-${pkgver}
    # 19 tests failing, likely because we are in a chroot
    make test
}

package() {
    cd ${_pkg}-${pkgver}
    make INSTALLDIRS=vendor DESTDIR="$pkgdir" install
}
