# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Adrian Perez de Castro <aperez@igalia.com>

pkgdesc='FUSE implementation of overlayfs'
pkgname=fuse-overlayfs
pkgver=1.1.1
pkgrel=1
arch=(x86_64)
url=https://github.com/containers/fuse-overlayfs
license=(GPL3)
depends=(fuse3)
makedepends=(git)
_commit=a7dd94374bab5d064975a974de961bcb4785b97d # 1.1.1
#source=("git+https://github.com/containers/fuse-overlayfs.git#commit=$_commit?signed")
source=("git+https://github.com/containers/fuse-overlayfs.git#tag=v$pkgver?signed")
validpgpkeys=('AC404C1C0BF735C63FF4D562263D6DF2E163E1EA')
sha512sums=('SKIP')

build () {
	cd "${pkgname}"
	autoreconf -fis
	./configure \
		--prefix=/usr \
		--sbindir=/usr/bin
	make
}

check(){
	cd "${pkgname}"
	make check
}

package (){
	cd "${pkgname}"
	make DESTDIR="${pkgdir}" install
}
