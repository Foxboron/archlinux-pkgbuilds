# Maintainer: Kyle Keen <keenerd@gmail.com>
# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Jelle van der Waa <jelle@vdwaa.nl>
# Contributor: Pierre Chapuis <catwell@archlinux.us>
# Contributor: Stefano Esposito <ragnarok@email.it>

pkgname=python-irc
pkgver=20.0.0
pkgrel=1
pkgdesc="IRC (Internet Relay Chat) protocol client library for Python"
depends=('python-six' 'python-pytz' 'python-more-itertools' 'python-jaraco'
         'python-tempora')
makedepends=('python-build' 'python-installer' 'python-setuptools-scm' 'python-wheel')
checkdepends=('python-pytest' 'python-pytest-flake8' 'python-pytest-black'
              'python-pytest-cov' 'python-pytest-mypy')
arch=('any')
url="https://pypi.python.org/pypi/irc"
license=('LGPL')
source=("https://files.pythonhosted.org/packages/source/i/irc/irc-$pkgver.tar.gz")
sha256sums=('59acb8d69d61a0cbd290e77e6ff10a8c7f2201fb8c7b7d5a195b5883d0c40b0a')

build() {
  cd "irc-$pkgver"
  export LC_ALL=en_US.UTF-8
  python -m build --wheel --no-isolation
}

check() {
  cd "irc-$pkgver"
  export LC_ALL=en_US.UTF-8
  pytest .
}

package() {
  cd "irc-$pkgver"
  export LC_ALL=en_US.UTF-8
  python -m installer --destdir="$pkgdir" dist/*.whl
# Remove sample scripts installed in generic namespace
  rm -r "$pkgdir"/usr/lib/python*/site-packages/scripts
}
