# Maintainer: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Maintainer: Morten Linderud <foxboron@archlinux.org>

pkgname=buildah
pkgver=1.12.0
pkgrel=1
pkgdesc="A tool which facilitates building OCI images"
arch=(x86_64)
url="https://github.com/containers/buildah"
license=(Apache)
depends=(ostree runc skopeo slirp4netns)
makedepends=(go-pie git bash-bats btrfs-progs device-mapper gpgme libassuan
             bzip2 go-md2man runc skopeo)
_commit=6cc46567d1c7011bc99ac99ecc6239f2d3df0aa9 # tags/v1.12.0
source=(git://github.com/containers/buildah.git#commit=$_commit)
md5sums=('SKIP')

pkgver() {
  cd buildah
  git describe --tags | sed 's/^[vV]//;s/-/+/g'
}

build() {
  cd $pkgname 
  export GOFLAGS="-trimpath"
  export CGO_LDFLAGS="$LDFLAGS"
  make PREFIX=/usr BUILDTAGS="containers_image_ostree_stub ostree seccomp"
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" PREFIX=usr install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
