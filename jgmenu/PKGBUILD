# Maintainer: Mortne Linderu <foxboron@archlinux.org>
# Contributor: Johan Malm <jgm323 at gmail dot com>

pkgname=jgmenu
pkgver=3.5
pkgrel=1
pkgdesc="Small X11 menu intended to be used with openbox and tint2"
arch=('i686' 'x86_64')
url="https://www.github.com/johanmalm/jgmenu"
license=('GPL')
depends=('libx11' 'cairo' 'pango' 'libxrandr' 'librsvg' 'libxml2' 'glib2' 'menu-cache' 'python')
source=("${pkgname}-${pkgver}.tar.gz::https://www.github.com/johanmalm/jgmenu/archive/v${pkgver}.tar.gz")
sha256sums=('4456fd24fb522439bb900b46d8e77087fadb7dd38676879883ddec9bdcb57993')

build() {
	cd "$pkgname-$pkgver"
	make
}

check(){
	cd "$pkgname-$pkgver"
	make check
	make test
}

package() {
	cd "$pkgname-$pkgver"
	make DESTDIR="$pkgdir" prefix=/usr install
}
