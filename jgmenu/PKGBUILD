# Maintainer: Mortne Linderu <foxboron@archlinux.org>
# Contributor: Johan Malm <jgm323 at gmail dot com>

pkgname=jgmenu
pkgver=4.0.1
pkgrel=1
pkgdesc="Small X11 menu intended to be used with openbox and tint2"
arch=('i686' 'x86_64')
url="https://www.github.com/johanmalm/jgmenu"
license=('GPL')
depends=('libx11' 'cairo' 'pango' 'libxrandr' 'librsvg' 'libxml2' 'glib2' 'menu-cache' 'python')
source=("${pkgname}-${pkgver}.tar.gz::https://www.github.com/johanmalm/jgmenu/archive/v${pkgver}.tar.gz")
sha256sums=('00093e8538ed9205ce7e2103f221aa9a0b7574fa7e209297b076754dbfc5c4c7')

build() {
	cd "$pkgname-$pkgver"
	./configure --prefix=/usr --with-lx --with-pmenu
	make
}

check(){
	cd "$pkgname-$pkgver"
	make check
	make test
}

package() {
	cd "$pkgname-$pkgver"
	make DESTDIR="$pkgdir" prefix=/usr install
}
