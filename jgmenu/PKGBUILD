# Maintainer: Mortne Linderu <foxboron@archlinux.org>
# Contributor: Johan Malm <jgm323 at gmail dot com>

pkgname=jgmenu
pkgver=4.1.0
pkgrel=1
pkgdesc="Simple, independent, contemporary-looking X11 menu, designed for scripting, ricing and tweaking"
arch=('i686' 'x86_64')
url="https://www.github.com/johanmalm/jgmenu"
license=('GPL')
depends=('libx11' 'cairo' 'pango' 'libxrandr' 'librsvg' 'libxml2' 'glib2' 'menu-cache' 'python')
source=("${pkgname}-${pkgver}.tar.gz::https://www.github.com/johanmalm/jgmenu/archive/v${pkgver}.tar.gz")
sha256sums=('0a693db7295200b22f24e0d8ab3986353911aa214cd8199f3ec583710dc72a92')

build() {
	cd "$pkgname-$pkgver"
	./configure --prefix=/usr --with-lx --with-pmenu
	make
}

check(){
	cd "$pkgname-$pkgver"
	make check
	make test
}

package() {
	cd "$pkgname-$pkgver"
	make DESTDIR="$pkgdir" prefix=/usr install
}
