# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Yunxiang Li <shironeko@waifu.club>

pkgname=poke
pkgver=1.1
pkgrel=1
pkgdesc="Interactive, extensible editor for binary data."
arch=(x86_64)
url="http://www.jemarch.net/poke.html"
license=('GPL3')
depends=('gc' 'readline' 'gettext' 'json-c')
makedepends=('help2man')
checkdepends=('dejagnu')
source=("https://ftp.gnu.org/gnu/poke/poke-${pkgver}.tar.gz"{,.sig})
validpgpkeys=('BDFA5717FC1DD35C2C3832A23EF90523B304AF08')
sha256sums=('cd68df63c741b4160b12cbea02f27c47158279466e3443930d2535a452c06ad6'
            'SKIP')

build() {
    cd "${pkgname}-${pkgver}"
    # TODO: This vendors two dependencies I think.
    ./configure --prefix=/usr
    make
}

check() {
    cd "${pkgname}-${pkgver}"
    make -k check
}

package() {
    cd "${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}/" install
}
