# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Andy Weidenbaum <archbaum@gmail.com>

pkgbase=python-jsonrpclib-pelix
pkgname=(python-jsonrpclib-pelix python2-jsonrpclib-pelix)
pkgver=0.3.2
pkgrel=1
pkgdesc="A Python JSON-RPC over HTTP that mirrors xmlrpclib syntax"
arch=(any)
makedepends=(python-setuptools python2-setuptools)
url="https://github.com/tcalmant/jsonrpclib"
license=('Apache')
source=(https://pypi.io/packages/source/j/jsonrpclib-pelix/jsonrpclib-pelix-$pkgver.tar.gz)
sha256sums=('14d288d1b3d3273cf96a729dd21a2470851c4962be8509f3dd62f0137ff90339')

prepare() {
  cp -r jsonrpclib-pelix-$pkgver jsonrpclib-pelix-$pkgver-py2
  cd jsonrpclib-pelix-$pkgver-py2
  find -name '*.py' | xargs sed -e 's|/usr/bin/python|/usr/bin/python2|' -e 's|/usr/bin/env python|/usr/bin/env python2|' -i
}

build() {
  cd jsonrpclib-pelix-$pkgver
  python setup.py build

  cd ../jsonrpclib-pelix-$pkgver-py2
  python2 setup.py build
}

package_python-jsonrpclib-pelix() {
  depends=(python)
  conflicts=(python-jsonrpclib)
  cd jsonrpclib-pelix-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-jsonrpclib-pelix() {
  depends=(python2)
  conflicts=(python2-jsonrpclib)
  cd jsonrpclib-pelix-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}

