# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: bonob <bonob at fastmail dot com>

pkgname=python-pytoolconfig
_name=${pkgname#python-}
pkgver=1.2.2
pkgrel=3
pkgdesc="Manage configuration for python tools, such as black and rope."
arch=('any')
url="https://pypi.org/project/pytoolconfig/"
license=('LGPL3')
depends=('python' 'python-tomli' 'python-packaging')
makedepends=('python-build' 'python-installer' 'python-wheel' 'python-pdm')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz")
sha256sums=('2512a1f261a40e73cef2e58e786184261b60c802ae7ed01249342b1949ec3aa2')

build() {
	cd "$_name-$pkgver"
	python -m build --wheel --no-isolation
}

package() {
	cd "$_name-$pkgver"
	python -m installer --destdir="$pkgdir" dist/*.whl
}
