# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Maintainer: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>

pkgname=skopeo
pkgver=1.2.3
pkgrel=1
pkgdesc='A command line utility for various operations on container images and image repositories.'
arch=(x86_64)
url='https://github.com/projectatomic/skopeo'
license=(APACHE)
depends=(gpgme device-mapper containers-common)
makedepends=(go go-md2man btrfs-progs git systemd)
_commit=f15564f705d353835cdc0f69f7df92168b7d6667	#refs/tags/v1.2.3
source=(git+$url#commit=$_commit)
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

build() {
  cd $pkgname
  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
  make bin/skopeo BUILDTAGS='containers_image_ostree_stub'
  make docs
}

package() {
  cd $pkgname
  make PREFIX="/usr" DESTDIR="$pkgdir" install
  rm -rf "${pkgdir}/etc/containers/registries.d"
  #rm "${pkgdir}/etc/containers/policy.json"
  # TODO: Remove next release
  rm "${pkgdir}/policy.json"
}
