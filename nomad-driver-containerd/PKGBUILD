# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=nomad-driver-containerd
_pkgname=containerd-driver
pkgver=0.9
pkgrel=1
pkgdesc="A nomad taskdriver for containerd containers"
arch=('x86_64')
url="https://github.com/Roblox/nomad-driver-containerd"
license=('MPL2')
depends=('nomad' 'glibc' 'containerd')
makedepends=('go' 'git')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
b2sums=('8c909d12de962e1fe72ea6f4a4a1230a2108f15aba467f0d2495f4eec0bc3650e4f93ee6358d9e6b4be65bb99432423230e7a25d61116ad5114546aa58c05195')

prepare() {
	cd "$pkgname-$pkgver"
  mkdir build
}

build() {
	cd "$pkgname-$pkgver"
  go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-linkmode external -extldflags \"${LDFLAGS}\"" \
    -o build \
    .
}

package() {
	cd "$pkgname-$pkgver"
  install -vDm755 "build/$pkgname" "$pkgdir/usr/lib/nomad/plugins/$_pkgname"
}
