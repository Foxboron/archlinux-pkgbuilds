# Maintainer: Morten Linderud <foxboron@archlinux.org>
pkgname=restic
pkgver=0.9.5
pkgrel=3
pkgdesc="Fast, secure, efficient backup program"
arch=('x86_64')
url="https://restic.net"
license=('BSD')
depends=('glibc')
makedepends=('go-pie' 'git')
optdepends=('fuse2: mount archive')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/restic/restic/archive/v${pkgver}.tar.gz")
sha256sums=('e22208e946ede07f56ef60c1c89de817b453967663ce4867628dff77761bd429')

build(){
  cd "${pkgname}-${pkgver}"
  export GOPATH="${srcdir}"
  go build -mod=vendor -trimpath -ldflags "-X 'main.version=${pkgver}' -extldflags ${LDFLAGS}" ./cmd/...
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm755 restic "${pkgdir}/usr/bin/restic"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm644 -t "$pkgdir/usr/share/man/man1/" doc/man/*.1
  install -Dm644 doc/zsh-completion.zsh "$pkgdir/usr/share/zsh/site-functions/_restic"
  install -Dm644 doc/bash-completion.sh "$pkgdir/usr/share/bash-completion/completions/restic"

}
