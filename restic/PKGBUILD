# Maintainer: Morten Linderud <foxboron@archlinux.org>
pkgname=restic
pkgver=0.9.5
pkgrel=2
pkgdesc="Fast, secure, efficient backup program"
arch=('x86_64')
url="https://restic.net"
license=('BSD')
depends=('glibc')
makedepends=('go-pie')
optdepends=('fuse2: mount archive')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/restic/restic/archive/v${pkgver}.tar.gz")
sha256sums=('e22208e946ede07f56ef60c1c89de817b453967663ce4867628dff77761bd429')

prepare(){
  export GOPATH="${srcdir}"
  mkdir -p src/github.com/restic
  #mv "${pkgname}-${pkgver}"/vendor/* src/
  ln -rTsf "${pkgname}-${pkgver}" src/github.com/restic/restic
}

build(){
  cd "src/github.com/restic/restic"
  export GOPATH="${srcdir}"
  go install -ldflags "-X 'main.version=${pkgver}' -extldflags ${LDFLAGS}" -gcflags "all=-trimpath=${GOPATH}" -asmflags "all=-trimpath=${GOPATH}" ./cmd/...
}

package() {
  install -Dm755 bin/restic "${pkgdir}/usr/bin/restic"

  cd "${pkgname}-${pkgver}"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm644 -t "$pkgdir/usr/share/man/man1/" doc/man/*.1
  install -Dm644 doc/zsh-completion.zsh "$pkgdir/usr/share/zsh/site-functions/_restic"
  install -Dm644 doc/bash-completion.sh "$pkgdir/usr/share/bash-completion/completions/restic"

}
