# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: icasdri <icasdri at gmail dot com>
# Contributor: hexchain <i@hexchain.org>

pkgname=python-mypy_extensions
pkgver=0.4.1
pkgrel=1
pkgdesc='Experimental type system extensions for programs checked with the mypy typechecker'
url="http://www.mypy-lang.org/"
arch=('any')
license=('MIT')
depends=('python')
makedepends=('python-setuptools')
source=("$pkgname-$pkgver.tar.gz::https://pypi.org/packages/source/m/mypy_extensions/mypy_extensions-$pkgver.tar.gz")
sha256sums=('37e0e956f41369209a3d5f34580150bcacfabaa57b33a15c0b25f4b5725e0812')

build() {
    cd "mypy_extensions-$pkgver"
    python setup.py build
}

package() {
    cd "mypy_extensions-$pkgver"
    python setup.py install --prefix="/usr" --root="${pkgdir}" --optimize=1 --skip-build
    install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
