# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Morten Linderud <foxboron@archlinux.org>

pkgname=todoman
pkgver=4.0.1
pkgrel=2
pkgdesc="A simple, standards-based, cli todo (aka: task) manager"
arch=('any')
url="https://github.com/pimutils/todoman"
license=('ISC')
depends=('python-atomicwrites' 'python-click' 'python-click-log'
'python-configobj' 'python-dateutil' 'python-humanize' 'python-icalendar'
'python-parsedatetime' 'python-pyxdg' 'python-tabulate' 'python-urwid')
makedepends=('python-sphinx-autorun' 'python-sphinx-click' 'python-sphinx_rtd_theme' 'python-setuptools-scm')
checkdepends=('python-freezegun' 'python-hypothesis' 'python-pytest'
'python-pytest-runner')
optdepends=('bash-completion: auto-complete in bash'
            'python-click-repl: REPL command'
            'zsh-completions: auto-complete in zsh')
source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('606be4553330741bb664c2261ba33f9feb68023c38c4e5278febadae3c9267d5bcc0b63966a41ac3d2ec38f44c16f9368ec502d2ce1a782a9108ca4a38d9e04c')
b2sums=('029c09e5d29d9f3f9ffa6419f9506b9227bab1659dfec0287989ba6c2ce4043111cb82def2934f1d07fa51473725c7a8dfae6d87671ea1aaebba4e9099edcce8')

prepare() {
  cd "$pkgname-$pkgver"
  # remove interfering options to pytest
  sed -e '/addopts/d' -i setup.cfg
  sed -i 's/from validate/from configobj.validate/g' todoman/configuration.py
  sed -i 's/import validate/import configobj.validate as validate/' docs/source/conf.py
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
  PYTHONPATH="$PYTHONPATH:${PWD}" make -C docs man
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  # tests require timezone to be set to UTC
  # https://github.com/pimutils/todoman/issues/404
  TZ="UTC" pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --optimize=1 --root="${pkgdir}"
  # docs
  install -vDm 644 {AUTHORS,CHANGELOG,CODE_OF_CONDUCT,README}.rst \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
  # manpages
  install -vDm 644 docs/build/man/*.1 -t "$pkgdir/usr/share/man/man1/"
  # license
  # wrong name: https://github.com/pimutils/todoman/issues/319
  install -vDm 644 LICENCE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  # sample config
  install -vDm 644 "config.py.sample" \
    -t "${pkgdir}/usr/share/doc/${pkgname}/examples/"
  # completions
  install -vDm 644 contrib/completion/bash/_todo \
    "${pkgdir}/usr/share/bash-completion/completions/todo"
  install -vDm 644 contrib/completion/zsh/_todo \
    "${pkgdir}/usr/share/zsh/site-functions/_todo"
}
