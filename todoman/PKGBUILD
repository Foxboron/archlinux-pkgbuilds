# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Morten Linderud <foxboron@archlinux.org>

pkgname=todoman
pkgver=4.1.0
pkgrel=1
pkgdesc="A simple, standards-based, cli todo (aka: task) manager"
arch=(any)
url="https://github.com/pimutils/todoman"
license=(ISC)
depends=(python-atomicwrites python-click python-click-log python-dateutil
python-humanize python-icalendar python-parsedatetime python-pyxdg
python-urwid)
makedepends=(python-sphinx-autorun python-sphinx-click python-sphinx_rtd_theme python-setuptools-scm)
checkdepends=(python-freezegun python-hypothesis python-pytest
python-pytest-runner)
optdepends=(
  'bash-completion: auto-complete in bash'
  'python-click-repl: REPL command'
  'zsh-completions: auto-complete in zsh'
)
source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('3e4789470794baf692a3d3d51952fafc35a18de40f4ddbf705b960208e8c4f406c3ea8adbd910d1bb603ef594346d57b18d698651c159e53068ca35ace23353b')
b2sums=('dc98a8ac33181b3f5076509d39067b53edd3b7e9e41f8b47bd3e9a04b8d978115c31d37d54ea97cc5008871ae849970b33631cdcc9b2bf12106bf03c9dd2f23c')

prepare() {
  cd "$pkgname-$pkgver"
  # remove interfering options to pytest
  sed -e '/addopts/d' -i setup.cfg
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
  PYTHONPATH="$PYTHONPATH:${PWD}" make -C docs man
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  # tests require timezone to be set to UTC
  # https://github.com/pimutils/todoman/issues/404
  TZ="UTC" pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --optimize=1 --root="${pkgdir}"
  # docs
  install -vDm 644 {AUTHORS,CHANGELOG,CODE_OF_CONDUCT,README}.rst -t "${pkgdir}/usr/share/doc/${pkgname}/"
  # manpages
  install -vDm 644 docs/build/man/*.1 -t "$pkgdir/usr/share/man/man1/"
  # license
  # wrong name: https://github.com/pimutils/todoman/issues/319
  install -vDm 644 LICENCE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  # sample config
  install -vDm 644 "config.py.sample" -t "${pkgdir}/usr/share/doc/${pkgname}/examples/"
  # completions
  install -vDm 644 contrib/completion/bash/_todo "${pkgdir}/usr/share/bash-completion/completions/todo"
  install -vDm 644 contrib/completion/zsh/_todo -t "${pkgdir}/usr/share/zsh/site-functions/"
}
