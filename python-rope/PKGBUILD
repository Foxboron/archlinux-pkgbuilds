# Maintainer: Alexander F Rødseth <xyproto@archlinux.org>
# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Thomas Dziedzic <gostrc@gmail.com>
# Contributor: Nicolás de la torre <ndelatorre@gmail.com>
# Contributor: Ryan Coyner <rcoyner@gmail.com>
# Contributor: Jens Maucher <defcon@archlinux.us>

pkgname=python-rope
pkgver=0.19.0
pkgrel=1
pkgdesc='Refactoring library'
arch=('any')
url='https://github.com/python-rope/rope'
license=('GPL')
makedepends=('python' 'python-setuptools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/python-rope/rope/archive/$pkgver.tar.gz")
sha256sums=('e74e4eb623f5d0a5b3e6eea20297e71d10e3170b630fe0b8c4963d98d99a63e5')

build(){
  cd "rope-$pkgver"
  python setup.py build
}

check(){
  cd "rope-$pkgver"
  python -m unittest
}

package() {
  cd "rope-$pkgver"
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}

# vim:set ts=2 sw=2 et:
