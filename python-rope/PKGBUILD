# $Id: PKGBUILD 64151 2012-02-09 14:12:07Z arodseth $
# Maintainer: Alexander F Rødseth <xyproto@archlinux.org>
# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Thomas Dziedzic <gostrc@gmail.com>
# Contributor: Nicolás de la torre <ndelatorre@gmail.com>
# Contributor: Ryan Coyner <rcoyner@gmail.com>
# Contributor: Jens Maucher <defcon@archlinux.us>

pkgbase=python-rope
pkgname=(python-rope python2-rope)
pkgver=0.10.7
pkgrel=4
pkgdesc='Refactoring library'
arch=('any')
url='https://github.com/python-rope/rope'
license=('GPL')
makedepends=('python' 'python-setuptools'
             'python2' 'python2-setuptools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/python-rope/rope/archive/$pkgver.tar.gz")
sha256sums=('4426d16686774f2b3d0431e5e54e7e261eab967e9e50e0af7fdaf95436057d74')

prepare() {
  cp -a rope-$pkgver{,-py2}
}

build(){
  cd "$srcdir/rope-$pkgver"
  python setup.py build

  cd "$srcdir/rope-$pkgver-py2"
  python2 setup.py build
}

package_python2-rope() {
  depends=("python2")
  cd "rope-$pkgver-py2"
  python2 setup.py install --root="$pkgdir" --optimize=1 --skip-build
}

package_python-rope() {
  depends=("python")
  cd "rope-$pkgver"
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}

# vim:set ts=2 sw=2 et:
