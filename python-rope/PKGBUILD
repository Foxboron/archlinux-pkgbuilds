# Maintainer: Alexander F Rødseth <xyproto@archlinux.org>
# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Thomas Dziedzic <gostrc@gmail.com>
# Contributor: Nicolás de la torre <ndelatorre@gmail.com>
# Contributor: Ryan Coyner <rcoyner@gmail.com>
# Contributor: Jens Maucher <defcon@archlinux.us>

pkgbase=python-rope
pkgname=(python-rope python2-rope)
pkgver=0.12.0
pkgrel=1
pkgdesc='Refactoring library'
arch=('any')
url='https://github.com/python-rope/rope'
license=('GPL')
makedepends=('python' 'python-setuptools'
             'python2' 'python2-setuptools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/python-rope/rope/archive/$pkgver.tar.gz")
sha256sums=('0452b8660e982ad4fb4d181aab7150685c14fea59b2f77e000efea5f773d5d7a')

prepare() {
  cp -a rope-$pkgver{,-py2}
}

build(){
  cd "$srcdir/rope-$pkgver"
  python setup.py build

  cd "$srcdir/rope-$pkgver-py2"
  python2 setup.py build
}

package_python2-rope() {
  depends=("python2")
  cd "rope-$pkgver-py2"
  python2 setup.py install --root="$pkgdir" --optimize=1 --skip-build
}

package_python-rope() {
  depends=("python")
  cd "rope-$pkgver"
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}

# vim:set ts=2 sw=2 et:
