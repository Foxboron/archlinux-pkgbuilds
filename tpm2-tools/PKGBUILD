# Maintainer: Jonas Witschel <diabonas@archlinux.org>
# Contributor: Bruno Pagani <archange@archlinux.org>
pkgname=tpm2-tools
pkgver=5.0
pkgrel=1
pkgdesc='Trusted Platform Module 2.0 tools based on tpm2-tss'
arch=('x86_64')
url='https://github.com/tpm2-software/tpm2-tools'
license=('BSD')
depends=('curl' 'openssl' 'tpm2-tss' 'util-linux-libs' 'libtss2-esys.so' 'libtss2-fapi.so'
         'libtss2-mu.so' 'libtss2-rc.so' 'libtss2-sys.so' 'libtss2-tctildr.so')
checkdepends=('cmocka' 'expect' 'ibm-sw-tpm2' 'iproute2' 'python-yaml' 'tpm2-abrmd' 'xxd')
optdepends=('tpm2-abrmd: user space resource manager')
source=("$url/releases/download/$pkgver/$pkgname-$pkgver.tar.gz"{,.asc}
        "tpm2-tools-5.0_fix_tests_with_tpm2-tss-3.0.2.patch::$url/commit/0db78069d9a384b33fd4c412f852a85ccf0c3733.patch")
sha512sums=('ea57a28a61e28b78cae7067ff58facd8754fafab7a2689fd93f8b3374073b6ac30301a75f8ff5c654800ab469ee6604d0b8a86c310631b9545b816ecaa05968e'
            'SKIP'
            '8b73e3d568485efb96900f6a5a7e36c950a9e18a8131f7a5c46ed6031f9c177062b6e7126a6bdc6ca63f9dc8a4bc2207439b114dec11b9f52d105fc62ad6c2bc')
validpgpkeys=('5B482B8E3E19DA7C978E1D016DE2E9078E1F50C1'  # William Roberts (Bill Roberts) <william.c.roberts@intel.com>
              '6313E6DC41AAFC315A8760A414986F6944B1F72B') # Desai, Imran (idesai-github-gpg) <imran.desai@intel.com>

prepare() {
	cd "$pkgname-$pkgver"
	# Fix test failure with tpm2-tss 3.0.2 (https://github.com/tpm2-software/tpm2-tools/pull/2268)
	patch --forward --strip=1 --input="$srcdir/tpm2-tools-5.0_fix_tests_with_tpm2-tss-3.0.2.patch"
	./bootstrap
}

build() {
	cd "$pkgname-$pkgver"
	./configure --prefix=/usr $( ((CHECKFUNC)) && echo --enable-unit)
	make
}

check() {
	cd "$pkgname-$pkgver"
	make check
}

package() {
	cd "$pkgname-$pkgver"
	make DESTDIR="$pkgdir" install
	install -Dm644 doc/LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
