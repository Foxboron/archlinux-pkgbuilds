# Maintainer: Bruno Pagani <archange@archlinux.org>
# Maintainer: Morten Linderud <foxboron@archlinux.org>

pkgname=tpm2-tools
pkgver=3.2.0
pkgrel=1
pkgdesc="Trusted Platform Module 2.0 tools based on tpm2-tss"
arch=(x86_64)
url="https://github.com/tpm2-software/tpm2-tools"
license=(BSD)
depends=(curl openssl tpm2-tss)
optdepends=('tpm2-abrmd: user space resource manager')
checkdepends=(cmocka) 
source=("$url/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.gz"{,.asc})
sha256sums=('ad79ee83e2d4b34302e8883eaf313b27dbfabfd9cbc8ebcd95cf78fa097aef14' 'SKIP')
validpgpkeys=(5B482B8E3E19DA7C978E1D016DE2E9078E1F50C1  # William Roberts (Bill Roberts) <william.c.roberts@intel.com>
              D75ED7AA24E50CD645C6F457C751E590D63F3D69  # Javier Martinez Canillas <javierm@redhat.com>
              5BEC526CE3A61CAF07E7A7DA49BCAE5443FFFC34) # Joshua Lock <joshua.g.lock@intel.com>

build() {
    cd ${pkgname}-${pkgver}
    ./configure \
        --enable-unit \
        --sysconfdir=/etc \
        --prefix=/usr
        make
}

check() {
    cd ${pkgname}-${pkgver}
    make check
}

package() {
    cd ${pkgname}-${pkgver}
    make DESTDIR="${pkgdir}" install
    install -Dm644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
