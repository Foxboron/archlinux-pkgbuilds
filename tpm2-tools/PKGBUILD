# Maintainer: Bruno Pagani <archange@archlinux.org>
# Maintainer: Morten Linderud <foxboron@archlinux.org>

pkgname=tpm2-tools
pkgver=4.0
pkgrel=2
pkgdesc="Trusted Platform Module 2.0 tools based on tpm2-tss"
arch=(x86_64)
url="https://github.com/tpm2-software/tpm2-tools"
license=(BSD)
depends=(curl openssl tpm2-tss)
optdepends=('tpm2-abrmd: user space resource manager')
makedepends=(pandoc)
checkdepends=(cmocka ibm-sw-tpm2 python-yaml tpm2-abrmd xxd)
source=("$url/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.gz"{,.asc})
sha256sums=('1e2c6946072235ab903777aa7db0191ab3d87167587dd164d4375b7790e4e00a' 'SKIP')
validpgpkeys=(5B482B8E3E19DA7C978E1D016DE2E9078E1F50C1  # William Roberts (Bill Roberts) <william.c.roberts@intel.com>
              6313E6DC41AAFC315A8760A414986F6944B1F72B) # Desai, Imran (idesai-github-gpg) <imran.desai@intel.com>

build() {
    cd ${pkgname}-${pkgver}
    ./configure \
        --sysconfdir=/etc \
        --prefix=/usr $( ((CHECKFUNC)) && echo --enable-unit)
    make
}

check() {
    cd ${pkgname}-${pkgver}
    make check
}

package() {
    cd ${pkgname}-${pkgver}
    make DESTDIR="${pkgdir}" install
    install -Dm644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
