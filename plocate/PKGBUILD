# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Hans-Nikolai Viessmann <hans AT viess DOT mn>

pkgname=plocate
pkgver=1.0.6
pkgrel=1
pkgdesc="Alternative to locate, faster and compatible with mlocate's database."
arch=('x86_64' 'i686')
url="https://plocate.sesse.net"
license=('GPL2')
depends=('zstd' 'liburing' 'mlocate')
makedepends=('meson')
source=("${url}/download/${pkgname}-${pkgver}.tar.gz"
        'plocate.tmpfiles'
        'plocate-build.service')
sha256sums=('48338c46afca22fd6f57faf69b7816cb70516d9279eb1e5d9895a14c87dbd617'
            'd2a8a86dc9b4bb2bf24dbfeb302101fca9dded2fbe204892f6ee3f9a9836a7dc'
            '1aacfdfb905d78eaa207f6cf295b0993c0863d27445eb02d7236bc8075b22e7e')

build() {
    cd $pkgname-$pkgver

    arch-meson \
        -Dinstall_cron=false \
        -Dlocategroup=locate \
        ../build

    meson compile -C ../build
}

package() {
    cd $pkgname-$pkgver
    DESTDIR="$pkgdir" meson install -C ../build

    chgrp 21 "${pkgdir}/usr/bin/plocate"
    chmod 2755 "${pkgdir}/usr/bin/plocate"

    install -Dm644 ../plocate-build.service "${pkgdir}/usr/lib/systemd/system/plocate-build.service"
    install ../build/update-plocate.sh "${pkgdir}/usr/bin/update-plocate.sh"
    install -Dm644 ../plocate.tmpfiles "$pkgdir/usr/lib/tmpfiles.d/plocate.conf"
}
