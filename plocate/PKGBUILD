# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Hans-Nikolai Viessmann <hans AT viess DOT mn>

pkgname=plocate
pkgver=1.1.5
pkgrel=1
pkgdesc="Alternative to locate, faster and compatible with mlocate's database."
arch=('x86_64' 'i686')
url="https://plocate.sesse.net"
license=('GPL2')
install=plocate.install
depends=('zstd' 'liburing' 'systemd')
makedepends=('meson')
conflicts=('mlocate')
source=("${url}/download/${pkgname}-${pkgver}.tar.gz"
        'plocate.sysusers'
        'plocate.tmpfiles'
        'updatedb.conf')
sha256sums=('bef2f880f155f31bb107cce741d2a1d3c4d8fd07340c6f8642540464d880d4cd'
            '1713a8fc8b81f4a390bf8597c4c0e917474379002dcc984aad2f44218d10c82a'
            'adda399ac42f0faa002caab5bf932777c0006ae7c4387add859c9f77321b4202'
            'ea65bb68bb854803965ac16c770ba2567256e2066d68c272f1a1974ffc5c5ee7')

build() {
    cd $pkgname-$pkgver
    arch-meson \
        -Dinstall_cron=false \
        -Dinstall_systemd=true\
        -Dlocategroup=locate \
        ../build
    meson compile -C ../build
}

package() {
    cd $pkgname-$pkgver
    DESTDIR="$pkgdir" meson install -C ../build

    chgrp 21 "${pkgdir}/usr/bin/plocate"
    chmod 2755 "${pkgdir}/usr/bin/plocate"

    install -Dm644 ../plocate.tmpfiles "$pkgdir/usr/lib/tmpfiles.d/plocate.conf"
    install -Dm644 ../plocate.sysusers "$pkgdir/usr/lib/sysusers.d/plocate.conf"
    install -Dm644 ../updatedb.conf "${pkgdir}/etc/updatedb.conf"

    mv "$pkgdir/usr/share/man/man5/updatedb.conf.5" \
       "$pkgdir/usr/share/man/man5/updatedb.plocate.conf.5"
}
