# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Angel 'angvp' Velasquez <angvp[at]archlinux.com.ve>
# Contributor: William Rea <sillywilly@gmail.com>

_origname=reportlab
pkgname=python-reportlab
pkgver=3.5.42
pkgrel=3
pkgdesc="A proven industry-strength PDF generating solution"
arch=('x86_64')
url="https://bitbucket.org/rptlab/reportlab"
depends=('freetype2')
depends=('python' 'python-pip' 'python-pillow')
makedepends=('freetype2' 'python-pillow' 'mercurial')
license=('BSD')
source=(https://files.pythonhosted.org/packages/source/${_origname::1}/${_origname}/${_origname}-${pkgver}.tar.gz)
sha256sums=('9c21f202697a6cea57b9d716288fc919d99cbabeb30222eebfc7ff77eac32744')

build() {
    cd ${_origname}-${pkgver}
    python setup.py build
}

check() {
    cd ${_origname}-${pkgver}
    python setup.py tests
    rm tests/_i_am_actually_a_*
}

package() {
    cd ${_origname}-${pkgver}
    python setup.py install --root="$pkgdir" --optimize=1 --skip-build
    install -D -m644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/license.txt
}
