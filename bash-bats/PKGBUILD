# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Jan-Erik Rediger <badboy at archlinux dot us>

pkgname=bash-bats
pkgver=0.4.0
pkgrel=2
pkgdesc="Bash Automated Testing System"
arch=("any")
url="https://github.com/sstephenson/bats"
license=("MIT")
depends=('bash')
makedepends=('git')
source=("git+https://github.com/sstephenson/bats.git#tag=v$pkgver")
md5sums=('SKIP')

check() {
  cd "${srcdir}/bats"
  bin/bats --tap test
}

package() {
  cd "${srcdir}/bats"
  #./install.sh "$pkgdir/usr"

  for fn in libexec/*; do
    install -Dm755 ${fn} \
      "${pkgdir}"/usr/lib/bats/$(basename ${fn})
  done

  install -dm755 "${pkgdir}"/usr/bin
  ln -s ../lib/bats/bats "${pkgdir}"/usr/bin/bats
  install -Dm744 man/bats.1 "${pkgdir}"/usr/share/man/man1/bats.1
  install -Dm744 man/bats.7 "${pkgdir}"/usr/share/man/man7/bats.7
  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
