# Maintainer: Morten Linderud <foxboron@archlinux.org>

pkgname=bucklespring
pkgver=1.4.0
pkgrel=3
pkgdesc="Nostalgia bucklespring keyboard sound, sampled from IBM's Model-M"
arch=('i686' 'x86_64')
url="https://github.com/zevv/bucklespring"
license=('MIT')
depends=('openal' 'alure' 'libxtst' 'dumb' 'mpg123')
source=("$pkgname-$pkgver.tar.gz::https://github.com/zevv/bucklespring/archive/v${pkgver}.tar.gz")
sha256sums=('b38b2c0ec905573a243cb9db543f8eeb92e4d040025ef338d8bcfcfd5ccd5348')

build() {
  cd "$pkgname-$pkgver"
  make PATH_AUDIO=/usr/share/bucklespring
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm755 buckle "$pkgdir"/usr/bin/buckle
  install -Dm644 -t "$pkgdir"/usr/share/bucklespring/ wav/*
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
